% mmvowels.mf -- malayalam vowels, vowel signs and virama
% (c) 1993 Jeroen Hellingman
% last edit: 24-JAN-1993

beginchar(c_anusvara,12uw#+rm#,12uh#,0); "U+0D02 malayalam sign anusvara";
    pickup frame_pen;
    z1=(0, 6uh);
    eye(1,2,3,4)(12uw,12uh);
    draw_eye(1,2,3,4);
    penlabels(1,2,3,4); 
endchar;

beginchar(c_visarga,8uw#+rm#,20uh#,0); "U+0D03 malayalam sign visarga";
    pickup frame_pen;
    % first eye
    z1=(0, 4uh);
    eye(1,2,3,4)(8uw,8uh);
    draw_eye(1,2,3,4);
    % second eye
    z5=(0, 16uh);
    eye(5,6,7,8)(8uw,8uh);
    draw_eye(5,6,7,8);
    penlabels(1,2,3,4,5,6,7,8); 
endchar;

beginchar(c_a,42uw#+rm#,20uh#,0); "U+0D05 malayalam letter a";
    pickup frame_pen;
    % first bow
    z1=(4uw, 0);
    z2=(0, 10uh);
    z3=(9uw, 20uh);
    % three-like shape
    z4=(16uw, 13uh);
    z5=(13uw, 9uh);
    z6=(8uw, 9uh);
    z7=(17uw, 4uh);
    % second bow
    z8=(13uw, 0);
    z9=z6;
    z10=(17uw, 20uh);
    z11=(25uw, 10uh);
    z12=(x11, .5uh);
    % third bow
    z13=(34uw, 20uh);
    z14=(42uw, 10uh);
    z15=(37uw, 0);
    z16=(34uw, 5uh);
    z17=(40uw, 16uh);
    draw z1..z2{up}..z3{right}..z4{down}..z5{left}--z6;
    draw z6--z5{right}..z7{down}..z8{left}..z9{up}..z10{right}..z11{down}--z12;
    draw z12--z11{up}..z13{right}..z14{down}..z15{left}..z16{up}..z17;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17);
endchar;

beginchar(c_aa,49uw#+rm#,20uh#,6uh#); "U+0D06 malayalam letter aa";
    % uses definition of "malayalam letter a"
    pickup frame_pen;
    % first bow
    z1=(4uw, 0);
    z2=(0, 10uh);
    z3=(9uw, 20uh);
    % three-like shape
    z4=(16uw, 13uh);
    z5=(13uw, 9uh);
    z6=(8uw, 9uh);
    z7=(17uw, 4uh);
    % second bow
    z8=(13uw, 0);
    z9=z6;
    z10=(17uw, 20uh);
    z11=(25uw, 10uh);
    z12=(x11, .5uh);
    % third bow
    z13=(34uw, 20uh);
    z14=(42uw, 10uh);
    end_loop_tail(14,15,16,17,18,19,20);

    draw z1..z2{up}..z3{right}..z4{down}..z5{left}--z6;
    draw z6--z5{right}..z7{down}..z8{left}..z9{up}..z10{right}..z11{down}--z12;
    draw z12--z11{up}..z13{right}..z14{down}..z15{left}..z16{up}..
        z17{right}..z18{down}..z19{left}..z20;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20);
endchar;

beginchar(c_i,32uw#+rm#,20uh#,10uh#); "U+0D07 malayalam letter i";
    pickup frame_pen;
    x5=0uw;
    half_ba(1,2,3,4,5,6,7,8,9,10,11');
    z11=(23uw, -4uh);
    z12=(6uw, y11);
    z13=(2uw, -7uh);
    z14=(x12, -10uh);
    z15=(34uw, y14);
    
    draw_eye(1,2,3,4);
    draw z4{left}..z5{up}..z6{right}..z7{down}--z8;
    draw z8--z7{up}..z9{right}..z10{down}..z11---z12..z13{down}..
        z14---z15;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15);
endchar;

beginchar(c_u,22uw#+rm#,20uh#,10uh#); "U+0D09 malayalam letter u";
    pickup frame_pen;
    % eye
    z1=(1uw, 6uh);
    z2=(6uw, 12uh);
    z3=(11uw, y1);
    z4=(x2, 0);
    % bow
    z5=(0, 10uh);
    z6=(11uw, 20uh);
    z7=(22uw, 8uh);
    % tail below
    z8=(13uw, -4uh);
    z9=(5uw, y8);
    z10=(1uw, -7uh);
    z11=(x9, -10uh);
    z12=(24uw, y11);
    draw z1{up}..z2{right}..z3{down}..z4{left}..cycle;
    draw z4{left}..z5{up}..z6{right}..z7{down}..z8---z9..
        z10{down}..z11---z12;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12);
endchar;

beginchar(c_vocalic_r, 28uw#+rm#, 20uh#, 16uh#); "U+0D0B malayalam letter vocalic r";
    pickup frame_pen;
    % first eye
    z1=(1uw, 14uh);
    z2=(6uw, 8uh);
    z3=(11uw, y1);
    z4=(x2, 20uh);
    % upper bow
    z5=(0, 10uh);
    z6=(10uw, 0);
    z6'=(18uw, 0);
    z7=(28uw, 10uh);
    % second eye
    z8=(x7 - 1uw, 14uh);
    z9=(x7 - 6uw, 20uh);
    z10=(x7 - 11uw, y8);
    z11=(x9, 8uh);
    % lower bow
    z12=(0, -8uh);
    z13=(10uw, -16uh);
    z13'=(18uw, -16uh);
    z14=(28uw, -8uh);

    draw_eye(1,4,3,2);
    draw_eye(10,9,8,11);
    draw z4{left}..z5{down}..z6---z6'..z7{up}..z9{left};
    draw z12{down}..z13---z13'..z14{up}..z6'---z6..cycle;
    penlabels(1,2,3,4,5,6,6',7,8,9,10,11,12,13,13',14);
endchar;

if unknown newfont:
beginchar(c_vocalic_rr, 28uw#+rm#, 20uh#, 16uh#); "U+0D60 malayalam letter vocalic rr";
    % uses definition of "malayalam letter vocalic rr"
    pickup frame_pen;
    % first eye
    z1=(1uw, 14uh);
    z2=(6uw, 8uh);
    z3=(11uw, y1);
    z4=(x2, 20uh);
    % upper bow
    z5=(0, 10uh);
    z6=(10uw, 0);
    z6'=(18uw, 0);
    z7=(28uw, 10uh);
    % second eye
    z8=(x7 - 1uw, 14uh);
    z9=(x7 - 6uw, 20uh);
    z10=(x7 - 11uw, y8);
    z11=(x9, 8uh);
    % lower bow
    z12=(0, -8uh);
    z13=(10uw, -16uh);
    z13'=(18uw, -16uh);
    z14=(28uw, -8uh);

    draw_eye(1,4,3,2);
    draw_eye(10,9,8,11);
    draw z4{left}..z5{down}..z6---z6'..z7{up}..z9{left};
    draw z12{down}..z13---z13'..z14{up}..z6'---z6..cycle;

    % inner m-like shape; different from "malayalam letter vocalic r"
    % first bow
    z15=(8uw, -11uh);
    z16=(7uw, -8uh);
    z17=(10.5uw, -4uh);
    z18=(14uw, -9uh);
    z19=(x18, y15);
    % second bow
    z20=(28uw - 10.5uw, y17);
    z21=(28uw - 7uw, y16);
    z22=(28uw - 8uw, y15);
    pickup sub_pen;
    draw z15..z16{up}..z17{right}..z18{down}---z19;
    draw z19---z18{up}..z20{right}..z21{down}..z22;
    
    penlabels(1,2,3,4,5,6,6',7,8,9,10,11,12,13,13',14,15,16,17,18,19,20,21,22);
endchar;
fi;

if unknown newfont:
beginchar(c_vocalic_l,34uw#+rm#,20uh#,0); "U+0D0C malayalam letter vocalic l";
    pickup frame_pen;
    x5=0uw;
    half_ba(1,2,3,4,5,6,7,8,9',10',11');
    z9=(26uw, 20uh);
    z10=(34uw, 10uh);
    end_loop(10,11,12,13);
    
    draw z1{up}..z2{right}..z3{down}..z4{left}..cycle;
    draw z4{left}..z5{up}..z6{right}..z7{down}--z8;
    draw z8--z7{up}..z9{right}..z10{down}..z11{left}..z12{up}..z13;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13);
endchar;
fi;

if unknown newfont:
beginchar(c_vocalic_ll,41uw#+rm#,20uh#,6uh#); "U+0D61 malayalam letter vocalic ll";
    pickup frame_pen;
    x5=0uw;
    half_ba(1,2,3,4,5,6,7,8,9',10',11');
    z9=(26uw, 20uh);
    z10=(34uw, 10uh);
    end_loop_tail(10,11,12,13,14,15,16);

    draw_eye(1,2,3,4);
    draw z4{left}..z5{up}..z6{right}..z7{down}--z8; 
    draw z8--z7{up}..z9{right}..z10{down}..z11{left}..z12{up}..
        z13{right}..z14{down}..z15{left}..z16;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
endchar;
fi;

beginchar(c_e,42uw#+rm#,20uh#,10uh#); "U+0D0E malayalam letter e";
    pickup frame_pen;
    % first bow
    z1=(4uw, 0);
    z2=(0, 7uh);
    z3=(7uw, 14uh);
    z4=(14uw, y2);
    z5=(10uw, 0);
    % lying bar
    z5'=(13uw, 0.5uh);
    z6=(31uw, y5');
    % standing bar
    z7=(x6, 16uh);
    z8=(27uw, -10uh);
    % second bow
    z9=(21uw, 5uh);
    z10=(32uw, 20uh);
    z11=(42uw, y9);
    z12=(34uw, y8);
    
    draw z1..z2{up}..z3{right}..z4{down}..z5;
    draw z5..z5'---z6;
    draw z7---z6..z8{left}..z9{up}..z10{right}..z11{down}..{left}z12;
    penlabels(1,2,3,4,5,5',6,7,8,9,10,11,12);
endchar;

beginchar(c_ee,42uw#+rm#,20uh#,10uh#); "U+0D0F malayalam letter ee";
    % uses definition of "malayalam letter e"
    pickup frame_pen;
    % first bow
    z1=(4uw, 0);
    z2=(0, 7uh);
    z3=(7uw, 14uh);
    z4=(14uw, y2);
    z5=(10uw, 0);
    % lying bar
    z5'=(13uw, 0.5uh);
    z6=(31uw, y5');
    % standing bar
    z7=(x6, 16uh);
    z8=(27uw, -10uh);
    % second bow
    z9=(21uw, 5uh);
    z10=(32uw, 20uh);
    % differs from "malayalam letter e"
    z11=(42uw, 10uh);
    % lower bow of three-like shape
    z12=(34uw, y5');
    z13=(42uw, -5uh);
    z14=(x12, -10uh);
    
    draw z1..z2{up}..z3{right}..z4{down}..z5;
    draw z5..z5'---z6;
    draw z7---z6..z8{left}..z9{up}..z10{right}..z11{down}..{left}z12;
    draw z12{right}..z13{down}..{left}z14;
    penlabels(1,2,3,4,5,5',6,7,8,9,10,11,12,13,14);
endchar;

beginchar(c_o,22uw#+rm#,20uh#,0); "U+0D12 malayalam letter o";
    pickup frame_pen;
    front_eye(1,2,3,4,5);
    % first bow
    z5=(0, 10uh);
    z6=(11uw, 20uh);
    z7=(19uw, 14uh);
    z8=(15uw, 10uh);
    z9=(22uw, 5uh);
    z10=(16uw, 0);
    z11=(14uw, 0.5uh);
    
    draw_eye(1,2,3,4);
    draw z4{left}..z5{up}..z6{right}..z7{down}..{left}z8;
    draw z8{right}..z9{down}..z10{left}..z11;
    penlabels(1,2,3,4,5,6,7,8,9,10,11);
endchar;

%%%%%% vowel signs

beginchar(c_sign_aa,14uw#+rm#,20uh#,0); "U+0D3E malayalam vowel sign aa";
    pickup frame_pen;
    z1=(0, 15uh);
    z2=(7uw, 20uh);
    z3=(14uw, 10uh);
    z4=(x2, 0);
    z5=(0, 5uh);
    draw z1..z2{right}..z3{down}..z4{left}..z5;
    penlabels(1,2,3,4,5);
endchar;

beginchar(c_sign_i,4uw#+rm#,30uh#,0); "U+0D3F malayalam vowel sign i";
    pickup frame_pen;
    z1=(-10uw, 25uh);
    z2=(-3uw, 30uh);
    z3=(4uw, 20uh);
    z4=(4uw, 0);
    draw z1{up}..z2{right}..z3{down}---z4;
    penlabels(1,2,3,4);
endchar;

beginchar(c_sign_ii,8uw#+rm#,30uh#,0); "U+0D40 malayalam vowel sign ii";
    pickup frame_pen;
    z1=(-6uw, 25uh);
    z2=(1uw, 30uh);
    z3=(8uw, 20uh);
    z4=(8uw, 0);
    eye(1,5,6,7)(7uw,7uh);
    draw_eye(1,5,6,7);
    draw z1{up}..z2{right}..z3{down}---z4;
    penlabels(1,2,3,4,5,6,7);
endchar;

if unknown newfont:
beginchar(c_sign_u,8uw#+rm#,0,9uh#); "U+0D41 malayalam vowel sign u";
    pickup sub_pen;
    % eye (diameter = 8)
    z1=(0, -5uh);
    eye(1,2,3,4)(8uw,8uh);
    draw_eye(1,2,3,4);
    penlabels(1,2,3,4);
endchar;
fi;

if unknown newfont:
beginchar(c_sign_uu,10uw#+rm#,0,11uh#); "U+0D42 malayalam vowel sign uu";
    pickup sub_pen;
    z1=(0, -6uh);
    eye(1,2,3,4)(10uw, 10uh);
    eye(5,2,6,7)(6uw,6uh);
    draw_eye(1,2,3,4);
    draw_eye(5,2,6,7);
    penlabels(1,2,3,4,5,6,7);
endchar;
fi;

beginchar(c_ref_sign_u,8uw#+rm#,20uh#,8uh#); "reformed script vowel sign u";
    pickup frame_pen;
    z1=(0uw, 20uh);
    z2=(5uw, 12uh);
    z3=(4uw, 0);
    eye(4,3,5,6)(8uw, 8uh);

    draw z1..z2{down}..z3{down};
    draw_eye(4,3,5,6);
    penlabels(1,2,3,4,6);
endchar;

beginchar(c_ref_sign_uu,9uw#+rm#,20uh#,9uh#); "reformed script vowel sign uu";
    pickup frame_pen;
    z1=(1uw, 20uh);
    z2=(6uw, 12uh);
    z3=(5uw, 0);
    eye(4',3,7,8)(9uw, 9uh);
    z4=z7-(.45uw, -2.25uh);
    z6=z4'+(1.8uw, 2.25uh);
    z5=(.5[x6,x4], y3 - 4.5uh);
    z9=z4'-(0, 1.35uh);
    
    draw z1..z2{down}..z3{down};
    draw z4..z5{left}..z6{up}..z3{right}..z7{down}..z8{left}..z9;
    penlabels(1,2,3,4,5,6,7,8,9);
endchar;

if unknown newfont:
beginchar(c_sign_vocalic_r,20uw#+rm#,0,11uh#); "U+0D43 malayalam vowel sign vocalic r";
    pickup frame_pen;
    z1=(20uw, 0.5uh);
    vowel_sign_vocalic_r(1,2,3,4,5);
    draw z1{down}..z2{left}..z3{up}..z4{right}..z5;
    penlabels(1,2,3,4,5);
endchar;
fi;

beginchar(c_ref_sign_vocalic_r,12uw#+rm#,20uh#,10uh#); "reformed script vowel sign vocalic r";
    pickup frame_pen;
    z1=(8uw, 19.5uh);
    z2=(7uw, 12uh);
    z3=(12uw, -4uh);
    z8=(.5[x3,x5], -10uh);
    y6=0;
    x5=0;
    eye(5,6,7,4)(7uw, 7uh);
    draw z1..z2..z3{down}..z8{left}..z5{up};
    draw_eye(5,6,7,4);
    penlabels(1,2,3,4,5,6,7);
endchar;

if unknown newfont:
beginchar(c_sign_vocalic_rr,20uw#+rm#,0,12uh#); "U+0D44 malayalam vowel sign vocalic rr";
    pickup frame_pen;
    z1=(20uw, 0);
    z2=(x1-12uw, -12uh);
    z3=(x1-20uw, -8uh);
    z4=(x2-4uw, -5uh);
    z5=(x2, y3-2uh);
    z6=(x2+4uw, y4);
    z7=(x1-4uw, -7uh);
    draw z1{down}..z2{left}..z3{up}..z4{right}..z5{down}---z2;
    draw z2---z5{up}..z6{right}..z7;
    penlabels(1,2,3,4,5,6,7);
endchar;
fi;

beginchar(c_sign_e,2.0u#+rm#,ht#,0); "U+0D46 malayalam vowel sign e";
    wd := 2.0u;
    pickup frame_pen;
    % from "malayalam letter i"
    % eye
    z1=(1/20wd, 6/20h);
    z2=(6/20wd, 12/20h);
    z3=(11/20wd, y1);
    z4=(x2, 0);
    % bow
    z5=(0, 1/2h);
    z6=(10/20wd, h);
    % differs from "malayalam letter i"
    z7=(wd, 1/2h);
    z8=(16/20wd, 0);
    draw z1{up}..z2{right}..z3{down}..z4{left}..cycle;
    draw z4{left}..z5{up}..z6{right}..z7{down}..z8;
    penlabels(1,2,3,4,5,6,7,8);
endchar;

beginchar(c_sign_ee,1.6u#+rm#,ht#,0); "U+0D47 malayalam vowel sign ee";
    wd := 1.6u;
    pickup frame_pen;
    % upper eye (diameter = 6)
    z1=(13/16wd, 19/20h);
    z2=(10/16wd, 16/20h);
    z3=(x1, 13/20h);
    z4=(wd, y2);
    % bow
    z5=(9/16wd, h);
    z6=(0, 1/2h);
    z7=(x5, 0);
    % lower eye (diameter = 6)
    z8=(x1, 1/20h);
    z9=(x4, 4/20h);
    z10=(x1, 7/20h);
    z11=(x2, y9);
    draw z2{up}..z1{right}..z4{down}..z3{left}..cycle;
    draw z4{up}..z5{left}..z6{down}..z7{right}..{up}z9;
    draw z11{up}..z10{right}..z9{down}..z8{left}..cycle;
    penlabels(1,2,3,4,5,6,7,8,9,10,11);
endchar;

beginchar(c_au_length_mark,2.0u#+rm#,ht#,0); "U+0D57 malayalam au length mark";
    wd := 2.0u;
    pickup frame_pen;
    % first bow
    z1=(2/20wd, 8/20h);
    z2=(0, 14/20h);
    z3=(5/20wd, h);
    z4=(10/20wd, 13/20h);
    z5=(x4, 8/20h);
    % second bow
    z6=(15/20wd, h);
    z7=(wd, 11/20h);
    z8=(10/20wd, 0);
    draw z1..z2{up}..z3{right}..z4{down}---z5;
    draw z5---z4{up}..z6{right}..z7{down}..z8{left};
    penlabels(1,2,3,4,5,6,7,8);
endchar;

beginchar(c_virama,10uw#+rm#,26uh#,0); "U+0D4D malayalam sign virama";
    pickup frame_pen;
    z2=(0, 26uh);
    z3=(.5[x2,x4], 21uh);
    z4=(10uw, y2);
    draw z2{down}..z3{right}..z4{up};
    penlabels(2,3,4);
endchar;

%%%%%% end of mmvowels.mf
