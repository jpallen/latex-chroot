% mmcillu.mf -- malayalam cillu letters
% (c) 1993 Jeroen Hellingman
% last edit: 24-JAN-1993

beginchar(c_nn_virama,50uw#+rm#,28uh#,0); "nn+virama";
    pickup frame_pen;
    % from "malayalam letter nga"
    % first bow
    front_eye(1,2,3,4,5);
    z5=(0, 10uh);
    z6=(9uw, 20uh);
    z7=(17uw, 10uh);
    z8=(x7, 0);
    % second bow
    z9=(25uw, 20uh);
    z10=(33uw, 10uh);
    z11=(x10, 0);
    % third bow
    z12=(41uw, 20uh);
    z13=(50uw, 10uh);
    virama_tail(13,14,15,16,17,18,19);
    draw_eye(1,2,3,4);
    draw z4{left}..z5{up}..z6{right}..z7{down}---z8;
    draw z8---z7{up}..z9{right}..z10{down}---z11;
    draw z11---z10{up}..z12{right}..z13{down}..z14{left}..
        z15{up}..z16..z17{up}..z18{left}..z19;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19);
endchar;

beginchar(c_n_virama,32uw#+rm#,28uh#,0); "n+virama";
    pickup frame_pen;
	x2=0;
    letter_na(1,2,3,4,5,6,7,8');
    virama_tail(7,8,9,10,11,12,13);
    draw z1..z2{up}..z3{right}..{down}z4---z5;
    draw z5---z4{up}..z6{right}..z7{down}..z8{left}..z9{up}..
        z10..z11{up}..z12{left}..z13;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13);
endchar;

beginchar(c_r_virama,20uw#+rm#,28uh#,0); "r+virama";
    pickup frame_pen;
    z1=(4uw, 0);
    z2=(0, 10uh);
    z3=(10uw, 20uh);
    z4=(20uw, 10uh);
    virama_tail(4,5,6,7,8,9,10);
    draw z1..z2{up}..z3{right}..z4{down}..z5{left}..z6{up}..z7..
        z8{up}..z9{left}..z10;
    penlabels(1,2,3,4,5,6,7,8,9,10);
endchar;

beginchar(c_l_virama,34uw#+rm#,28uh#,0); "l+virama";
    pickup frame_pen;
    x2=0;
	letter_ta(1,2,3,4,5,6,7',8',9');
	z7=(.5[x6, x8], 20uh);
	z8=(x8'+4uw, 8uh);
    virama_tail(8,9,10,11,12,13,14);
    draw z1..z2{up}..z3{right}..z4{down}..z5{left}..z6{up}..
        z7{right}..z8{down}..z9{left}..z10{up}..z11..z12{up}..
        z13{left}..z14;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14);
endchar;

beginchar(c_ll_virama,28uw#+rm#,20uh#,0); "ll+virama";
    pickup frame_pen;
    z1=(3uw, 10uh); % not used
    z2=(0, 8uh);
    z3=(6uw, 0);
    z4=(13uw, 6uh);
    z5=(22uw, 10uh);
    z6=(28uw, 5uh);
    z7=(x5, 0);
    z8=(5uw, 20uh);
    draw z2{down}..z3{right}..z5{right}..z6{down}..z7{left}..z4..z8;
    penlabels(1,2,3,4,5,6,7,8);
endchar;

beginchar(c_var_ll_virama,34uw#+rm#,28uh#,0); "variant ll+virama";
    pickup frame_pen;
    x2=0uw;
    letter_ga(1,2,3,4,5',6',7');
    z5=(.5[x4, x6], 20uh);
    z6=(x6'+2uw, 8uh);
    virama_tail(6,7,8,9,10,11,12);
    
    draw z1..z2{down}..z3{right}..z4{up}..z5{right}..z6{down}..z7{left}..
    	z8{up}..z9..z10{up}..z11{left}..z12;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12);
endchar;

beginchar(c_k_virama,36uw#+rm#,28uh#,0); "k+virama";
    pickup frame_pen;
    x6 = 0;
    letter_ka(1,2,3,4,4',5,6,7,8',9',10',11');
    z8=z8'+(4uw, 0);
    z9=z9'+(4uw, 0);
    virama_tail(9,10,11,12,13,14,15);
    
    draw_eye(1,2,3,4);
    draw z4'..z5{left}..z6{up}..z7{right}---z8{right}..z9{down}..
        z10{left}..z11{up}..z12..z13{up}..z14{left}..z15;
    penlabels(1,2,3,4,4',5,6,7,8,9,10,11,12,13,14,15); 
endchar;

beginchar(c_y_virama,32uw#+rm#,28uh#,0); "y+virama";
    pickup frame_pen;
    x5=0uw;
    letter_ya(1,2,3,4,5,6,7,8,9,10);
    z11=(x10-3uw, 24uh); 
    z12=(x10+1uw, 28uh);
    z13=(x10+6uw, 24uh);
    draw z1..z2{right}..z3{down}..z4{left}..z5{up}..z6{right}..
       z7{(1,-8)}..z8{right}..z9{up}..z10..z11{up}..z12{right}..z13;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13);
endchar;

%%%%% end of cillu.mf

