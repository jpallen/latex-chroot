% mmnums.mf -- malayalam numerals
% (c) 1993 Jeroen Hellingman
% last edit: 24-JAN-1993

beginchar(c_mal_quarter,2uw#+rm#,20uh#,8uh#); "malayalam sign for one quarter";
    pickup frame_pen;
    z1=(0uw, 19.5uh);
    z2=(x1, -8uh);
    z3=(3uw, -8uh);

    draw z1--z2 & z2--z3;
    penlabels(1,2,3); 
endchar;

% Alternatively, Tha can be used for zero

beginchar(c_mal_zero,32uw#+rm#,20uh#,0); "U+0D66 malayalam digit zero";
    pickup frame_pen;
    z1=(1uw, 6.5uh);
    eye(1,2,3,4)(13uw,13uh);
    z5=(0, 9uh);
    z6=(11uw, 19.5uh);
    z7=(32uw, y6);

    draw_eye(1,2,3,4);
    draw z4{left}..z5{up}..z6{right}---z7;
    penlabels(1,2,3,4,5,6,7); 
endchar;

beginchar(c_mal_one,32uw#+rm#,20uh#,8uh#); "U+0D67 malayalam digit one";	
    pickup frame_pen;
    z1=(8uw, -7.5uh);
    z2=(5uw, y1);
    z3=(0, -3.5uh);
    z4=(x2, .5uh); 
    z5=(32uw, y4);
    end_tha_bow(5,6,7,8,9);

	draw z1---z2{left}..z3{up}..z4{right}---z5;
	draw z5---z6{up}..z7{left}..z8{down}---z9;
    penlabels(1,2,3,4,5,6,7,8,9);
endchar;

beginchar(c_mal_two,34uw#+rm#,20uh#,0); "U+0D68 malayalam digit two";	
    pickup frame_pen;
	x2=0;
	large_bow(1,2,3,4,5);
	z6=(x5+3uw, 0.5uh);
	z7=(34uw, y6);
	
	draw_bow(1,2,3,4,5);
	draw z5..z6---z7;
    penlabels(1,2,3,4,5,6,7);
endchar;

beginchar(c_mal_three,46uw#+rm#,20uh#,0); "U+0D69 malayalam digit three";	
    pickup frame_pen;
	x2=0;
	letter_na(1,2,3,4,5,6,7,8);
	z9=(x8+3uw, 0.5uh);
	z10=(46uw, y9);
	
	draw_letter_na(1,2,3,4,5,6,7,8);
	draw z8..z9---z10;
    penlabels(1,2,3,4,5,6,7,8,9,10);
endchar;

beginchar(c_mal_four,26uw#+rm#,28uh#,0); "U+0D6A malayalam digit four";	
    pickup frame_pen;
	x2=0;
	letter_ra(1,2,3,4,5,6',7');
	z6 = (11uw, y6');
	z7 = (26uw, 24uh);
	z8 = (x7-6uw, 28uh);
	
	draw z1..z2{up}..z3{right}..z4{down}..z5{left}..z6{up}..
		z7{up}..z8;
    penlabels(1,2,3,4,5,6,7,8);
endchar;

beginchar(c_mal_five,28uw#+rm#,24uh#,6uh#); "U+0D6B malayalam digit five";	
    pickup frame_pen;
    x2=6uw;

    z1=(x2+3uw, 0uh);
    z2=(x2, 9uh);
    z3=(x2+8.5uw, 18uh);
    z4=(x2+15uw, 13uh);
    z5=(x2+8uw, 8uh);
    z6=(x2+6uw, y5);
    z7=(x2+16uw, .5[y6,y8]);
    z8=(x5, -6uh);
    z9=(12uw, -6uh);
    z10=(0, 10uh);
    z11=(14uw, 24uh);
    z12=(28uw, 10uh);
    z13=(26uw, -2uh);

    draw z1..z2{up}..z3{right}..z4{down}..{left}z5---z6;
    draw z6---z5{right}..z7{down}..z8{left}..z9{left}..z10{up}..z11{right}..
    	z12{down}..z13;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13);
endchar;

beginchar(c_mal_six,48uw#+rm#,20uh#,8uh#); "U+0D6C malayalam digit six";	
    pickup frame_pen;
    x2=0;
    letter_n_na(1',2',3,4',5',6,7',8',9,10',11');
    z1 = z1'+(0, 2uh);
    y2 = y4 = y7 = y2' + 1uh;
    x2 = x2'; x4 = x4'; x7 = x7';
    y5 = y8 = y5' + 2uh;
    x5 = x5'; x8 = x8';
    
    z10=(x10', 11uh);
    z11=(x10', 8uh);
    z12=(32uw, -8uh);
    z13=(30uw, y12);
    
    draw z1..z2{up}..z3{right}..z4{down}---z5;
    draw z5---z4{up}..z6{right}..z7{down}---z8;
    draw z8---z7{up}..z9{right}..z10---z11..z12---z13;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13);
endchar;

beginchar(c_mal_seven,30uw#+rm#,20uh#,0); "U+0D6D malayalam digit seven";	
    pickup frame_pen;
	x2=14uw;
	y4=0uh;
	eye(1,2,3,4)(12uw,12uh);
	z5=(10uw, 0);
	z6=(0, 10uh);
	z7=(12uw, 20uh);
	z8=(30uw, 0);

	draw_eye(1,2,3,4);
	draw z4..z5{left}..z6{up}..z7{right}..z8{down};
    penlabels(1,2,3,4,5,6,7,8);
endchar;

beginchar(c_mal_eight,34uw#+rm#,20uh#,10uh#); "U+0D6E malayalam digit eight";	
    pickup frame_pen;
	x2=0;
	large_bow(1,2,3,4,5);
	z6=(x5+3uw, 0.5uh);
	z7=(34uw, y6);

	z8=(x7, 19.5uh);
	z9=(x7-10uw, -10uh);
	z10=(x5-2uw, y9);
	
	draw_bow(1,2,3,4,5);
	draw z5..z6---z7;
	draw z8---z7..z9---z10;
    penlabels(1,2,3,4,5,6,7,8,9,10);
endchar;

beginchar(c_mal_nine,32uw#+rm#,28uh#,0); "U+0D6F malayalam digit nine";	
    pickup frame_pen;
	x2=0;
    letter_na(1,2,3,4,5,6,7,8');
    virama_tail(7,8,9,10,11,12,13);
    draw z1..z2{up}..z3{right}..{down}z4---z5;
    draw z5---z4{up}..z6{right}..z7{down}..z8{left}..z9{up}..
        z10..z11{up}..z12{left}..z13;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13);
endchar;
