% mmrconj.mf -- Malayalam ra-conjuncts
% (c) 1993 Jeroen Hellingman
% last edit: 24-JAN-1993

beginchar(c_k_ra,40uw#+rm#,20uh#,8uh#); "k+ra";
    pickup frame_pen;
    x6=8uw;
    letter_ka(1,2,3,4,4',5,6,7,8,9,10',11');
    subscribed_ra(9,10,11,12,13)(8uh);

    draw z1{up}..z2{right}..z3{down}..z4{left}..cycle;
    draw z4'..z5{left}..z6{up}..z7{right}---z8{right}..z9{down}..
        z10..z11..z12{up}..z13;
    penlabels(1,2,3,4,4',5,6,7,8,9,10,11,12,13);
endchar;

beginchar(c_k_k_ra,55uw#+rm#,20uh#,10uh#); "k+k+ra";
    pickup frame_pen;
    x6=8uw;
    letter_k_ka(1,2,3,4,4',5,6,7,8,9,10,11,12,13',14');
    z13 = (x13', 8uh);
    subscribed_ra(13,14,15,16,17)(10uh);

    draw_eye(1,2,3,4);
    draw z4'..z5{left}..z6{up}..z7{right}---z8{right}..z9{down}..
        z10{left}..z11{up}..z12{right}..z13{down}..z14..z15..z16{up}..z17;
    penlabels(1,2,3,4,4',5,6,7,8,9,10,11,12,13,14,15,16,17);
endchar;

beginchar(c_g_ra,40uw#+rm#,20uh#,8uh#); "g+ra";
    pickup frame_pen;
    x2=8uw;
    letter_ga(1,2,3,4,5,6',7');
    z6=(x6', 8uh);
    subscribed_ra(6,7,8,9,10)(8uh);

    draw z1..z2{down}..z3{right}..z4{up}..z5{right}..z6{down}..
        z7..z8..z9{up}..z10;
    penlabels(1,2,3,4,5,6,7,8,9,10);
endchar;

beginchar(c_ch_ra,61uw#+rm#,20uh#,10uh#); "ch+ra";
    pickup frame_pen;
    x6=8uw;
    letter_cha(1,2,3,4,5,6,7,8,9,10,11,12',13',14');
    end_loop_tail(11,12,13,14,15,16',17');
    subscribed_ra(15,16,17,18,19)(10uh);

    draw z1..z2{up}..z3{right}..z4{down}..z5{left};
    draw z6--z7..z8;
    draw z8..z9{up}..z10{right}..z11{down}..z12{left}..z13{up}..
        z14{right}..z15{down}..z16..z17..z18{up}..z19;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19);
endchar;

beginchar(c_j_ra,40uw#+rm#,20uh#,16uh#); "j+ra";
    pickup frame_pen;
    x1=8uw;
    letter_ja(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18);    
    % tail for ra
    z16'=(x1+26uw, 2uh);
    z19=(x1+32uw, -4.5uh);
    z20=(24uw, -16uh);
    z21=(0, 5uh);
    z22=(3uw, 20uh);

    draw z1..z5{up}..z6{right}..{down}z7---z8;
    draw_eye(1,2,3,4);
    draw z8---z7{up}..z9{right}..z10{down}..z11..z12{down}..
        z13{right}..z14..z16'{right}..z19{down}..z20{left}..
        z21{up}..z22;
    draw_eye(15,16,17,18);
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,16',17,18,19,20,21,22);
endchar;

beginchar(c_tt_ra,18uw#+6uw#+rm#,20uh#,0uh#); "tt+ra";
    pickup frame_pen;
    x3=6uw;
    letter_tta(1,2,3,4,5,6',7');
    z6=(15uw, 0);
    z7=(12uw, 0);
    z8=(0, 12uh);
    z9=(2uw, 19.5uh);
    
    draw z1..z2{left}..z3{down}..z4{4,1}..z5{down}..z6..z7..z8{up}..z9;
    penlabels(1,2,3,4,5,6,7,8,9);
endchar;

beginchar(c_t_t_ra,64uw#+rm#,ht#,10uh#); "t+t+ra";
    pickup frame_pen;
	x2=8uw;
	letter_ta(1,2,3,4,5,6,7',8',9');
	z7=z7'-(1uw,0);
	z8=z8'-(2uw,0);
    z9=(x8,0.5uh);
    x9a = x8-2uw;
	letter_ta(8a,9a,10',11,12,13,14,15',16');
	z10=z10' + (1uw, 0);
	z15=(x15',8uh);
    subscribed_ra(15,16,17,18,19)(10uh);
    
    draw z1..z2{up}..z3{right}..z4{down}..z5{left}..z6{up}..
        z7{right}..z8{down}---z9;
    draw z9---z8{up}..z10{right}..z11{down}..z12{left}..z13{up}..
        z14{right}..z15{down}..z16..z17..z18{up}..z19;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19);
endchar;

beginchar(c_t_ra,38uw#+rm#,ht#,8uh#); "t+ra";
    pickup frame_pen;
    x2=8uw;
    letter_ta(1,2,3,4,5,6,7,8',9');
    z8=(x8', 8uh);
    z9=(19uw, -8uh);
    z10=(0, 10uh);
    z11=(3.5uw, 20uh);

    draw z1..z2{up}..z3{right}..z4{down}..z5{left}..z6{up}..
        z7{right}..z8{down}..z9{left}..z10{up}..z11;
    penlabels(1,2,3,4,5,6,7,8,9,10,11);
endchar;

beginchar(c_d_ra,18uw#+8uw#+rm#,20uh#,6uh#); "d+ra";
    pickup frame_pen;
    x2=8uw;
    letter_da(1',2,3,4,5,6,7',8',9');
    z1=z1'+(1.5uw, 0);
    z7=z7'-(0, 2uh);
    % ra around letter
    z8=(17uw, -6uh);
    z9=(12uw, -6uh);
    z10=(0, 10uh);
    z11=(3.5uw, 20uh);
    draw z1..z2{up}..z3{right}..z4{down}..{left}z5---z6;
    draw z6---z5{right}..z7{down}..z8..z9..z10{up}..z11;
    penlabels(1,2,3,4,5,6,7,8,9,10,11);
endchar;

beginchar(c_dh_ra,40uw#+rm#,20uh#,6uh#); "dh+ra";
    pickup frame_pen;
    x2=8uw;
    letter_dha(1,2,3,4,5,6,7,8);
    subscribed_ra(7,9,10,11,12)(6uh);

    draw z1..z2{down}..z3{right}..{up}z4---z5;
    draw z5---z4{down}..z6{right}..z7{up}..z8;
    draw z7..z9..z10..z11{up}..z12;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12);
endchar;

beginchar(c_n_t_ra,52uw#+rm#,20uh#,10uh#); "n+t+ra";
    pickup frame_pen;
    x2=8uw;
    letter_na(1,2,3,4,5,6a,7',8');
    z6=(.5[x6a,x6b], y6a);
    x5'=x5-2uw;
    letter_ta(4',5',6b,7,8,9,10,11',12');
    z11=(x11', 8uh);
    subscribed_ra(11,12,13,14,15)(10uh);
    
    draw z1..z2{up}..z3{right}..z4{down}---z5;
    draw z4{up}..z6{right}..z7{down}..z8{left}..z9{up}..z10{right}..
        z11{down}..z12..z13..z14{up}..z15;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15);
endchar;

beginchar(c_n_d_ra,35uw#+6uw#+rm#,20uh#,8uh#); "n+d+ra";
    pickup frame_pen;
    x2=8uw;
    letter_na(1,2,3,4,5,6,7a,8a);
    letter_da(4',5',6,7',8',9',10',11',12');
    %
    z7=z7'-(0,.5uh);
    z8=z8'-(0,1uh);
    z9=z9'-(0,1uh);
    z10=(x10', 3uh);
    z11=(.5[x10,x12], -8uh);
    z12=(0, 10uh);
    z13=(3.5uw, 20uh);
    
    draw z1..z2{up}..z3{right}..z4{down}---z5;
    draw z4{up}..z6{right}..z7{down}..z8{left}---z9;
    draw z8{right}..z10{down}..z11{left}..z12{up}..z13;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13);
endchar;

beginchar(c_n_ra,40uw#+rm#,20uh#,8uh#); "n+ra";
    pickup frame_pen;
    x2=8uw;
    letter_na(1,2,3,4,5,6,7',8');
    z7=(x7', 8uh);
    subscribed_ra(7,8,9,10,11)(8uh);
    
    draw z1..z2{up}..z3{right}..{down}z4---z5;
    draw z5---z4{up}..z6{right}..z7{down}..z8..z9..z10{up}..z11;
    penlabels(1,2,3,4,5,6,7,8,9,10,11);
endchar;

beginchar(c_bh_ra,22uw#+6uw#+rm#,20uh#,4uh#); "bh+ra";
    pickup frame_pen;
    x2=8uw;
    letter_bha(1',2,3,4,5,6,7,8,9,10',11',12');
    z1=z1'+(1.5uw, 2uw);
    z10=(x10', 2uh);
    % ra around letter
    z11=(18uw, -4uh);
    z12=(12uw, -4uh);
    z13=(0, 10uh);
    z14=(3.5uw, 19.5uh);
    
    draw z1..z2{up}..z3{right}..z4{down}..z5..z6..z7{down}..
        z8..z9..z10{down}..z11..z12..z13{up}..z14;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14);
endchar;

beginchar(c_sh_ra,47uw#+rm#,ht#,8uh#); "sh+ra";
    pickup frame_pen;
    x2=8uw;
    letter_ga(1,2,3,4,5,6,7');
    end_loop_tail(6,7,8,9,10,11',12');
	subscribed_ra(10,11,12,13,14)(8uh);
    
    draw z1..z2{down}..z3{right}..z4{up}..z5{right}..z6{down}..z7{left}..
        z8{up}..z9{right}..z10{down}..z11..z12..z13{up}..z14;
    penlabels(1,2,3,4,5,6,7,8,9,11,12,13,14);
endchar;

beginchar(c_s_ra,52uw#+rm#,20uh#,8uh#); "s+ra";
    pickup frame_pen;
    x2=8uw;
    letter_sa(1,2,3,4,5,6,7,8,9,10);
 	subscribed_ra(9,11,12,13,14)(8uh);

    draw z1..z2{up}..z3{right}..{down}z4---z5;
    draw z5---z4{up}..z6{right}..z7{down}..z8{right}..z9{up}..z10;
    draw z9{down}..z11..z12..z13{up}..z14;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14);
endchar;

beginchar(c_h_ra,42uw#+8uw#+rm#,20uh#,10uh#); "h+ra";
    pickup frame_pen;
    % bow
	x2=8uw;
    letter_ha(1,2,3,4,5,6,7,8,9,10,11',12');
    z11=(x11', 8uh);
	subscribed_ra(11,12,13,14,15)(10uh);
    
    draw_bow(1,2,3,4,5);
    draw z5..z6---z7..z8;
    draw z8..z9{up}..z10{right}..z11{down}..z12..z13..z14{up}..z15;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15);
endchar;

%%%%%% end of mmconj.mf
