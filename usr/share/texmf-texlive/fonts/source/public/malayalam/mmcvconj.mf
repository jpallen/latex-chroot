% mmcvconj.mf -- malayalam consonant-vowel conjuncts
% (c) 1993 Jeroen Hellingman
% last edit: 24-JAN-1993

%%%%% ka related glyphs

if unknown subfont:
beginchar(c_ku,30uw#+rm#,20uh#,0); "ku";
    pickup frame_pen;
    x6 = 0;
    letter_ka(1,2,3,4,4',5,6,7,8',9',10',11');
    % three-like shape for "u"
    z8=(x10, y1);
    z9=(30uw, y10+2.75uw);
    z10=(x9 - 3.5uw, y6);
    z11=(x10 - .5uw, y6);
    z12=(x9, y10-2.75uw);
    z13=(x10, 0);
    z14=(x11, 0);

    draw_eye(1,2,3,4);
    draw z4'..z5{left}..z6{up}..z7{right}---z8{right}..z9{down}..
        z10{left}--z11;
    draw z11--z10{right}..z12{down}..z13{left}..z14;
    penlabels(1,2,3,4,4',5,6,7,8,9,10,11,12,13,14);
endchar;
fi;

if unknown subfont:
beginchar(c_kuu,32uw#+rm#,20uh#,10uh#); "kuu";
    pickup frame_pen;
    x6 = 0;
    letter_ka(1,2,3,4,4',5,6,7,8',9',10',11');
    % three-like shape and tail for "uu"
    z8=(x10, y1);
    z9=(32uw, y10+3.75uh);
    z10=(x9 - 4uw, 3.5uh);
    z11=(x10 - 2uw, y10);
    z12=(x9, y10-3.75uw);
    z13=(x8, -4uw);
    % tail below
    z14=(x15+3uw, -4uw);
    z15=(0.5uw, -7uh);
    z16=(x14, -10uh);
    z17=(32uw, -10uh);

    draw_eye(1,2,3,4);
    draw z4'..z5{left}..z6{up}..z7{right}---z8{right}..z9{down}..
        z10{left}--z11;
    draw z11--z10{right}..z12{down}..z13{left}---z14{left}..z15{down}..
        z16{right}---z17;
    penlabels(1,2,3,4,4',5,6,7,8,9,10,11,12,13,14,15,16,17);
endchar;
fi;

if unknown subfont:
beginchar(c_var_kuu,40uw#+rm#,20uh#,10uh#); "old variant kuu";
    pickup frame_pen;
    x6 = 8uw;
    letter_ka(1,2,3,4,4',5,6,7,8,9,10,11');
    z11=(x11', 0);
    z12=(x9, -4uh);
    subscribed_ra(12,13',14,15,16)(10uh);
    z13=(x13'+4uw, y13');
    
    draw_eye(1,2,3,4);
    draw z4'..z5{left}..z6{up}..z7{right}---z8{right}..z9{down}..
        z10{left}..z11;
    draw z10{right}..z12{down}..z13..z14..z15{up}..z16;
    penlabels(1,2,3,4,4',5,6,7,8,9,10,11,12,13,14,15,16);
endchar;
fi;

if unknown subfont:
beginchar(c_sec_ku,30uw#+rm#,11uh#,0); "secondary ku";
    pickup frame_pen;
    x6 = 0;
    letter_ka(1,2,3,4,4',5,6,7,8',9',10',11');
    % three-like shape for "u"
    z8=(x10, y1);
    z9=(30uw, y10+2.75uw);
    z10=(x9 - 3.5uw, y6);
    z11=(x10 - .5uw, y6);
    z12=(x9, y10-2.75uw);
    z13=(x10, 0);
    z14=(x11, 0);

    draw z1{down}..z4{right}..z3{up};
    draw z4'..z5{left}..z6{up}..z7{right}---z8{right}..z9{down}..
        z10{left}--z11;
    draw z11--z10{right}..z12{down}..z13{left}..z14;
    penlabels(1,2,3,4,4',5,6,7,8,9,10,11,12,13,14);
endchar;
fi;

if unknown subfont:
beginchar(c_sec_kuu,32uw#+rm#,11uh#,10uh#); "secondary kuu";
    pickup frame_pen;
    x6 = 0;
    letter_ka(1,2,3,4,4',5,6,7,8',9',10',11');
    % three-like shape and tail for "uu"
    z8=(x10, y1);
    z9=(32uw, y10+3.75uh);
    z10=(x9 - 4uw, 3.5uh);
    z11=(x10 - 2uw, y10);
    z12=(x9, y10-3.75uw);
    z13=(x8, -4uw);
    % tail below
    z14=(x15+3uw, -4uw);
    z15=(0.5uw, -7uh);
    z16=(x14, -10uh);
    z17=(32uw, -10uh);

    draw z1{down}..z4{right}..z3{up};
    draw z4'..z5{left}..z6{up}..z7{right}---z8{right}..z9{down}..
        z10{left}--z11;
    draw z11--z10{right}..z12{down}..z13{left}---z14{left}..z15{down}..
        z16{right}---z17;
    penlabels(1,2,3,4,4',5,6,7,8,9,10,11,12,13,14,15,16,17);
endchar;
fi;

if unknown subfont:
beginchar(c_kr,32uw#+rm#,20uh#,11uh#); "kr";
    pickup frame_pen;
    x6=0;
    letter_ka(1,2,3,4,4',5,6,7,8',9',10',11');
    z8=(x3+1uw, y1);
    z9=(32uw, 2uh);  
    vowel_sign_vocalic_r(9,10,11,12,13);
    
    draw z1{up}..z2{right}..z3{down}..z4{left}..cycle;
    draw z4'..z5{left}..z6{up}..z7{right}---z8{right}..z9{down}..
        z10{left}..z11{up}..z12{right}..z13;
    penlabels(1,2,3,4,4',5,6,7,8,9,10,11,12,13);
endchar;
fi;

if unknown subfont:
beginchar(c_k_ku,49uw#+rm#,20uh#,0); "k+ku";
    pickup frame_pen;
    x6 = 0;
    letter_k_ka(1,2,3,4,4',5,6,7,8,9,10,11,12',13',14');
    z12 = z12' + (1uw, 0);
    vowel_sign_ra_u(12,13,14,15,16,17);

    draw_eye(1,2,3,4);
    draw z4'..z5{left}..z6{up}..z7{right}---z8{right}..z9{down}..
        z10{left}..z11{up}..z12{right}..z13{down}..z14{left};
    draw z14{right}..z15{down}..z16{left}..z17;
    penlabels(1,2,3,4,4',5,6,7,8,9,10,11,12,13,14,15,16,17);
endchar;
fi;

if unknown subfont:
beginchar(c_sec_k_ku,49uw#+rm#,11uh#,0); "secondary k+ku";
    pickup frame_pen;
    x6 = 0;
    letter_k_ka(1,2,3,4,4',5,6,7,8,9,10,11,12',13',14');
    z12 = z12' + (1uw, 0);
    vowel_sign_ra_u(12,13,14,15,16,17);

    draw z1{down}..z4{right}..z3{up};
    draw z4'..z5{left}..z6{up}..z7{right}---z8{right}..z9{down}..
        z10{left}..z11{up}..z12{right}..z13{down}..z14{left};
    draw z14{right}..z15{down}..z16{left}..z17;
    penlabels(1,2,3,4,4',5,6,7,8,9,10,11,12,13,14,15,16,17);
endchar;
fi;

if unknown subfont:
beginchar(c_k_kuu,49uw#+rm#,20uh#,10uh#); "k+kuu";
    pickup frame_pen;
    x6 = 0;
    letter_k_ka(1,2,3,4,4',5,6,7,8,9,10,11,12',13',14');
    z12=z12' + (1uw, 0);
    x18=.5uw;
    vowel_sign_uu(12,13,14,15,16,17,18,19,20);

    draw_eye(1,2,3,4);
    draw z4'..z5{left}..z6{up}..z7{right}---z8{right}..z9{down}..
        z10{left}..z11{up}..z12{right}..z13{down}..z14{left};
    draw z14{right}..z15{down}..z16{left}---z17{left}..z18{down}..
        z19{right}---z20;
    penlabels(1,2,3,4,4',5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20);
endchar;
fi;

if unknown subfont:
beginchar(c_sec_k_kuu,49uw#+rm#,11uh#,10uh#); "secondary k+kuu";
    pickup frame_pen;
    x6 = 0;
    letter_k_ka(1,2,3,4,4',5,6,7,8,9,10,11,12',13',14');
    z12=z12' + (1uw, 0);
    x18=.5uw;
    vowel_sign_uu(12,13,14,15,16,17,18,19,20);

    draw z1{down}..z4{right}..z3{up};
    draw z4'..z5{left}..z6{up}..z7{right}---z8{right}..z9{down}..
        z10{left}..z11{up}..z12{right}..z13{down}..z14{left};
    draw z14{right}..z15{down}..z16{left}---z17{left}..z18{down}..
        z19{right}---z20;
    penlabels(1,2,3,4,4',5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20);
endchar;
fi;

%%%%%% ga related glyphs

beginchar(c_gu,32uw#+rm#,ht#,10uh#); "gu";
    pickup frame_pen;
    x2=0uw;
    letter_ga(1,2,3,4,5,6',7');
    z6=(x6', 8uh);
    x9=.5uw;
    vowel_sign_u(6,7,8,9,10,11);

    draw z1..z2{down}..z3{right}..z4{up}..z5{right}..z6{down}..
        z7{left}---z8{left}..z9{down}..z10{right}---z11;
    penlabels(1,2,3,4,5,6,7,8,9,10,11);
endchar;

beginchar(c_guu,36uw#+rm#,ht#,10uh#); "guu";
    pickup frame_pen;
    x2=0uw;
    letter_ga(1,2,3,4,5',6',7');
    z5=z5'+(1uw, 0);
    x11=.5uw;
    vowel_sign_uu(5,6,7,8,9,10,11,12,13);

    draw z1..z2{down}..z3{right}..z4{up}..z5{right}..z6{down}..z7{left};
    draw z7{right}..z8{down}..z9{left}---z10..z11{down}..z12{right}---z13;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13);
endchar;

if unknown subfont:
beginchar(c_gr,32uw#+rm#,20uh#,11uh#); "gr";
    pickup frame_pen;
    x2=0uw;
    letter_ga(1,2,3,4,5,6',7');
    z6=(x6', 8uh);
    vowel_sign_vocalic_r(6,7,8,9,10);
    draw z1..z2{down}..z3{right}..z4{up}..z5{right}..z6{down}..
        z7{left}..z8{up}..z9{right}..z10;
    penlabels(1,2,3,4,5,6,7,8,9,10);
endchar;
fi;

%%%%%% nga related shapes

beginchar(c_ng_ku,30uw#+rm#,20uh#,0); "ng+ku";
    pickup frame_pen;
    x6 = 0;
    letter_ka(1,2,3,4,4',5,6,7,8',9',10',11');
    % three-like shape for "u"
    z8=(x10, y1);
    z9=(30uw, y10+2.75uw);
    z10=(x9 - 3.5uw, y6);
    z11=(x10 - .5uw, y6);
    z12=(x9, y10-2.75uw);
    z13=(x10, 0);
    z14=(x11, 0);

    % top left eye for "nga"
    z19=(11uw, 16uh);
    z20=(5uw, y2);
    z21=(0.5uw, 15.5uh);
    draw z19..z20{left}..z21{down}..z7{right};

    draw_eye(1,2,3,4);
    draw z4'..z5{left}..z6{up}..z7{right}---z8{right}..z9{down}..
        z10{left}--z11;
    draw z11--z10{right}..z12{down}..z13{left}..z14;
    penlabels(1,2,3,4,4',5,6,7,8,9,10,11,12,13,14,20,21);
endchar;

beginchar(c_ng_kuu,32uw#+rm#,20uh#,10uh#); "ng+kuu";
    pickup frame_pen;
    x6 = 0;
    letter_ka(1,2,3,4,4',5,6,7,8',9',10',11');
    % three-like shape and tail for "uu"
    z8=(x10, y1);
    z9=(32uw, y10+3.75uh);
    z10=(x9 - 4uw, 3.5uh);
    z11=(x10 - 2uw, y10);
    z12=(x9, y10-3.75uw);
    z13=(x8, -4uw);
    % tail below
    z14=(x15+3uw, -4uw);
    z15=(0.5uw, -7uh);
    z16=(x14, -10uh);
    z17=(32uw, -10uh);

    % top left eye for "nga"
    z19=(11uw, 16uh);
    z20=(5uw, y2);
    z21=(0.5uw, 15.5uh);
    draw z19..z20{left}..z21{down}..z7{right};

    draw_eye(1,2,3,4);
    draw z4'..z5{left}..z6{up}..z7{right}---z8{right}..z9{down}..
        z10{left}--z11;
    draw z11--z10{right}..z12{down}..z13{left}---z14{left}..z15{down}..
        z16{right}---z17;
    penlabels(1,2,3,4,4',5,6,7,8,9,10,11,12,13,14,15,16,17,19,20,21);
endchar;

%%%%%% cha related shapes

beginchar(c_chu,54uw#+rm#,20uh#,10uh#); "chu";
    pickup frame_pen;
    x6=0;   
    letter_cha(1,2,3,4,5,6,7,8,9,10,11,12',13',14');
    x18=.5uw;
    loop_u(11,12,13,14,15,16,17,18,19,20);
    draw z1..z2{up}..z3{right}..z4{down}..z5{left};
    draw z6--z7..z8;
    draw z8..z9{up}..z10{right}..z11{down}..z12{left}..z13{up}..
        z14{right}..z15{down}..z16{left}---z17{left}..z18{down}..
        z19{right}---z20;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20);
endchar;

beginchar(c_chuu,55uw#+rm#,20uh#,10uh#); "chuu";
    pickup frame_pen;
    x6=0;   
    letter_cha(1,2,3,4,5,6,7,8,9,10,11,12',13',14');
    x20=.5uw;
    loop_uu(11,12,13,14,15,16,17,18,19,20,21,22);
    draw z1..z2{up}..z3{right}..z4{down}..z5{left};
    draw z6--z7..z8;
    draw z8..z9{up}..z10{right}..z11{down}..z12{left}..z13{up}..
        z14{right}..z15{down}..z16{left};
    draw z16{right}..z17{down}..z18{left}---z19{left}..z20{down}..
        z21{right}---z22;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22);
endchar;

%%%%%% ja related glyphs

beginchar(c_ju,32uw#+rm#,20uh#,17uh#); "ju";
    pickup frame_pen;
    x1=0;
    letter_ja(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18);
    % tail for u
    z16'=(26uw, 2uh);
    z19=(32uw, -4.5uh);
    z20=(25uw, -11uh);
    z21=(x22+3uw, y20);
    z22=(.5uw, -14uh);
    z23=(x21, -17uh);
    z24=(32uw, y23);

    draw z1..z5{up}..z6{right}..{down}z7---z8;
    draw_eye(1,2,3,4);
    draw z8---z7{up}..z9{right}..z10{down}..z11..z12{down}..
        z13{right}..z14..z16'{right}..z19{down}..{left}z20---
        z21{left}..z22{down}..{right}z23---z24;
    draw_eye(15,16,17,18);
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,16',17,18,19,20,21,22,23,24);
endchar;

beginchar(c_juu,31uw#+rm#,20uh#,17uh#); "juu";
    pickup frame_pen;
    x1=0;
    letter_ja(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18);
    % tail for uu
    z16'=(26uw, 2uh);
    z19=(32uw, -4.5uh);
    z19'=(28.5uw, -4.5uh);
    z20=(26uw, -11uh);
    z21=(x22+3uw, y20);
    z22=(.5uw, -14uh);
    z23=(x21, -17uh);
    z24=(32uw, y23);

    draw z1..z5{up}..z6{right}..{down}z7---z8;
    draw_eye(1,2,3,4);
    draw z8---z7{up}..z9{right}..z10{down}..z11..z12{down}..
        z13{right}..z14..z16'{right}..z19'{left};
    draw z19'{right}..{left}z20---
        z21{left}..z22{down}..{right}z23---z24;
    draw_eye(15,16,17,18);
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,16',17,18,19,19',20,21,22,23,24);
endchar;

beginchar(c_j_ju,64uw#+rm#,20uh#,17uh#); "j+ju";
    pickup frame_pen;
    x1=0;
    letter_ja(1,2,3,4,5,6,7,8,9,10,11,12,13,14',15',16',17',18');
    z14=(24uw, 1uh);
    x1a=32uw;
    letter_ja(1a,2a,3a,4a,5a,6a,7a,8a,9a,10a,11a,12a,13a,14a,15a,16a,17a,18a);
    
    % tail for u
    z16'a=(x1a+26uw, 2uh);
    z19a=(x1a+32uw, -4.5uh);
    z20a=(x1a+25uw, -11uh);
    z21a=(x22a+3uw, y20a);
    z22a=(.5uw, -14uh);
    z23a=(x21a, -17uh);
    z24a=(x1a+32uw, y23a);

    draw z1..z5{up}..z6{right}..{down}z7---z8;
    draw_eye(1,2,3,4);
    draw z8---z7{up}..z9{right}..z10{down}..z11..z12{down}..
        z13{right}..z14..z1a{up};

    draw z1a..z5a{up}..z6a{right}..{down}z7a---z8a;
    draw_eye(1a,2a,3a,4a);
    draw z8a---z7a{up}..z9a{right}..z10a{down}..z11a..z12a{down}..
        z13a{right}..z14a..z16'a{right}..z19a{down}..{left}z20a---
        z21a{left}..z22a{down}..{right}z23a---z24a;
    draw_eye(15a,16a,17a,18a);
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14);
    penlabels(1a,2a,3a,4a,5a,6a,7a,8a,9a,10a,11a,12a,
    13a,14a,15a,16a,16'a,17a,18a,19a,20a,21a,22a,23a,24a);
endchar;

beginchar(c_j_juu,63uw#+rm#,20uh#,17uh#); "j+juu";
    pickup frame_pen;
    x1=0;
    letter_ja(1,2,3,4,5,6,7,8,9,10,11,12,13,14',15',16',17',18');
    z14=(24uw, 1uh);
    x1a=32uw;
    letter_ja(1a,2a,3a,4a,5a,6a,7a,8a,9a,10a,11a,12a,13a,14a,15a,16a,17a,18a);

    % tail for uu
    z16'a=(x1a+26uw, 2uh);
    z19a=(x1a+32uw, -4.5uh);
    z19'a=(x1a+28.5uw, -4.5uh);
    z20a=(x1a+26uw, -11uh);
    z21a=(x22a+3uw, y20a);
    z22a=(.5uw, -14uh);
    z23a=(x21a, -17uh);
    z24a=(x1a+32uw, y23a);

    draw z1..z5{up}..z6{right}..{down}z7---z8;
    draw_eye(1,2,3,4);
    draw z8---z7{up}..z9{right}..z10{down}..z11..z12{down}..
        z13{right}..z14..z1a{up};

    draw z1a..z5a{up}..z6a{right}..{down}z7a---z8a;
    draw_eye(1a,2a,3a,4a);
    draw z8a---z7a{up}..z9a{right}..z10a{down}..z11a..z12a{down}..
        z13a{right}..z14a..z16'a{right}..z19'a{left};
    draw z19'a{right}..{left}z20a---
        z21a{left}..z22a{down}..{right}z23a---z24a;
    draw_eye(15a,16a,17a,18a);
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14);
    penlabels(1a,2a,3a,4a,5a,6a,7a,8a,9a,10a,11a,12a,
    13a,14a,15a,16a,16'a,17a,18a,19a,19'a,20a,21a,22a,23a,24a);
endchar;

%%%%%% nna related shapes

beginchar(c_nnu,50uw#+rm#,20uh#,0); "nnu";
    pickup frame_pen;
    % from "malayalam letter nga"
    % first bow
    front_eye(1,2,3,4,5);
    z5=(0, 1/2h);
    z6=(9uw, h);
    z7=(17uw, 1/2h);
    z8=(x7, 0);
    % second bow
    z9=(25uw, h);
    z10=(33uw, 1/2h);
    z11=(x10, 0);
    % third bow
    z12=(41uw, h);
    z13=(50uw, 1/2h);
    end_loop(13,14,15,16);
    
    draw_eye(1,2,3,4);
    draw z4{left}..z5{up}..z6{right}..z7{down}---z8;
    draw z8---z7{up}..z9{right}..z10{down}---z11;
    draw z11---z10{up}..z12{right}..z13{down}..z14{left}..z15{up}..z16;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
endchar;

beginchar(c_nnuu,50uw#+7uw#+rm#,20uh#,6uh#); "nnuu";
    pickup frame_pen;
    % from "malayalam letter nga"
    % first bow
    front_eye(1,2,3,4,5);
    z5=(0, 1/2h);
    z6=(9uw, h);
    z7=(17uw, 1/2h);
    z8=(x7, 0);
    % second bow
    z9=(25uw, h);
    z10=(33uw, 1/2h);
    z11=(x10, 0);
    % third bow
    z12=(41uw, h);
    z13=(50uw, 1/2h);
    end_loop_tail(13,14,15,16,17,18,19);
    
    draw_eye(1,2,3,4);
    draw z4{left}..z5{up}..z6{right}..z7{down}---z8;
    draw z8---z7{up}..z9{right}..z10{down}---z11;
    draw z11---z10{up}..z12{right}..z13{down}..z14{left}..z15{up}..
        z16{right}..z17{down}..z18{left}..z19;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19);
endchar;

%%%%%% ta related shapes

beginchar(c_tu,30uw#+rm#,20uh#,10uh#); "tu";
    pickup frame_pen;
    x2=0;
    letter_ta(1,2,3,4,5,6,7,8',9');   
    z8=(x8', 8uh);
    x11=.5uw;
    vowel_sign_u(8,9,10,11,12,13);
    
    draw z1..z2{up}..z3{right}..z4{down}..z5{left}..z6{up}..
        z7{right}..z8{down}..z9{left}---z10{left}..z11{down}..
        z12{right}---z13;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13);
endchar;

beginchar(c_tuu,32uw#+rm#,20uh#,10uh#); "tuu";
    pickup frame_pen;
    x2=0;
    letter_ta(1,2,3,4,5,6,7',8',9');
    z7 = z7' + (1uw, 0);
    x13=.5uw;
    vowel_sign_uu(7,8,9,10,11,12,13,14,15);
    
    draw z1..z2{up}..z3{right}..z4{down}..z5{left}..z6{up}..
        z7{right}..z8{down}..z9{left};
    draw z9{right}..z10{down}..z11{left}---z12..z13{down}..
    	z14{right}---z15;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15);
endchar;

if unknown subfont:
beginchar(c_tr,30uw#+rm#,ht#,11uh#); "tr";
    pickup frame_pen;
    x2=0;
    letter_ta(1,2,3,4,5,6,7,8',9');
    z8=(x8', 8uh);
    vowel_sign_vocalic_r(8,9,10,11,12);
        
    draw z1..z2{up}..z3{right}..z4{down}..z5{left}..z6{up}..
        z7{right}..z8{down}..z9{left}..z10{up}..z11{right}..z12;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12);
endchar;
fi;

beginchar(c_t_tu,56uw#+rm#,ht#,10uh#); "t+tu";
    pickup frame_pen;
	x2=0;
	letter_ta(1,2,3,4,5,6,7',8',9');
	z7=z7' - (1uw, 0);
	z8=z8' - (2uw, 0);
    z9=(x8, .5uh);
    x9a = x8 - 2uw;
	letter_ta(8a, 9a, 10',11,12,13,14,15',16');
	z10=z10' + (1uw, 0);

    z15=(x15', 8uh);
    x18=.5uw;
    vowel_sign_u(15,16,17,18,19,20);
	    
    draw z1..z2{up}..z3{right}..z4{down}..z5{left}..z6{up}..
        z7{right}..z8{down}---z9;
    draw z9---z8{up}..z10{right}..z11{down}..z12{left}..z13{up}..
        z14{right}..z15{down}..z16{left}---z17..z18{down}..
        z19{right}--z20;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20);
endchar;

beginchar(c_t_tuu,58uw#+rm#,ht#,10uh#); "t+tuu";
    pickup frame_pen;
	x2=0;
	letter_ta(1,2,3,4,5,6,7',8',9');
	z7=z7' - (1uw, 0);
	z8=z8' - (2uw, 0);
    z9=(x8, .5uh);
    x9a = x8 - 2uw;
	letter_ta(8a, 9a, 10',11,12,13,14',15',16');
	z10=z10' + (1uw, 0);

	z14=z14'+(1uw,0);
    x20=.5uw;
    vowel_sign_uu(14,15,16,17,18,19,20,21,22);
	    
    draw z1..z2{up}..z3{right}..z4{down}..z5{left}..z6{up}..
        z7{right}..z8{down}---z9;
    draw z9---z8{up}..z10{right}..z11{down}..z12{left}..z13{up}..
        z14{right}..z15{down}..z16{left};
    draw z16{right}..z17{down}..z18{left}---z19..z20{down}..
        z21{right}--z22;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22);
endchar;

%%%%%% da related shapes

if unknown subfont:
beginchar(c_dr,18uw#+rm#,20uh#,11uh#); "dr";
    pickup frame_pen;
    x2=0;
    letter_da(1,2,3,4,5,6,7',8',9');
    z7=(x7', 2uh);
    vowel_sign_vocalic_r(7,8,9,10,11);
    
    draw z1..z2{up}..z3{right}..z4{down}..{left}z5---z6;
    draw z6---z5{right}..z7{down}..z8{left}..z9{up}..z10{right}..z11;
    penlabels(1,2,3,4,5,6,7,8,9,10,11);
endchar;
fi;

%%%%%% dha related glyphs

if unknown subfont:
beginchar(c_dhr,32uw#+rm#,20uh#,11uh#); "dhr";
    pickup frame_pen;
    x2=0;
    letter_dha(1,2,3,4,5,6,7,8);
    vowel_sign_vocalic_r(7,9,10,11,12);
    draw z1..z2{down}..z3{right}..{up}z4---z5;
    draw z5---z4{down}..z6{right}..z7{up}..z8;
    draw z7{down}..z9{left}..z10{up}..z11{right}..z12;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12);
endchar;
fi;

%%%%%% na related glyphs

beginchar(c_nu,32uw#+rm#,20uh#,0); "nu";
    pickup frame_pen;
    z1=(4uw, 0);
    z2=(0, 1/2h);
    z3=(8uw, h);
    z4=(16uw, 1/2h);
    z5=(x4, 0);
    z6=(24uw, h);
    z7=(32uw, 1/2h);
    end_loop(7,8,9,10);
    draw z1..z2{up}..z3{right}..{down}z4---z5;
    draw z5---z4{up}..z6{right}..z7{down}..z8{left}..z9{up}..z10;
    penlabels(1,2,3,4,5,6,7,8,9,10);
endchar;

beginchar(c_nuu,32uw#+7uw#+rm#,20uh#,6uh#); "nuu";
    pickup frame_pen;
    z1=(4uw, 0);
    z2=(0, 1/2h);
    z3=(8uw, h);
    z4=(16uw, 1/2h);
    z5=(x4, 0);
    z6=(24uw, h);
    z7=(32uw, 1/2h);
    end_loop_tail(7,8,9,10,11,12,13);
    draw z1..z2{up}..z3{right}..{down}z4---z5;
    draw z5---z4{up}..z6{right}..z7{down}..z8{left}..z9{up}..
        z10{right}..z11{down}..z12{left}..z13;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13);
endchar;

if unknown subfont:
beginchar(c_nr,32uw#+rm#,20uh#,11uh#); "nr";
    pickup frame_pen;
    x2=0;
    letter_na(1,2,3,4,5,6,7',8');
    z7=(x7', 8uh);
    vowel_sign_vocalic_r(7,8,9,10,11);  
    
    draw z1..z2{up}..z3{right}..z4{down}---z5;
    draw z4{up}..z6{right}..z7{down}..z8{left}..z9{up}..z10{right}..z11;
    penlabels(1,2,3,4,5,6,7,8,9,10,11);
endchar;
fi;

beginchar(c_n_tu,44uw#+rm#,20uh#,10uh#); "n+tu";
    pickup frame_pen;
    x2=0;
    letter_na(1,2,3,4,5,6a,7',8');
    z6=(.5[x6a,x6b], y6a);
    x5'=x5-2uw;
    letter_ta(4',5',6b,7,8,9,10,11',12');
    z11=(x11', 8uh);
    x14=.5uw;
    vowel_sign_u(11,12,13,14,15,16);
    
    draw z1..z2{up}..z3{right}..z4{down}---z5;
    draw z4{up}..z6{right}..z7{down}..z8{left}..z9{up}..z10{right}..
        z11{down}..z12{left}---z13..z14{down}..z15{right}---z16;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
endchar;

beginchar(c_n_tuu,46uw#+rm#,20uh#,10uh#); "n+tuu";
    pickup frame_pen;
    x2=0;
    letter_na(1,2,3,4,5,6a,7',8');
    z6=(.5[x6a,x6b], y6a);
    x5'=x5-2uw;
    letter_ta(4',5',6b,7,8,9,10',11',12');

	z10=z10'+(1uw,0);
    x16=.5uw;
    vowel_sign_uu(10,11,12,13,14,15,16,17,18);
    
    draw z1..z2{up}..z3{right}..z4{down}---z5;
    draw z4{up}..z6{right}..z7{down}..z8{left}..z9{up}..z10{right}..
        z11{down}..z12{left};
    draw z12{right}..z13{down}..z14{left}---z15..z16{down}..
    	z17{right}---z18;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18);
endchar;

beginchar(c_n_nu,48uw#+rm#,20uh#,0); "n+nu";
    pickup frame_pen;
    x2=0;
    letter_n_na(1,2,3,4,5,6,7,8,9,10,11');
    
    end_loop(10,11,12,13);
    draw z1..z2{up}..z3{right}..z4{down}---z5;
    draw z5---z4{up}..z6{right}..z7{down}---z8;
    draw z8---z7{up}..z9{right}..z10{down}..z11{left}..z12{up}..z13;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13);
endchar;

beginchar(c_n_nuu,48uw#+7uw#+rm#,20uh#,6uh#); "n+nuu";
    pickup frame_pen;
    x2=0;
    letter_n_na(1,2,3,4,5,6,7,8,9,10,11');
    
    end_loop_tail(10,11,12,13,14,15,16);
    draw z1..z2{up}..z3{right}..z4{down}---z5;
    draw z5---z4{up}..z6{right}..z7{down}---z8;
    draw z8---z7{up}..z9{right}..z10{down}..z11{left}..z12{up}..
        z13{right}..z14{down}..z15{left}..z16;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
endchar;

%%%%%% bha related glyphs

beginchar(c_bhu,20uw#+rm#,20uh#,6uh#); "bhu";
    pickup frame_pen;
    x2=0;
    letter_bha(1,2,3,4,5,6,7,8,9,10,11',12');
    z11=(14uw, 0);
    % bow for u
    z12=(x13+3uw, 0);
    z13=(5.5uw, -3uh);
    z14=(x12, -6uh);
    z15=(20uw, y14);
    
    draw z1..z2{up}..z3{right}..z4{down}..z5..z6..z7{down}..
        z8..z9..z10{down}..z11{left}..z12{left}..z13{down}..
        z14{right}---z15;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15);
endchar;

beginchar(c_bhuu,20uw#+8uw#+rm#,20uh#,9uh#); "bhuu";
    pickup frame_pen;
    x2=8uw;
    letter_bha(1,2,3,4,5,6,7,8,9,10,11,12');
    z12=(x2+8uw, 0);
    % curl around letter
    z13=(24uw, 0);
    z14=(28uw, -3.5uh);
    z15=(14uw, -9uh);
    z16=(0, 10uh);
    z17=(3.5uw, 20uh);
    
    draw z1..z2{up}..z3{right}..z4{down}..z5..z6..z7{down}..
        z8..z9..z10{down}..z11{left}..z12{left};
    draw z13{right}..z14{down}..z15{left}..z16{up}..z17;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17);
endchar;

if unknown subfont:
beginchar(c_bhr,20uw#+rm#,20uh#,11uh#); "bhr";
    pickup frame_pen;
    x2=0;
    letter_bha(1,2,3,4,5,6,7,8,9,10',11',12');
    z10=(x10', -1uh);
    vowel_sign_vocalic_r(10,11,12,13,14);
    draw z1..z2{up}..z3{right}..z4{down}..z5..z6..z7{down}..
        z8..z9..z10'..z10..z11{left}..z12{up}..z13{right}..z14;
    penlabels(1,2,3,4,5,6,7,8,9,10,10',11,12,13,14);
endchar;
fi;

%%%%%% ra related glyphs

if unknown subfont:
beginchar(c_ru,32uw#+rm#,ht#,0); "ru";
    pickup frame_pen;
    x2=0;
    letter_ta(1,2,3,4,5,6,7',8',9');
    z7 = z7'+(1uw, 0);
    vowel_sign_ra_u(7,8,9,10,11,12);

    draw z1..z2{up}..z3{right}..z4{down}..z5{left}..z6{up}..
        z7{right}..z8{down}..z9{left};
    draw z9{right}..z10{down}..z11{left}..z12;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12);
endchar;
fi;

if unknown subfont:
beginchar(c_ruu,40uw#+rm#,ht#,8uh#); "ruu";
    pickup frame_pen;
    % first bow
    z1=(x2+4uw, 0);
    z2=(8uw, 10uh);
    z3=(x2+10uw, 20uh);
    z4=(x2+20uw, 8uh);
    z5=(x2+15uw, 0);
    % second bow
    z6=(x2+10uw, y4);
    z7=(x2+22uw, 20uh);
    z8=(x10 - 1uw, 13uh);
    z9=(x10 - 6uw, 8uh);
    z10=(x2+32uw, 2uh);
    % bow below
    subscribed_ra(10,11',12,13,14)(8uh);
    z11=(x11'+2uw, y11');

    draw z1..z2{up}..z3{right}..z4{down}..z5{left}..z6{up}..
        z7{right}..z8{down}..z9{left};
    draw z9{right}..z10{down}..z11..z12..z13{up}..z14;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14);
endchar;
fi;

%%%%%% sha related shapes

beginchar(c_shu,40uw#+rm#,ht#,10uh#); "shu";
    pickup frame_pen;
    x2=0;
    letter_sha(1,2,3,4,5,6,7',8',9');
    x13=.5uw;
    loop_u(6,7,8,9,10,11,12,13,14,15);

    draw z1..z2{down}..z3{right}..z4{up}..z5{right}..z6{down}..z7{left}..
        z8{up}..z9{right}..z10{down}..z11{left}---z12{left}...z13{down}..
        z14{right}---z15;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15);
endchar;

beginchar(c_shuu,41uw#+rm#,ht#,10uh#); "shuu";
    pickup frame_pen;
    x2=0;
    letter_sha(1,2,3,4,5,6,7',8',9');
    x15=.5uw;
    loop_uu(6,7,8,9,10,11,12,13,14,15,16,17);

    draw z1..z2{down}..z3{right}..z4{up}..z5{right}..z6{down}..z7{left}..
        z8{up}..z9{right}..z10{down}..z11{left};
    draw z11{right}..z12{down}..z13{left}---z14{left}...z15{down}..
        z16{right}---z17;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17);
endchar;

if unknown subfont:
beginchar(c_shr,39uw#+rm#,ht#,11uh#); "shr";
    pickup frame_pen;
    x2=0;
    letter_ga(1,2,3,4,5,6,7');
    end_loop_tail(6,7,8,9,10,11',12')
    vowel_sign_vocalic_r(10,11,12,13,14);
    draw z1..z2{down}..z3{right}..z4{up}..z5{right}..z6{down}..z7{left}..
        z8{up}..z9{right}..z10{down}..z11{left}..z12{up}..z13{right}..z14;
    penlabels(1,2,3,4,5,6,7,8,9,11,12,13,14);
endchar;
fi;

%%%%%% sa related glyphs

if unknown subfont:
beginchar(c_sr,44uw#+rm#,20uh#,11uh#); "sr";
    pickup frame_pen;
    x2=0;
    letter_sa(1,2,3,4,5,6,7,8,9,10);
    vowel_sign_vocalic_r(9,11,12,13,14);
    
    draw z1..z2{up}..z3{right}..{down}z4---z5;
    draw z5---z4{up}..z6{right}..z7{down}..z8{right}..z9{up}..z10;
    draw z9{down}..z11{left}..z12{up}..z13{right}..z14;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14);
endchar;
fi;

%%%%%% ha related glyphs

beginchar(c_hu,42uw#+rm#,ht#,10uh#); "hu";
    pickup frame_pen;
	x2=0;
    letter_ha(1,2,3,4,5,6,7,8,9,10,11',12');
    z11=(x11', 8uh);
    x14=.5uw;
    vowel_sign_u(11,12,13,14,15,16);

    draw_bow(1,2,3,4,5);
    draw z5..z6---z7..z8;
    draw z8..z9{up}..z10{right}..z11{down}..z12{left}---z13{left}..
        z14{down}..z15{right}---z16;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
endchar;

beginchar(c_huu,44uw#+rm#,ht#,10uh#); "huu";
    pickup frame_pen;
	x2=0;
    letter_ha(1,2,3,4,5,6,7,8,9,10',11',12');
	z10=z10'+(1uw, 0);
	x16=.5uw;
	vowel_sign_uu(10,11,12,13,14,15,16,17,18);

    draw_bow(1,2,3,4,5);
    draw z5..z6---z7..z8;
    draw z8..z9{up}..z10{right}..z11{down}..z12{left};
    draw z12{right}..z13{down}..z14{left}---z15{left}..
        z16{down}..z17{right}---z18;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18);
endchar;

if unknown subfont:
beginchar(c_hr,41uw#+rm#,20uh#,11uh#); "hr";
    pickup frame_pen;
    % bow
    z1=(4uw, 0);
    small_bow(1,2,3,4,5);
    z6=(x5+3uw, .5uh);
    z7=(22uw, y6);
    z8=(x7+3uw, 0);
    large_bow(8,9,10,11',12');
    z11=(x11', 8uh);
    vowel_sign_vocalic_r(11,12,13,14,15);
    draw_bow(1,2,3,4,5);
    draw z5..z6---z7..z8;
    draw z8..z9{up}..z10{right}..z11{down}..z12{left}..z13{up}..
        z14{right}..z15;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15);
endchar;
fi;

%%%%%% end of mmcvconj.mf
