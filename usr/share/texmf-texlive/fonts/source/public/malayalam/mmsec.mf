% mmsec.mf -- malayalam secondary consonants and reepham
% (c) 1993 Jeroen Hellingman
% last edit: 24-JAN-1993

if unknown newfont :
beginchar(c_sec_ka,32uw#+rm#,11uh#,0); "secondary ka";
    pickup frame_pen;
    x6 = 0;
    letter_ka(1,2,3,4,4',5,6,7,8,9,10,11);
        
    draw z1{down}..z4{right}..z3{up};
    draw z4'..z5{left}..z6{up}..z7{right}---z8{right}..z9{down}..
        z10{left}..z11;
    penlabels(1,2,3,4,4',5,6,7,8,9,10,11,20,21);
endchar;
fi;

if unknown newfont:
beginchar(c_sec_k_ka,47uw#+rm#,11uh#,0); "secondary k+ka";
    pickup frame_pen;
    x6 = 0;
    letter_k_ka(1,2,3,4,4',5,6,7,8,9,10,11,12,13,14);

    draw z1{down}..z4{right}..z3{up};
    draw z4'..z5{left}..z6{up}..z7{right}---z8{right}..z9{down}..
        z10{left}..z11{up}..z12{right}..z13{down}..z14;
    penlabels(1,2,3,4,4',5,6,7,8,9,10,11,12,13,14);
endchar;
fi;

beginchar(c_sec_ya,12uw#+rm#,20uh#,10uh#); "secondary ya";
    pickup frame_pen;
    z1=(8uw, 19.5uh);
    z2=(7uw, 12uh);
    z3=(12uw, -4uh);
    z4=(.5[x3,x5], -10uh);
    z5=(-1uw, -6uh);
    z6=(6uw, 0);
    draw z1..z2..z3{down}..z4{left}..z5{up}..z6{up};
    penlabels(1,2,3,4,5,6);
endchar;

if unknown newfont:
beginchar(c_sec_ya_os,6uw#+rm#,20uh#,12uh#); "secondary ya (old style)";
    pickup frame_pen;
    z1=(2uw, 19.5uh);
    z2=(6uw, 5uh);
    
    z8=(-rm, .5uh);
    z7=(x8-4uw, -6uh);
    z6=(x5+5uw, y7);
    z5=(x8-16uw, .5[y7,y4]);
    z4=(x5+3uw, -12uh);
    z3=(x8, y4);

    draw z1..z2{down}..z3..z4..z5{up}..z6{right}---z7{right}..z8{up};
    penlabels(1,2,3,4,5,6,7,8);
endchar;
fi;

if unknown newfont:
beginchar(c_sec_ya_deep,7uw#+rm#,20uh#,18uh#); "secondary ya (old style, deep version)";
    pickup frame_pen;
    z1=(2uw, 19.5uh);
    z2=(7uw, 2uh);
    
    z8=(-rm, -5.5uh);
    z7=(x8-4uw, -12uh);
    z6=(x5+5uw, y7);
    z5=(x8-16uw, .5[y7,y4]);
    z4=(x5+3uw, -18uh);
    z3=(x8, y4);

    draw z1..z2{down}..z3..z4..z5{up}..z6{right}---z7{right}..z8{up};
    penlabels(1,2,3,4,5,6,7,8);
endchar;
fi;

if unknown newfont:
beginchar(c_sec_ya_deeper,7uw#+rm#,20uh#,22uh#); "secondary ya (old style, deeper version)";
    pickup frame_pen;
    z1=(2uw, 19.5uh);
    z2=(7uw, 0uh);
    
    z8=(-rm, -9.5uh);
    z7=(x8-4uw, -16uh);
    z6=(x5+5uw, y7);
    z5=(x8-16uw, .5[y7,y4]);
    z4=(x5+3uw, -22uh);
    z3=(x8, y4);

    draw z1..z2{down}..z3..z4..z5{up}..z6{right}---z7{right}..z8{up};
    penlabels(1,2,3,4,5,6,7,8);
endchar;
fi;

if unknown newfont:
beginchar(c_sec_ya_deepest,8uw#+rm#,20uh#,28uh#); "secondary ya (old style, deepest version)";
    pickup frame_pen;
    z1=(2uw, 19.5uh);
    z2=(8uw, -3uh);
    
    z8=(-rm, -15.5uh);
    z7=(x8-4uw, -22uh);
    z6=(x5+5uw, y7);
    z5=(x8-16uw, .5[y7,y4]);
    z4=(x5+3uw, -28uh);
    z3=(x8, y4);

    draw z1..z2{down}..z3..z4..z5{up}..z6{right}---z7{right}..z8{up};
    penlabels(1,2,3,4,5,6,7,8);
endchar;
fi;

if unknown newfont:
beginchar(c_reepham,2uw#+rm#,29uh#,0); "reepham";
    pickup sub_pen;
    z1=(0uw, 29uh);
    z2=(2uw, 29uh);
    z3=(1uw, 22uh);
    draw z1--z2 & z2--z3 & z3--z1;
    fill z1--z2--z3--cycle;
    penlabels(1,2,3);
endchar;
fi;

if unknown newfont:
beginchar(c_sec_ra_narrow,28uw#+rm#,0,6uh#); "secondary ra (narrow version)";
% width adjusted for "Ta", "ma", "La", "zha"
    pickup frame_pen;
    z1=(28uw, .5uh);
    subscribed_ra(1,2,3,4,5)(6uh);
    draw z1{down}..z2..z3..z4{up}..z5;
    penlabels(1,2,3,4,5);
endchar;
fi;

if unknown newfont:
beginchar(c_sec_ra,42uw#+rm#,0,9uh#); "secondary ra";
% width adjusted for "kha", "nya", "pa", "va"
    pickup frame_pen;
    z1=(42uw, .5uh);
    subscribed_ra(1,2,3,4,5)(9uh);
    draw z1{down}..z2..z3..z4{up}..z5;
    penlabels(1,2,3,4,5);
endchar;
fi;

if unknown newfont:
beginchar(c_sec_ra_wide,44uw#+rm#,0,9uh#); "secondary ra (wide version)";
% width adjusted for "ca", "tha", "Sa"
    pickup frame_pen;
    z1=(44uw, .5uh);
    subscribed_ra(1,2,3,4,5)(9uh);
    draw z1{down}..z2..z3..z4{up}..z5;
    penlabels(1,2,3,4,5);
endchar;
fi;

if unknown newfont:
beginchar(c_sec_ra_wider,48uw#+rm#,0,10uh#); "secondary ra (wider version)";
% width adjusted for "ca", "tha", "Sa"
    pickup frame_pen;
    z1=(48uw, .5uh);
    subscribed_ra(1,2,3,4,5)(10uh);
    draw z1{down}..z2..z3..z4{up}..z5;
    penlabels(1,2,3,4,5);
endchar;
fi;

if unknown newfont:
beginchar(c_sec_ra_even_wider,54uw#+rm#,0,11uh#); "secondary ra (even wider version)";
% width adjusted for "nga", "ba", "Da", Dha"
    pickup frame_pen;
    z1=(54uw, .5uh);
    subscribed_ra(1,2,3,4,5)(11uh);
    draw z1{down}..z2..z3..z4{up}..z5;
    penlabels(1,2,3,4,5);
endchar;
fi;

if unknown newfont:
beginchar(c_sec_ra_widest,58uw#+rm#,0,12uh#); "secondary ra (widest version)";
% width adjusted for "gha", "Na"
    pickup frame_pen;
    z1=(58uw, .5uh);
    subscribed_ra(1,2,3,4,5)(12uh);
    draw z1{down}..z2..z3..z4{up}..z5;
    penlabels(1,2,3,4,5);
endchar;
fi;

beginchar(c_ref_sec_ra,4uw#+rm#,20uh#,8uh#); "reformed script secondary ra";
    pickup frame_pen;
    z1=(12uw, -7uh);
    z2=(5uw, -8uh);
    z3=(0, 2uh);
    z4=(2uw, 20uh);
    draw z1..z2..z3{up}..z4;
    penlabels(1,2,3,4);
endchar;

beginchar(c_sec_la,22uw#+rm#,0,12uh#); "secondary la";
    pickup frame_pen;
    z1=(19uw, 0);
    z2=(22uw, -6uh);
    z3=(17uw, -12uh);
    z3'=(12uw, -6uh);
    z4=(6uw, 0);
    z5=(0, -6uh);
    z6=(0, -8uh);
    eye(6,7,8,9)(8uw,8uh);
    draw_eye(6,7,8,9);
    draw z6..z5{up}..z4{right}..z3'{down}..z3{right}..z2{up}..z1;
    penlabels(1,2,3,3',4,5,6,7,8,9);
endchar;

beginchar(c_sec_va,18uw#+rm#,20uh#,0); "secondary va";
    pickup frame_pen;
    z1=(0, 0.5uh);
    z2=(18uw, y1);
    z3=(x2, 19.5uh);
    draw z1--z2--z3;
    penlabels(1,2,3);
endchar;

beginchar(c_sec_va_sub,36uw#+rm#,20uh#,6uh#); "secondary va (subscribed version)";
% version of secodary va for use with na to prevent confusion with mpa
    pickup frame_pen;
    z1=(0, -5.5uh);
    z2=(36uw, y1);
    z3=(x2, 19.5uh);
    draw z1--z2--z3;
    penlabels(1,2,3);
endchar;

beginchar(c_sec_tha,30uw#+rm#,20uh#,0); "secondary tha";
    pickup frame_pen;
    z1=(0, 0.5uh);
    z2=(30uw, y1);
    end_tha_bow(2,3,4,5,6);
    draw z1---z2;
    draw_end_tha_bow(2,3,4,5,6);
    penlabels(1,2,3,4,5,6); 
endchar;

beginchar(c_sub_triangle,26uw#+rm#,0,10uh#); "subscribed triangle for double letters";
    pickup frame_pen;
    x1=26uw;
    make_sub_triangle(1,2,3,4);
endchar;

beginchar(c_sec_ssa,30uw#+rm#,20uh#,0); "secondary ssa";
    pickup frame_pen;
    z1=(0, 1/40h);
    z2=(30uw, y1);
    end_ssa(2,3,4,5,6,7,8);
    draw z1---z2;
    draw_end_ssa(2,3,4,5,6,7,8);
    penlabels(1,2,3,4,5,6,7,8); 
endchar;

%%%%%% eof
