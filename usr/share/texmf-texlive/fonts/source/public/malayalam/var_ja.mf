% var_ja.mf -- malayalam variant of ja
% copyright 1992 Jeroen Hellingman
% last edit: 29-NOV-1992

% version of ja with standard height (tail not hanging under base-
% line). Some glyphs still needs to be adapted to this ja shape:
% j+ju, j+juu.

def var_letter_ja(suffix a,b,c,d,e,f,g,h,i,j,k,l,m,n,o,p,q,r) =
% define points of "malayalam letter ja"
% x coordinates relative to x.a
% y coordinates absolute;
    % first eye (diameter = 7)
    z.a=(x.a, 15uh);
    eye(a,b,c,d)(7uw, 7uh);
    % first bow
    z.e=(x.a, 15uh);
    z.f=(x.a+7uw, 20uh);
    z.g=(x.a+14uw, 15uh);
    z.h=(x.g, 12.5uh);
    % second bow
    z.i=(x.a+20uw, 20uh);
    z.j=(x.a+26uw, 15uh);
    % tail
    z.k=(x.a+13uw, 8.5uh);
    z.l=(x.a+2uw, 3uh);  % was (x.a+.5uw, 3uh)
    z.m=(x.a+6uw,  0uh);
    z.n=(x.a+14uw, 3uh);
    % second eye (diameter = 5)
    z.p=(x.a+23uw, 5uh);
    eye(o,p,q,r)(5uw, 5uh);
enddef;

beginchar(28,26uw#+rm#,20uh#,0); "U+0D1C malayalam letter ja";
    pickup frame_pen;
    x1 = 0uw;
    var_letter_ja(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18);

    draw_eye(1,2,3,4);
    draw z1..z5{up}..z6{right}..{down}z7---z8;
    draw z8---z7{up}..z9{right}..z10{down}..z11..z12{down}..
        z13{right}..z14..{right}z16;
    draw_eye(15,16,17,18);
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18);
endchar;

beginchar(162,58uw#+rm#,20uh#,0); "j+ja";
    pickup frame_pen;
    x1=0;
    var_letter_ja(1,2,3,4,5,6,7,8,9,10,11,12,13,14',15',16',17',18');
    z14=(24uw, 5uh);
    x1a=32uw;
    var_letter_ja(1a,2a,3a,4a,5a,6a,7a,8a,9a,10a,11a,12a,13a,14a,15a,16a,17a,18a);

    draw_eye(1,2,3,4);
    draw z1..z5{up}..z6{right}..{down}z7---z8;
    draw z8---z7{up}..z9{right}..z10{down}..z11..z12{down}..
        z13{right}..z14..z1a{up};

    draw_eye(1a,2a,3a,4a);
    draw z1a..z5a{up}..z6a{right}..{down}z7a---z8a;
    draw z8a---z7a{up}..z9a{right}..z10a{down}..z11a..z12a{down}..
        z13a{right}..z14a..{right}z16a;
    draw_eye(15a,16a,17a,18a);
    
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14);
    penlabels(1a,2a,3a,4a,5a,6a,7a,8a,9a,10a,11a,12a,13a,14a,15a,16a,17a,18a);
endchar;

beginchar(184,77uw#+rm#,20uh#,0); "j+nya";
    pickup frame_pen;
    x1 = 0uw;
    var_letter_ja(1,2,3,4,5,6,7,8,9,10,11,12,13,14',15,16,17,18);
    z14=(24uw, 5uh);
    x1a=32uw;
    letter_nya(1a,2a,3a,4a,5a,6a,7a,8a,9a,10a,11a,12a,13a,14a,15a);
    
    draw_eye(1,2,3,4);
    draw z1..z5{up}..z6{right}..{down}z7---z8;
    draw z8---z7{up}..z9{right}..z10{down}..z11..z12{down}..
        z13{right}..z14..{right}z2a;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18);

    draw_eye(1a,2a,3a,4a);
    draw z4a{left}..z5a{up}..z6a{right}..z7a{down}---z8a;
    draw z8a---z7a{up}..z9a{right}..z10a{down}..z11a{left}..z12a{up}..
        z13a{right}..z14a{down}..z15a;
    penlabels(1a,2a,3a,4a,5a,6a,7a,8a,9a,10a,11a,12a,13a,14a,15a);
endchar;

beginchar(186,52uw#+rm#,20uh#,0uh#); "ny+ja";
    pickup frame_pen;
    x5=0uw;
    half_ba(1,2,3,4,5,6,7,8,9,10',11');
    z10=z9+(4uw, -2uh);
    x1a=26uw;
    var_letter_ja(1a,2a,3a,4a,5a,6a,7a,8a,9a,10a,11a,12a,13a,14a,15a,16a,17a,18a);
    
    draw_eye(1,2,3,4);
    draw z4{left}..z5{up}..z6{right}..z7{down}--z8;
    draw z8--z7{up}..z9{right}..z10;

    draw_eye(1a,2a,3a,4a);
    draw z1a..z5a{up}..z6a{right}..{down}z7a---z8a;
    draw z8a---z7a{up}..z9a{right}..z10a{down}..z11a..z12a{down}..
        z13a{right}..z14a..{right}z16a;
    draw_eye(15a,16a,17a,18a);
    
    penlabels(1,2,3,4,5,6,7,8,9,10);
    penlabels(1a,2a,3a,4a,5a,6a,7a,8a,9a,10a,11a,12a,13a,14a,15a,16a,17a,18a);
endchar;

if unknown newfont:
beginchar(203,40uw#+rm#,20uh#,8uh#); "j+ra";
    pickup frame_pen;
    x1=8uw;
    var_letter_ja(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18);    
    % tail for ra
    z19=(34uw, 6uh);
    z20=(40uw, 0uh);
    subscribed_ra(20,21,22,23,24)(8uh);

    draw z1..z5{up}..z6{right}..{down}z7---z8;
    draw_eye(1,2,3,4);
    draw z8---z7{up}..z9{right}..z10{down}..z11..z12{down}..
        z13{right}..z14..z19{right}..z20{down}..z21..z22..
        z23{up}..z24;
    draw_eye(15,16,17,18);
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24);
endchar;
fi;

if unknown newfont:
beginchar(131,31uw#+rm#,20uh#,10uh#); "ju";
    pickup frame_pen;
    x1=0;
    var_letter_ja(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18);
    % tail for u
    z19=(26uw, 6uh);
    z20=(31uw, .5[y19,y21]);
    x23=.5uw;
    vowel_sign_u(20,21',22,23,24,25);
    z21=(x19, y21');

    draw z1..z5{up}..z6{right}..{down}z7---z8;
    draw_eye(1,2,3,4);
    draw z8---z7{up}..z9{right}..z10{down}..z11..z12{down}..
        z13{right}..z14..z19{right}..z20{down}..{left}z21---
        z22{left}..z23{down}..{right}z24---z25;
    draw_eye(15,16,17,18);
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25);
endchar;
fi;

if unknown newfont:
beginchar(142,32uw#+rm#,20uh#,10uh#); "juu";
    pickup frame_pen;
    x1=0;
    var_letter_ja(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18);
    % tail for uu
    z19=(29uw, 6.5uh);
    z21=(x19, .5[y19,y22]);
    z22=(x19, -4uh);
    z23=(x24+3uw, y22);
    z24=(.5uw, -7uh);
    z25=(x23, -10uh);
    z26=(32uw, y25);

    draw z1..z5{up}..z6{right}..{down}z7---z8;
    draw_eye(1,2,3,4);
    draw z8---z7{up}..z9{right}..z10{down}..z11..z12{down}..
        z13{right}..z14..z19{right}..z21{left};
    draw z21{right}..{left}z22---z23{left}..z24{down}..{right}z25---z26;
    draw_eye(15,16,17,18);
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26);
endchar;
fi;

endinput;

beginchar(255,26uw#+rm#,20uh#,10uh#); "special letter ja";
    pickup frame_pen;
    x1 = 0uw;
    letter_ja(1,2,3,4,5,6,7,8,9,10,11,12',13',14',15',16',17',18');
    z12=(x12', -5uh);
    z13=(10uw, -10uh);
    z14=(25uw, -7uh);
    z16=(60uw, -2uh);
    eye(15,16,17,18)(8uw, 8uh);

    draw_eye(1,2,3,4);
    draw z1..z5{up}..z6{right}..{down}z7---z8;
    draw z8---z7{up}..z9{right}..z10{down}..z11..z12{down}..
        z13{right}..z14..{right}z16;
    draw_eye(15,16,17,18);
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18);
endchar;

% end of var_ja.mf
