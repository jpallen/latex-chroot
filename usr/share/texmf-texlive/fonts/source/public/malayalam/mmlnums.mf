% mmlnums.mf -- Latin digits for Malayalam-TeX font
% (c) 1993 Jeroen Hellingman
% last edit: 24-JAN-1992

beginchar(c_zero,12uw#+rm#,20uh#,0); "U+0030 digit zero";
    pickup frame_pen;
    z1=(0uw, 10uh);
    eye(1,2,3,4)(12uw,20uh);
    draw_eye(1,2,3,4);
    penlabels(1,2,3,4); 
endchar;

beginchar(c_one,12uw#+rm#,20uh#,0); "U+0031 digit one";
    pickup frame_pen;
    z1=(x2-4uw, 16uh);
    z2=(6uw, 19.5uh);
    z3=(x2, .5uh);
    z4=(x2-3uw, y3);
    z5=(x2+3uw, y3);
	draw z1{2,1}..{1,2}z2--z3;
	draw z4--z5;
    penlabels(1,2,3); 
endchar;

beginchar(c_two,12uw#+rm#,20uh#,0); "U+0032 digit two";
    pickup frame_pen;
    z1=(1uw, 15uh);				% was (.5uw, 14uh)
    z2=(.5[x1,x3], 20uh);
    z3=(11uw, y1);
    z5=(.5uw, 0.5uh);
    z6=(4.5uw, 2uh);
    z7=(11.5uw, .5uh);
    
	draw z1{up}..z2{right}..z3{down}..z5{down} & 
		z5--z7{right};
    penlabels(1,2,3,4,5,6,7);
endchar;

beginchar(c_three,12uw#+rm#,20uh#,0); "U+0033 digit three";
    pickup frame_pen;
    z1=(1uw, 16uh);
    z2=(.5[x1,x3], 20uh);
    z3=(11uw, .5[y2,y4]);
    z4=(x2, 11uh);
    z4'=(4uw, y4);
    z5=(12uw, .5[y4,y6]);
    z6=(.5[x5,x7], 0uh);
    z7=(0uw, 4.5uh);
    
    draw z1..z2{right}..z3{down}..z4{left}--z4';
    draw z4{right}..z5{down}..z6{left}..z7;
    penlabels(1,2,3,4,4',5,6,7); 
endchar;

beginchar(c_four,12uw#+rm#,20uh#,0); "U+0034 digit four";
    pickup frame_pen;
    z1=(0uw, 6uh);
    z2=(12uw, y1);
    z3=(9uw, 19.5uh);
    z4=(x3, 0.5uh);
    
	draw z2--z1--z3;
	draw z3--z4;
    penlabels(1,2,3,4); 
endchar;

beginchar(c_five,12uw#+rm#,20uh#,0); "U+0035 digit five";
    pickup frame_pen;
    z1=(10.5uw, 19.5uh);
    z2=(3uw, y1);
    z3=(1uw, 11uh);
    z4=(.5[x3,x5], 14uh);
    z5=(12uw, .5[y4,y6]);
    z6=(.5[x5,x7], 0uh);
    z7=(0uw, 4.5uh);
    
    draw z1--z2--z3 & z3..z4{right}..z5{down}..z6{left}..z7;
    penlabels(1,2,3,4,5,6,7); 
endchar;

beginchar(c_six,12uw#+rm#,20uh#,0); "U+0036 digit six";
    pickup frame_pen;
	z1=(7uw, 19.5uh);
	z2=(1uw, 6uh);
	eye(2,3,4,5)(10uw, 12uh);
	
	draw z1{-1,-1}..z2{down};
	draw_eye(2,3,4,5);
    penlabels(1,2,3,4,5); 
endchar;

beginchar(c_seven,12uw#+rm#,20uh#,0); "U+0037 digit seven";
    pickup frame_pen;
	z1=(1uw, 19.5uh);
	z2=(7uw, 18uh);
	z3=(11uw, y1);
	z4=(3uw, 10uh);
	z5=(x4, 0.5uh);
	draw z1--z3 & z3{curl 0}..{down}z5;
    penlabels(1,2,3,4,5); 
endchar;

beginchar(c_eight,12uw#+rm#,20uh#,0); "U+0038 digit eight";
    pickup frame_pen;
    z4=(6uw, 11uh);
    eye(1,2,3,4)(9uw, 9uh);
    eye(5,4,6,7)(10uw, 11uh);
    
    draw_eye(1,2,3,4);
    draw_eye(5,4,6,7);
    penlabels(1,2,3,4,5,6,7); 
endchar;

beginchar(c_nine,12uw#+rm#,20uh#,0); "U+0039 digit nine";
    pickup frame_pen;
	z1=(5uw, 0.5uh);
	z2=(1uw, 14uh);
	eye(2,3,4,5)(10uw, 12uh);
	
	draw z1{1,1}..z4{up};
	draw_eye(2,3,4,5);
    penlabels(1,2,3,4,5); 
endchar;

%%%%%% end of mmlnums.mf
