% mmconj.mf -- malayalam conjuncts
% (c) 1993 Jeroen Hellingman
% last edit: 22-JAN-1993

%%%%% ka related glyphs

if unknown subfont:
beginchar(c_k_ka,47uw#+rm#,20uh#,0); "k+ka";
    pickup frame_pen;
    x6 = 0;
    letter_k_ka(1,2,3,4,4',5,6,7,8,9,10,11,12,13,14);

    draw_eye(1,2,3,4);
    draw z4'..z5{left}..z6{up}..z7{right}---z8..
    	z9{down}..z10{left}..z11{up}..z12{right}..z13{down}..z14;
    penlabels(1,2,3,4,4',5,6,7,8,9,10,11,12,13,14);
endchar;
fi;

if unknown subfont:
beginchar(c_k_ta,60uw#+rm#,20uh#,0); "k+ta";
    pickup frame_pen;
    x6 = 0;
    letter_ka(1,2,3,4,4',5,6,7,8',9',10',11');
    z8=(x8'-2uw, y8');
    z9=(x9'-2uw, y9');
    z10=(x9, 0);
    x12=x9;
    letter_ta(11,12,13,14,15,16,17,18,19);

    draw_eye(1,2,3,4);
    draw z4'..z5{left}..z6{up}..z7{right}---z8{right}..z9{down}---z10;
    draw z10---z12{up}..z13{right}..z14{down}..z15{left}..z16{up}..
        z17{right}..z18{down}..z19;
    penlabels(1,2,3,4,4',5,6,7,8,9,10,11,12,13,14,15,16,17,18,19);
endchar;
fi;

if unknown subfont:
beginchar(c_k_ssa,57uw#+rm#,20uh#,0); "k+ssa";
    pickup frame_pen;
    x6 = 0;
    letter_ka(1,2,3,4,4',5,6,7,8,9',10',11');
    z9=(x9', .5[y8,y10]);
    z10=(x10', y12);
    z11=(x11', y12);
    z12=(57uw, .5uh);
    end_ssa(12,13,14,15,16,17,18);
    draw z11--z12;
    draw_end_ssa(12,13,14,15,16,17,18);
    draw_eye(1,2,3,4);
    draw z4'..z5{left}..z6{up}..z7{right}---z8{right}..z9{down}..
        z10{left};
    penlabels(range 1 thru 18, 4'); 
endchar;
fi;

%%%%%% ga related glyphs

beginchar(c_g_gha,58uw#+rm#,20uh#,0); "g+gha";
    pickup frame_pen;
	path ga_path;
    % little ga
    x2=0;
	letter_ga(1,2,3,4,5,6,7); % size: 20uh x 32uw
    ga_path = z1..z2{down}..z3{right}..z4{up}..z5{right}..z6{down}..z7;
	ga_path := ga_path scaled 12/20 shifted (0, 8uh);
	z8 = z7 scaled 12/20 shifted (0, 8uh);
	z9 = (x8+3uw, y8+.5uh);
    x11=44uw;
    letter_la(10,11,12,13,14,15);
    
    draw ga_path;
    draw z8..z9---z11;
    draw z11{up}..z12{left}..z10{down}---z13--z14--z15;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15);  
endchar;

beginchar(c_g_da,33uw#+rm#,20uh#,0); "g+da";
    pickup frame_pen;
    x2=0uw;
    letter_ga(1,2,3,4,5,6',7');
    letter_da(3',4',5,6,7,8,9,10,11);
    
    draw z1..z2{down}..z3{right}..z4{up}..z5{right}..z6{down}..
        z7{left}---z8;
    draw z7{right}..z9{down}..z10{left}..z11;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12);
endchar;

beginchar(c_g_d_dha,57uw#+rm#,20uh#,0); "g+d+dha";
    pickup frame_pen;
    x2a=0;
    letter_ga(1a,2a,3a,4a,5a,6a,7a);
    x5a=x3;
    letter_da(1,2,3,4',5',6',7',8',9');
    z4=(x4', .5[y3,y6]);
    z5=(x5', 12uh);
    z6=(x6', y5);
    z7=(x7', .5[y6,y8]);
    z8=(x8', 4uh);
    z9a=(x9 + 2uw, y8);
    z9=(x9', .5[y8,y10]);
    z9b=(x9a, y10);
    z10=(.5[x9,x11], 0);
    x11=26uw+15uw;
    letter_dha(10',11',12',11,12,13,14,15);
    
    draw z1a..z2a{down}..z3a{right}..z4a{up}..z3{right}...z4{down}...
        {left}z5---z6;
    draw z6---z5{right}...z7{down}...z8{left}...
        z9a{left}...z9{down}...z9b{right}...
        z10{right}..z11{up}---z12;
    draw z11{down}..z13{right}..z14{up}..z15;
    penlabels(1a,2a,3a,4a,5a,3,4,5,6,7,8,9,9a,9b,10,11,12,13,14,15);
endchar;

beginchar(c_g_na,48uw#+rm#,20uh#,0); "g+na";
    pickup frame_pen;
    x2=0uw;
    letter_ga(1,2,3,4,5,6',7');
    letter_na(3',4',5,6,7,8,9,10);
    
    draw z1..z2{down}..z3{right}..z4{up}..z5{right}..z6{down}---z7;
    draw z6{up}..z8{right}..z9{down}..z10;
    penlabels(1,2,3,4,5,6,7,8,9,10,11);
endchar;

beginchar(c_g_ma,36uw#+rm#,20uh#,0); "g+ma";
    pickup frame_pen;
    x2=0uw;
    letter_ga(1,2,3,4,5',6',7');
    x5=x4;
    letter_ma(5,6,7,8,9,10,11,12);
    
    draw z1..z2{down}..z3{right}..z4{up};
    draw_letter_ma(5,6,7,8,9,10,11,12);
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12);
endchar;

%%%%%% nga related glyphs

beginchar(c_ng_nga,50uw#+rm#,ht#,0); "ng+nga";
    pickup frame_pen;
    % first bow
    front_eye(1,2,3,4,5);
    z5=(0, 10uh);
    z6=(12.5uw, 20uh);
    z7=(x8+5uw, 8uh);
    z8=(21uw, 0);
    % second bow
    z9=(x8-5uw, 8uh);
    z10=(25.5uw, 20uh);
    z11=(x17-17uw, 10uh);
    z12=(x11, 0);
    % three-like shape, from "nga"
    z13=(x17-8uw, 20uh);
    z14=(x17-1uw, 15uh);
    z15=(x17-6uw, 10uh);
    z16=(x17-10uw, y15);
    z17=(50uw, 5/20h);
    z18=(x17-6uw, 0);
    z19=(x17-10uw, 1uh);

    draw_eye(1,2,3,4);
    draw z4{left}..z5{up}..z6{right}..z7{down}..z8{left}..
        z9{up}..z10{right}..z11{down}---z12;
    draw z12---z11{up}..z13{right}..z14{down}..z15{left}---z16;
    draw z16---z15{right}..z17{down}..z18{left}..z19;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19);
endchar;

beginchar(c_ng_ka,32uw#+rm#,20uh#,0); "ng+ka";
    pickup frame_pen;
    x6 = 0;
    letter_ka(1,2,3,4,4',5,6,7,8,9,10,11);
    
    % top left eye for "nga"
    z19=(11uw, 16uh);
    z20=(5uw, y2);
    z21=(0.5uw, 15.5uh);
    draw z19..z20{left}..z21{down}..z7{right};
    
    draw_eye(1,2,3,4);
    draw z4'..z5{left}..z6{up}..z7{right}---z8{right}..z9{down}..
        z10{left}..z11;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,19,20,21);
endchar;

%%%%%% ca related glyphs

if unknown newfont:
beginchar(c_var_c_ca,49uw#+rm#,20uh#,0); "variant c+ca";
    pickup frame_pen;
	% first bow
	z10=(0, .5uh);
    z11=(4uw, 6uh);
    z12=(4uw, 7uh);
    z13=(10.5uw, 14uh);
    x1=17uw;
    letter_ca(1,2,3,4,5,6,7,8);
    draw z11..z12{up}..z13{right}..z2{down};
    draw z2{up}..z3{right}..z4{down}..z5{left};
    draw z10--z7--z8;
    penlabels(1,2,3,4,5,6,7,8,10,11,12,13);
endchar;
fi;

if unknown newfont:
beginchar(c_var_c_cha,59uw#+rm#,20uh#,0); "variant c+cha";
    pickup frame_pen;
	% first bow
	z20=(0, .5uh);
    z21=(4uw, 6uh);
    z22=(4uw, 7uh);
    z23=(10.5uw, 14uh);
    x1=17uw;
    letter_cha(1,2,3,4,5,6,7,8,9,10,11,12,13,14);
    draw z21..z22{up}..z23{right}..z2{down};
    draw z2{up}..z3{right}..z4{down}..z5{left};
    draw z20--z7..z8;
    draw z8..z9{up}..z10{right}..z11{down}..z12{left}..z13{up}..z14;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,20,21,22,23);
endchar;
fi;

%%%%%% ja related glyphs

beginchar(c_j_ja,58uw#+rm#,20uh#,6uh#); "j+ja";
    pickup frame_pen;
    x1=0;
    letter_ja(1,2,3,4,5,6,7,8,9,10,11,12,13,14',15',16',17',18');
    z14=(24uw, 1uh);
    x1a=32uw;
    letter_ja(1a,2a,3a,4a,5a,6a,7a,8a,9a,10a,11a,12a,13a,14a,15a,16a,17a,18a);

    draw_eye(1,2,3,4);
    draw z1..z5{up}..z6{right}..{down}z7---z8;
    draw z8---z7{up}..z9{right}..z10{down}..z11..z12{down}..
        z13{right}..z14..z1a{up};

    draw_eye(1a,2a,3a,4a);
    draw z1a..z5a{up}..z6a{right}..{down}z7a---z8a;
    draw z8a---z7a{up}..z9a{right}..z10a{down}..z11a..z12a{down}..
        z13a{right}..z14a..{right}z16a;
    draw_eye(15a,16a,17a,18a);
    
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14);
    penlabels(1a,2a,3a,4a,5a,6a,7a,8a,9a,10a,11a,12a,13a,14a,15a,16a,17a,18a);
endchar;

beginchar(c_j_jha,81uw#+rm#,20uh#,6uh#); "j+jha";
    pickup frame_pen;
    x1=0;
    letter_ja(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18);
	x18'=x17-2uw;
	letter_jha(17',18',19,20,21,22,23,24,25,26,27,28,29,30);

    draw_eye(1,2,3,4);
    draw z1..z5{up}..z6{right}..{down}z7---z8;
    draw z8---z7{up}..z9{right}..z10{down}..z11..z12{down}..
        z13{right}..z14..{right}z16;
    draw_eye(15,16,17,18);
    
    draw z10..z19{right}..z20{down}..z21{left}..z22{up}..z23{right}..
    	z24{down}..z25{right}..z26{up}---z27;
    draw z26{down}..z28{right}..z29{up}..z30;
    
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,
    	21,22,23,24,25,26,27,28,29,30);
endchar;

beginchar(c_j_nya,77uw#+rm#,20uh#,6uh#); "j+nya";
    pickup frame_pen;
    x1 = 0uw;
    letter_ja(1,2,3,4,5,6,7,8,9,10,11,12,13,14',15,16,17,18);
    z14=(25uw, 3uh);

	x1a=32uw;
	letter_nya(1a,2a,3a,4a,5a,6a,7a,8a,9a,10a,11a,12a,13a,14a,15a);

    draw_eye(1a,2a,3a,4a);
    draw z4a{left}..z5a{up}..z6a{right}..z7a{down}---z8a;
    draw z8a---z7a{up}..z9a{right}..z10a{down}..z11a{left}..z12a{up}..
        z13a{right}..z14a{down}..z15a;
    penlabels(1a,2a,3a,4a,5a,6a,7a,8a,9a,10a,11a,12a,13a,14a,15a);

    draw_eye(1,2,3,4);
    draw z1..z5{up}..z6{right}..{down}z7---z8;
    draw z8---z7{up}..z9{right}..z10{down}..z11..z12{down}..
        z13{right}..z14..{right}z2a;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18);
endchar;

%%%%%% nya related glyphs

beginchar(c_ny_ca,60uw#+rm#,20uh#,0); "ny+ca";
    % link eye is almost same as in "sh+ca"
    pickup frame_pen;
    x5=0uw;
    half_ba(1,2,3,4,5,6,7,8,9',10',11');
    z9=z9'+(2uw, 0);
    z10=z10'+(2uw, 1uh);
    z12=(.5[x10,x13], 5uh);
    z13=z10-(8uw, 0);
	z14=(x13+9uw, 20uh);
	z15=(x14+9uw, 10uh);
    x17=24uw;
    letter_ca(12',13',14',15',16,17,18,19);
    
    draw_eye(1,2,3,4);
    draw z4{left}..z5{up}..z6{right}..z7{down}--z8;
    draw z8--z7{up}..z9{right}..z10{down}..z12{left}..z13{up}..
        z14{right}..z15{down}..z16{left};
    draw z17--z18 & z18--z19;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19);
endchar;

beginchar(c_ny_cha,72uw#+rm#,20uh#,0); "ny+cha";
    pickup frame_pen;
    x5=0uw;
    half_ba(1,2,3,4,5,6,7,8,9',10',11');
    z9=z9'+(2uw, 0);
    z10=z10'+(2uw, 1uh);
    z12=(.5[x10,x13], 5uh);
    z13=z10-(8uw, 0);
	z14=(x13+9uw, 20uh);
	z15=(x14+9uw, 10uh);
    x17=26uw;
    letter_cha(12',13',14',15',16',17,18,19,20,21,22,23,24,25);
    z16=(x16'-2uw, y16');
    
    draw_eye(1,2,3,4);
    draw z4{left}..z5{up}..z6{right}..z7{down}--z8;
    draw z8--z7{up}..z9{right}..z10{down}..z12{left}..z13{up}..
        z14{right}..z15{down}..z16{left};
    draw z17--z18..z19;
    draw z19..z20{up}..z21{right}..z22{down}..z23{left}..z24{up}..z25;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25);
endchar;

beginchar(c_ny_nya,72uw#+rm#,ht#,0); "ny+nya";
    pickup frame_pen;
	x5 = 0;
    letter_nya(1,2,3,4,5,6,7,8,9,10,11,12,13',14',15');
	z13=z13' - (1uw, 0);
	z14=z14' - (2uw, 0);
    z15=(x14, .5uh);
    x15a = x14 - 2uw;
	letter_ta(14a,15a,16',17,18,19,20,21,22);
	z16=z16' + (1uw, 0);

    draw_eye(1,2,3,4);
    draw z4{left}..z5{up}..z6{right}..z7{down}---z8;
    draw z8---z7{up}..z9{right}..z10{down}..z11{left}..z12{up}..
        z13{right}..z14{down}--z15;
    draw z14{up}..z16{right}..z17{down}..z18{left}..z19{up}..
    	z20{right}..z21{down}..z22;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22);
endchar;

beginchar(c_ny_ja,52uw#+rm#,20uh#,6uh#); "ny+ja";
    pickup frame_pen;
    x5=0uw;
    half_ba(1,2,3,4,5,6,7,8,9,10',11');
    z10=z9+(4uw, -2uh);
    x1a=26uw;
    letter_ja(1a,2a,3a,4a,5a,6a,7a,8a,9a,10a,11a,12a,13a,14a,15a,16a,17a,18a);
    
    draw_eye(1,2,3,4);
    draw z4{left}..z5{up}..z6{right}..z7{down}--z8;
    draw z8--z7{up}..z9{right}..z10;

    draw_eye(1a,2a,3a,4a);
    draw z1a..z5a{up}..z6a{right}..{down}z7a---z8a;
    draw z8a---z7a{up}..z9a{right}..z10a{down}..z11a..z12a{down}..
        z13a{right}..z14a..{right}z16a;
    draw_eye(15a,16a,17a,18a);
    
    penlabels(1,2,3,4,5,6,7,8,9,10);
    penlabels(1a,2a,3a,4a,5a,6a,7a,8a,9a,10a,11a,12a,13a,14a,15a,16a,17a,18a);
endchar;

%%%%%% tta related glyphs

beginchar(c_tt_tta,18uw#+rm#,20uh#,10uh#); "tt+tta";
    wd := 18uw;
    pickup frame_pen;
    z1=(18uw, h-2uh);
    z2=(1/2wd, h);
    z3=(0, 15uh);
    z4=(1/2wd,10uh);
    z5=(wd, 5uh);
    z6=(1/2wd, 0);
    z7=(0uw, 0);
    % different from tta
    z8=(wd, -5uh);
    z9=(1/2wd, -10uh);
    z10=(0, -8uh);
    
    draw z1..z2{left}..z3{down}..z4{4,1}..z5{down}..z6{left}..z7;
    draw z6{right}..z8{down}..z9{left}..z10;
    penlabels(1,2,3,4,5,6,7,8,9,10);
endchar;

%%%%%% nna related glyphs

beginchar(c_nn_tta,52uw#+rm#,20uh#,0); "nn+tta";
    pickup frame_pen;
    x5=0uw;
    half_ba(1,2,3,4,5,6,7,8,9,10,11a);
    z11=(x10, 0);
    x11'=x11;
    letter_bha(10',11',12,13,14,15,16,17,18,19,20,21);
    
    draw_eye(1,2,3,4);
    draw z4{left}..z5{up}..z6{right}..z7{down}--z8;
    draw z8--z7{up}..z9{right}..z10{down}---z11;
    draw z10{up}..z12{right}..z13{down}..z14..z15..z16{down}..z17..
        z18..z19{down}..z20{left}..z21;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21);
endchar;

beginchar(c_nn_dda,76uw#+rm#,20uh#,0); "nn+dda";
    pickup frame_pen;
    x5=0uw;
    half_ba(1,2,3,4,5,6,7,8,9,10,11a);
    z11=(x10, 0);
    x11'=x11;
    letter_dda(10',11',12,13,14,15,16,17,18,19);
    
    draw_eye(1,2,3,4);
    draw z4{left}..z5{up}..z6{right}..z7{down}--z8;
    draw z8--z7{up}..z9{right}..z10{down}---z11;
    draw z10{up}..z12{right}..z13{down}..z14{right}..z15{up}---z16;
    draw z15{down}..z17{right}..z18{up}..z19;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19);
endchar;

beginchar(c_nn_ddha,76uw#+rm#,20uh#,0); "nn+ddha";
    pickup frame_pen;
    x5=0uw;
    half_ba(1,2,3,4,5,6,7,8,9,10,11a);
    z11=(x10, 0);
    x11'=x11;
    letter_dda(10',11',12,13,14,15,16,17,18,19');
    upside_down_end_loop(18,19,20,21);
    
    draw_eye(1,2,3,4);
    draw z4{left}..z5{up}..z6{right}..z7{down}--z8;
    draw z8--z7{up}..z9{right}..z10{down}---z11;
    draw z10{up}..z12{right}..z13{down}..z14{right}..z15{up}---z16;
    draw z15{down}..z17{right}..z18{up}..z19{left}..z20{down}..z21;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22);
endchar;

beginchar(c_nn_ka,54uw#+rm#,20uh#,0); "nn+ka";
    pickup frame_pen;
    x5=0uw;
    half_ba(1,2,3,4,5,6,7,8,9,10',11');
    x10=x10';
    letter_ka(10,11,12,13,13',14,15,16,17,18,19,20);
    
    draw_eye(1,2,3,4);
    draw z4{left}..z5{up}..z6{right}..z7{down}--z8;
    draw z8--z7{up}..z9{right}..z10{down};
    draw_eye(10,11,12,13);
    draw z13'..z14{left}..z15{up}..z16{right}---z17{right}..
        z18{down}..z19{left}..z20;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,13',14,15,16,17,18,19,20);
endchar;

%%%%%% ta related glyphs

beginchar(c_t_ta,56uw#+rm#,ht#,0); "t+ta";
    pickup frame_pen;
	x2=0;
	letter_ta(1,2,3,4,5,6,7',8',9');
	z7=z7'-(1uw, 0);
	z8=z8'-(2uw, 0);
    z9=(x8,.5uh);
    x9a = x8-2uw;
	letter_ta(8a,9a,10',11,12,13,14,15,16);
	z10=z10' + (1uw, 0);
	    
    draw z1..z2{up}..z3{right}..z4{down}..z5{left}..z6{up}..
        z7{right}..z8{down}---z9;
    draw z9---z8{up}..z10{right}..z11{down}..z12{left}..z13{up}..
        z14{right}..z15{down}..z16;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16);
endchar;

beginchar(c_t_na,44uw#+rm#,20uh#,0); "t+na";
    pickup frame_pen;
    x2=0;
    letter_ta(1,2,3,4,5,6,7a,8',9');
    z7=(.5[x7a,x7b], y7a);
    x8=x8'-2uw;
    letter_na(5',6',7b,8,9,10,11,12);
    
    draw z1..z2{up}..z3{right}..z4{down}..z5{left}..z6{up}..
        z7{right}..z8{down}--z9;
    draw z8{up}..z10{right}..z11{down}..z12;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12);
endchar;

beginchar(c_t_bha,50uw#+rm#,ht#,0); "t+bha";
    pickup frame_pen;
    x2=0;
    letter_ta(1,2,3,4,5,6,7,8,9);
    x12=x8;
    letter_bha(11',12,13,14,15,16,17,18,19,20,21,22);
    draw z1..z2{up}..z3{right}..z4{down}..z5{left}..z6{up}..
        z7{right}..z8{down}..z9;
    draw z12{up}..z13{right}..z14{down}..z15..z16..z17{down}..
        z18..z19..z20{down}..z21{left}..z22;
    penlabels(1,2,3,4,5,6,7,8,9,11,12,13,14,15,16,17,18,19,20,21,22);
endchar;

beginchar(c_t_ma,48uw#+rm#,ht#,0); "t+ma";
    pickup frame_pen;
    x2=0;
    letter_ta(1,2,3,4,5,6,7',8',9');
    z7=(x7'-1uw, y7');
    x8=x8'-2uw;
    letter_ma(8,9,10,11,12,13,14,15);
    draw z1..z2{up}..z3{right}..z4{down}..z5{left}..z6{up}..
        z7{right}..z12{down};
    draw_letter_ma(8,9,10,11,12,13,14,15);
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15);
endchar;

beginchar(c_t_sa,56uw#+rm#,20uh#,0); "t+sa";
    pickup frame_pen;
    x2=0;
    letter_ta(1,2,3,4,5,6,7a,8',9');
    z7=(.5[x7a,x7b], y7a);
    x8=x8'-2uw;
    letter_sa(5',6',7b,8,9,10,11,12,13,14);
    
    draw z1..z2{up}..z3{right}..z4{down}..z5{left}..z6{up}..
        z7{right}..z8{down}--z9;
    draw z8{up}..z10{right}..z11{down}..z12{right}..z13{up}..z14;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14);
endchar;

%%%%%% da related glyphs

beginchar(c_d_da,18uw#+rm#,20uh#,10uh#); "d+da";
    pickup frame_pen;
    x2=0uw;
    letter_da(1,2,3,4,5,6,7,8,9');
    z9=(7uw, 0uh);
    z10=(18uw, -5uh);
    z11=(12uw, -10uh);
    z12=(0uw, -8uh);
    
    draw z1..z2{up}..z3{right}..z4{down}..{left}z5---z6;
    draw z6---z5{right}..z7{down}..z8{left}..z9;
    draw z8{right}..z10{down}..z11{left}..z12;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12);
endchar;

beginchar(c_d_dha,42uw#+rm#,20uh#,0); "d+dha";
    pickup frame_pen;
    x2=0;
    letter_da(1,2,3,4',5',6',7',8',9');
    z4=(x4', .5[y3,y6]);
    z5=(x5', 12uh);
    z6=(x6', y5);
    z7=(x7', .5[y6,y8]);
    z8=(x8', 4uh);
    z9a=(x9 + 2uw, y8);
    z9=(x9', .5[y8,y10]);
    z9b=(x9a, y10);
    z10=(.5[x9,x11], 0);
    x11=26uw;
    letter_dha(10',11',12',11,12,13,14,15);
    
    draw z1..z2{up}..z3{right}...z4{down}...{left}z5---z6;
    draw z6---z5{right}...z7{down}...z8{left}...
        z9a{left}...z9{down}...z9b{right}...
        z10{right}..z11{up}---z12;
    draw z11{down}..z13{right}..z14{up}..z15;
    penlabels(1,2,3,4,5,6,7,8,9,9a,9b,10,11,12,13,14,15);
endchar;

%%%%%% dha related glyphs

%%%%%% na related glyphs

beginchar(c_n_ta,44uw#+rm#,20uh#,0); "n+ta";
    pickup frame_pen;
    x2=0;
    letter_na(1,2,3,4,5,6a,7',8');
    z6=(.5[x6a,x6b], y6a);
    x5'=x5-2uw;
    letter_ta(4',5',6b,7,8,9,10,11,12);
    
    draw z1..z2{up}..z3{right}..z4{down}---z5;
    draw z4{up}..z6{right}..z7{down}..z8{left}..z9{up}..z10{right}..
        z11{down}..z12;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12);
endchar;

beginchar(c_n_da,33uw#+rm#,20uh#,0); "n+da";
    pickup frame_pen;
    x2=0;
    letter_na(1,2,3,4,5,6,7',8');
    letter_da(4',5',6,7,8,9,10,11,12);
    
    draw z1..z2{up}..z3{right}..z4{down}---z5;
    draw z4{up}..z6{right}..z7{down}..z8{left}---z9;
    draw z8{right}..z10{down}..z11{left}..z12;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12);
endchar;

beginchar(c_n_dha,60uw#+rm#,20uh#,0); "n+dha";
    pickup frame_pen;
    x2=0;
    letter_sa(1,2,3,4,5,6,7,8',9',10');
    x7' = x7 - 2uw;
    z8 = (.5[x7,x9], 0);
    letter_dha(6',7',8a,9,10,11,12,13);
    
    draw z1..z2{up}..z3{right}..{down}z4---z5;
    draw z4{up}..z6{right}..z7{down}..z8{right}..z9{up}---z10;
    draw z9{down}..z11{right}..z12{up}..z13;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13);
endchar;

beginchar(c_n_na,48uw#+rm#,20uh#,0); "n+na";
    pickup frame_pen;
    x2=0;
    letter_n_na(1,2,3,4,5,6,7,8,9,10,11);
    
    draw z1..z2{up}..z3{right}..z4{down}---z5;
    draw z5---z4{up}..z6{right}..z7{down}---z8;
    draw z8---z7{up}..z9{right}..z10{down}..z11;
    penlabels(1,2,3,4,5,6,7,8,9,10,11);
endchar;

beginchar(c_n_ma,36uw#+rm#,20uh#,0); "n+ma";
    pickup frame_pen;
    x2=0;
    letter_na(1,2,3,4,5',6',7',8');
    x5=x4;
    letter_ma(5,6,7,8,9,10,11,12);
    
    draw z1..z2{up}..z3{right}..z4{down};
    draw_letter_ma(5,6,7,8,9,10,11,12);
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12);
endchar;

%%%%%% bha related glyphs

beginchar(c_bh_dha,44uw#+rm#,20uh#,0); "bh+dha";
    pickup frame_pen;
    z1=(2uw, 0);
    z2=(0, 10uh);
    z3=(11uw, 20uh);
    z4=(19uw, .5[y3,y5]);
    z5=(x4-2uw, y4-2uh);
    z6=(x7+2uw, y7+2uh);
    z7=(6uw, 12.5uh);
    z8=(x7+2uw, y7-2uh);
    z9=(x10-2uw, y10+2uh);
    z10=(20uw, 7.5uh);
    z11=(14uw, 4.5uh);
    z12=(6uw, .5[y11,y13]);
    z13=(.5[x12,x14], 0);
    x14=28uw;
    letter_dha(13',14',15',14,15,16,17,18);
    
    draw z1..z2{up}..z3{right}...z4{down}..z5..z6..z7{down}..
        z8..z9..z10{down}..z11{left}..z12{down}...z13{right}..
        z14{up}---z15;
    draw z14{down}..z16{right}..z17{up}..z18;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18);
endchar;

%%%%%% ma related glyphs

beginchar(c_m_pa,46uw#+rm#,20uh#,0); "m+pa";
    pickup frame_pen;
    x2=0;
    letter_na(1,2,3,4,5,6,7,8);
    z9=z8+(3uw, .5uh);
    z10=(x7+14uw, y9);
    z11=(x10, 19.5uh);
    draw_letter_na(1,2,3,4,5,6,7,8);
    draw z8..z9---z10 & z10--z11;
    penlabels(1,2,3,4,5,6,7,8,9,10,11);
endchar;

%%%%%% sha related glyphs

beginchar(c_sh_ca,58uw#+rm#,ht#,0); "sh+ca";
    % link eye is almost same as in "ny+ca"
    pickup frame_pen;
    x2=0;
    letter_sha(1,2,3,4,5,6',7',8',9');
    z6=z6'+(0, 1uh);
    z10=(.5[x6,x11], 5uh);
    z11=z6-(8uw, 0);
	z12=(x11+9uw, 20uh);
	z13=(x12+9uw, 10uh);
    x15=22uw;
    letter_ca(10',11',12',13',14,15,16,17);

    draw z1..z2{down}..z3{right}..z4{up}..z5{right}..z6{down}..
        z10{left}..z11{up}..z12{right}..z13{down}..z14{left};
    draw z15--z16 & z16--z17;
    penlabels(1,2,3,4,5,6,10,11,12,13,14,15,16,17);
endchar;

%%%%%% sa related glyphs

%%%%%% ha related glyphs

beginchar(c_h_na,56uw#+rm#,20uh#,0); "h+na";
    pickup frame_pen;
    % bow
    z1=(4uw, 0);
    small_bow(1,2,3,4,5);
    z6=(x5+3uw, .5uh);
    z7'=(x7-3uw, .5uh);
    x8=24uw;
    letter_na(7,8,9,10,11,12,13,14);

    draw_bow(1,2,3,4,5);
    draw z5..z6---z7'..z7;
    draw_letter_na(7,8,9,10,11,12,13,14);
    penlabels(1,2,3,4,5,6,7,7',8,9,10,11,12,13,14);
endchar;

beginchar(c_h_ma,44uw#+rm#,20uh#,0); "h+ma";
    pickup frame_pen;
    % bow
    z1=(4uw, 0);
    small_bow(1,2,3,4,5);
    z6=(x5+3uw, .5uh);
    x7=24uw;
    letter_ma(7,8,9,10,11,12,13,14);

    draw_bow(1,2,3,4,5);
    draw z5..z6---z7;
    draw_letter_ma(7,8,9,10,11,12,13,14);
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14);
endchar;

%%%%%% end of mmconj.mf
