% mmcons.mf -- malayalam consonants
% (c) 1993 Jeroen Hellingman
% last edit: 24-JAN-1993

if unknown subfont:
beginchar(c_ka,32uw#+rm#,20uh#,0); "U+0D15 malayalam letter ka";
    pickup frame_pen;
    x6 = 0;
    letter_ka(1,2,3,4,4',5,6,7,8,9,10,11);
    
    draw_eye(1,2,3,4);
    draw z4'..z5{left}..z6{up}..z7{right}---z8{right}..z9{down}..
        z10{left}..z11;
    penlabels(1,2,3,4,4',5,6,7,8,9,10,11); 
endchar;
fi;

beginchar(c_kha,34uw#+rm#,ht#,0); "U+0D16 malayalam letter kha";
    pickup frame_pen;
    front_eye(1,2,3,4,5);
    % bow
    z5=(0, 1/2h);
    z6=(10uw, h);
    z7=(20uw, 1/2h);
    z8=(16uw, 0);
    draw_eye(1,2,3,4);
    draw z4{left}..z5{up}..z6{right}..z7{down}..z8;
    % end hook
    z9=(x8+3uw, 1/40h);
    z10=(34uw, y9);
    z11=(x10, 19.5uh);
    draw z8..z9---z10 & z10---z11;
    penlabels(1,2,3,4,5,6,7,8,9,10,11); 
endchar;

beginchar(c_ga,32uw#+rm#,20uh#,0); "U+0D17 malayalam letter ga";
    pickup frame_pen;
    x2=0uw;
    letter_ga(1,2,3,4,5,6,7);
    draw z1..z2{down}..z3{right}..z4{up}..z5{right}..z6{down}..z7;
    penlabels(1,2,3,4,5,6,7);   
endchar;

beginchar(c_gha,51uw#+rm#,20uh#,0); "U+0D18 malayalam letter gha";
    pickup frame_pen;
    % first little bow (diameter = 12)
    z1=(3uw, 8uh);
    z2=(0, 14uh);
    z3=(6uw, 20uh);
    z4=(12uw, 14uh);
    z5=(9uw, 8uh);
    z6=(x5+3uw, y5+.5uh);
    x8=37uw;
    letter_la(7,8,9,10,11,12);
    
    draw_bow(1,2,3,4,5);
    draw z5..z6---z8;
    draw z8{up}..z9{left}..z7{down}---z10--z11--z12;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12);  
endchar;

beginchar(c_nga,34uw#+rm#,ht#,0); "U+0D19 malayalam letter nga";
    pickup frame_pen;
    % from "malayalam letter i" (z1..z9)
    % eye
    z1=(1uw, 6/20h);
    z2=(6uw, 12/20h);
    z3=(11uw, y1);
    z4=(x2, 0);
    % first bow
    z5=(0, 1/2h);
    z6=(9uw, h);
    z7=(17uw, 1/2h);
    z8=(x7, 0);
    % different from "malayalam letter i"
    % second bow
    z9=(26uw, h);
    z10=(33uw, 15/20h);
    % three-like shape
    z11=(28uw, 10/20h);
    z12=(24uw, y11);
    z13=(34uw, 5/20h);
    z14=(28uw, 0);
    z15=(24uw, 1/20h);
    draw z1{up}..z2{right}..z3{down}..z4{left}..cycle;
    draw z4{left}..z5{up}..z6{right}..z7{down}--z8;
    draw z8--z7{up}..z9{right}..z10{down}..{left}z11---z12;
    draw z12--z11{right}..z13{down}..z14{left}..z15;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15);
endchar;

beginchar(c_ca,36uw#+rm#,20uh#,0); "U+0D1A malayalam letter ca";
    pickup frame_pen;
    x6=0;
    letter_ca(1,2,3,4,5,6,7,8);
    draw z1..z2{up}..z3{right}..z4{down}..z5{left};
    draw z6--z7--z8;
    penlabels(1,2,3,4,5,6,7,8); 
endchar;

beginchar(c_cha,46uw#+rm#,20uh#,0); "U+0D1B malayalam letter cha";
    pickup frame_pen;
    x6=0;
    letter_cha(1,2,3,4,5,6,7,8,9,10,11,12,13,14);
    draw z1..z2{up}..z3{right}..z4{down}..z5{left};
    draw z6--z7..z8;
    draw z8..z9{up}..z10{right}..z11{down}..z12{left}..z13{up}..z14;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14);
endchar;

beginchar(c_ja,26uw#+rm#,20uh#,6uh#); "U+0D1C malayalam letter ja";
    pickup frame_pen;
    x1 = 0uw;
    letter_ja(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18);

    draw_eye(1,2,3,4);
    draw z1..z5{up}..z6{right}..{down}z7---z8;
    draw z8---z7{up}..z9{right}..z10{down}..z11..z12{down}..
        z13{right}..z14..{right}z16;
    draw_eye(15,16,17,18);
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18);
endchar;

beginchar(c_jha,58uw#+rm#,ht#,0); "U+0D1D malayalam letter jha";
    pickup frame_pen;
    x2=0uw;
	letter_jha(1,2,3,4,5,6,7,8,9,10,11,12,13,14);

    draw z1..z2{up}..z3{right}..z4{down}..z5{left}..z6{up}..z7{right}..
        z8{down}..z9{right}..z10{up}---z11;
    draw z10{down}..z12{right}..z13{up}..z14;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14);
endchar;

beginchar(c_nya,46uw#+rm#,ht#,0); "U+0D1E malayalam letter nya";
    pickup frame_pen;
	x5 = 0;
    letter_nya(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15);
    
    draw_eye(1,2,3,4);
    draw z4{left}..z5{up}..z6{right}..z7{down}---z8;
    draw z8---z7{up}..z9{right}..z10{down}..z11{left}..z12{up}..
        z13{right}..z14{down}..z15;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15);
endchar;

beginchar(c_tta,18uw#+rm#,20uh#,0); "U+0D1F malayalam letter tta";
    pickup frame_pen;
    x3=0;
    letter_tta(1,2,3,4,5,6,7);
    draw z1..z2{left}..z3{down}..z4{4,1}..z5{down}..z6{left}..z7;
    penlabels(1,2,3,4,5,6,7);
endchar;

beginchar(c_ttha,20uw#+rm#,20uh#,0); "U+0D20 malayalam letter ttha";
    pickup frame_pen;
    z1=(0,10uh);
    eye(1,2,3,4)(20uw,20uh);
    draw_eye(1,2,3,4);
    penlabels(1,2,3,4);
endchar;

beginchar(c_dda,44uw#+rm#,20uh#,0); "U+0D21 malayalam letter dda";
    pickup frame_pen;
    x2=0;
    letter_dda(1,2,3,4,5,6,7,8,9,10);
    draw z1..z2{up}..z3{right}..z4{down}..z5{right}..{up}z6---z7;
    draw z7---z6{down}..z8{right}..z9{up}..z10;
    penlabels(1,2,3,4,5,6,7,8,9,10);
endchar;

beginchar(c_ddha,44uw#+rm#,20uh#,0); "U+0D22 malayalam letter ddha";
    pickup frame_pen;
    x2=0;
    letter_dda(1,2,3,4,5,6,7,8,9,10');
    upside_down_end_loop(9,10,11,12);
    draw z1..z2{up}..z3{right}..z4{down}..z5{right}..{up}z6---z7;
    draw z7---z6{down}..z8{right}..z9{up}..z10{left}..z11{down}..z12;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12);
endchar;

beginchar(c_nna,50uw#+rm#,ht#,0); "U+0D23 malayalam letter nna";
    pickup frame_pen;
    x5=0uw;
    half_ba(1,2,3,4,5,6,7,8,9',10',11');
    z9=(25uw, 20uh);
    z10=(33uw, 10uh);
    z11=(x10, 0);
    % third bow
    z12=(41uw, 20uh);
    z13=(50uw, 10uh);
    z14=(46uw, 0);
    
    draw_eye(1,2,3,4);
    draw z4{left}..z5{up}..z6{right}..z7{down}---z8;
    draw z8---z7{up}..z9{right}..z10{down}---z11;
    draw z11---z10{up}..z12{right}..z13{down}..z14;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14);
endchar;

beginchar(c_ta,30uw#+rm#,ht#,0); "U+0D24 malayalam letter ta";
    pickup frame_pen;
    x2=0;
    letter_ta(1,2,3,4,5,6,7,8,9);   
    draw z1..z2{up}..z3{right}..z4{down}..z5{left}..z6{up}..
        z7{right}..z8{down}..z9;
    penlabels(1,2,3,4,5,6,7,8,9);
endchar;

beginchar(c_tha,32uw#+rm#,ht#,0); "U+0D25 malayalam letter tha";
    pickup frame_pen;
    z1=(0, 19.5uh);
    z2=(0, 1/40h);
    z3=(32uw, y2);
    end_tha_bow(3,4,5,6,7);
    draw z1---z2 & z2--z3;
    draw_end_tha_bow(3,4,5,6,7);
    penlabels(1,2,3,4,5,6,7);   
endchar;

beginchar(c_da,18uw#+rm#,20uh#,0); "U+0D26 malayalam letter da";
    pickup frame_pen;
    x2=0;
    letter_da(1,2,3,4,5,6,7,8,9);
    draw z1..z2{up}..z3{right}..z4{down}..{left}z5---z6;
    draw z6---z5{right}..z7{down}..z8{left}..z9;
    penlabels(1,2,3,4,5,6,7,8,9);
endchar;

beginchar(c_dha,32uw#+rm#,20uh#,0); "U+0D27 malayalam letter dha";
    pickup frame_pen;
    x2=0;
    letter_dha(1,2,3,4,5,6,7,8);
    draw z1..z2{down}..z3{right}..{up}z4---z5;
    draw z5---z4{down}..z6{right}..z7{up}..z8;
    penlabels(1,2,3,4,5,6,7,8);
endchar;

beginchar(c_na,32uw#+rm#,20uh#,0); "U+0D28 malayalam letter na";
    pickup frame_pen;
    x2=0;
    letter_na(1,2,3,4,5,6,7,8);
    draw_letter_na(1,2,3,4,5,6,7,8);
    penlabels(1,2,3,4,5,6,7,8);
endchar;

beginchar(c_pa,36uw#+rm#,ht#,0); "U+0D2A malayalam letter pa";
    pickup frame_pen;
    % bow
    z1=(4uw, 0);
    small_bow(1,2,3,4,5);
    % end hook
    z6=(x5+3uw, 1/40h);
    z7=(36uw, y6);
    z8=(x7, 19.5uh);
    draw_bow(1,2,3,4,5);
    draw z5..z6---z7 & z7---z8;
    penlabels(1,2,3,4,5,6,7,8); 
endchar;

beginchar(c_pha,40uw#+rm#,ht#,0); "U+0D2B malayalam letter pha";
    pickup frame_pen;
    % bow
    z1=(4uw, 0);
    small_bow(1,2,3,4,5);
    % end hook
    z6=(x5+3uw, 1/40h);
    z7=(40uw, y6);
    end_tha_bow(7,8,9,10,11);
    draw_bow(1,2,3,4,5);
    draw z5..z6---z7;
    draw_end_tha_bow(7,8,9,10,11);
    penlabels(1,2,3,4,5,6,7,8,9,10,11); 
endchar;

beginchar(c_ba,46uw#+rm#,20uh#,0); "U+0D2C malayalam letter ba";
    pickup frame_pen;
    x5=0uw;
    half_ba(1,2,3,4,5,6,7,8,9,10,11);
    % end hook
    z12=(x11+3uw, 0.5uh);
    z13=(46uw, y12);
    z14=(x13, 19.5uh);
    
    draw_eye(1,2,3,4);
    draw z4{left}..z5{up}..z6{right}..z7{down}--z8;
    draw z8--z7{up}..z9{right}..z10{down}..z11;
    draw z11..z12---z13---z14;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14);
endchar;

beginchar(c_bha,20uw#+rm#,20uh#,0); "U+0D2D malayalam letter bha";
    pickup frame_pen;
    x2=0;
    letter_bha(1,2,3,4,5,6,7,8,9,10,11,12);
    draw z1..z2{up}..z3{right}..z4{down}..z5..z6..z7{down}..
        z8..z9..z10{down}..z11{left}..z12;
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12);
endchar;

beginchar(c_ma,20uw#+rm#,20uh#,0); "U+0D2E malayalam letter ma";
    pickup frame_pen;
    x1=0;
    letter_ma(1,2,3,4,5,6,7,8);
    draw_letter_ma(1,2,3,4,5,6,7,8);
    penlabels(1,2,3,4,5,6,7,8);
endchar;

beginchar(c_ya,32uw#+rm#,20uh#,0); "U+0D2F malayalam letter ya";
    pickup frame_pen;
    x5=0uw;
    letter_ya(1,2,3,4,5,6,7,8,9,10);
    draw z1..z2{right}..z3{down}..z4{left}..z5{up}..z6{right}..
       z7{(1,-8)}..z8{right}..z9{up}..z10;
    penlabels(1,2,3,4,5,6,7,8,9,10,11);
endchar;

if unknown subfont:
beginchar(c_ra,20uw#+rm#,20uh#,0); "U+0D30 malayalam letter ra";
    pickup frame_pen;
    x2=0;
    letter_ra(1,2,3,4,5,6,7);
    draw z1..z2{up}..z3{right}..z4{down}..z5{left}..z6{up}..z7;
    penlabels(1,2,3,4,5,6,7);
endchar;
fi;

if unknown subfont:
beginchar(c_rra,20uw#+rm#,20uh#,0); "U+0D31 malayalam letter rra";
    pickup frame_pen;
    z1=(4uw, 0);
    z2=(0, 1/2h);
    z3=(10uw, h);
    z4=(20uw, 1/2h);
    z5=(16uw, 0);
    draw_bow(1,2,3,4,5);
    penlabels(1,2,3,4,5);
endchar;
fi;

beginchar(c_la,32uw#+rm#,20uh#,0); "U+0D32 malayalam letter la";
    pickup frame_pen;
	x1=0;
	letter_la(1,2,3,4,5,6);
    draw z1---z2{up}..z3{left}..z1{down}---z4--z5--z6;
    penlabels(1,2,3,4,5,6); 
endchar;

beginchar(c_lla,22uw#+rm#,20uh#,10uh#); "U+0D33 malayalam letter lla";
    % like "malayalam letter o"
    pickup frame_pen;
    front_eye(1,2,3,4,5);
    % first bow
    z5=(0, 10uh);
    z6=(10uw, 20uh);
    z7=(19uw, 13uh);
    z8=(15uw, 8uh);
    z9=(22uw, 3uh);
    % tail below
    z10=(14uw, -4uh);
    z11=(5uw, -4uh);
    z12=(1uw, -7uh);
    z13=(5uw, -10uh);
    z14=(24uw, y13);

    draw_eye(1,2,3,4);
    draw z4{left}..z5{up}..z6{right}..z7{down}..{left}z8;
    draw z8{right}..z9{down}..z10..z11{left}..z12{down}..z13{right}---z14;
    
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14);
endchar;

beginchar(c_llla,22uw#+rm#,20uh#,0); "U+0D34 malayalam letter llla";
    pickup frame_pen;
    z1=(4uw, h);
    z2=(0, 15uh);
    z3=(10uw, 8uh);
    z4=(22uw, 15uh);
    z5=(17uw, h);
    z6=(12uw, 15uh);
    z7=(14uw, 5uh);
    z8=(8uw, 0);
    z9=(3uw, 2uh);
    draw z1..z2{down}..z3..z4{up}..z5{left}..z6..z7..z8{left}..z9;
    penlabels(1,2,3,4,5,6,7,8,9);
endchar;

beginchar(c_va,34uw#+rm#,ht#,0); "U+0D35 malayalam letter va";
    pickup frame_pen;
    % bow
    z1=(4uw, 0);
    large_bow(1,2,3,4,5);
    % end hook
    z6=(x5+3uw, 0.5uh);
    z7=(34uw, y6);
    z8=(x7, 19.5uh);
    draw_bow(1,2,3,4,5);
    draw z5..z6---z7---z8;
    penlabels(1,2,3,4,5,6,7,8); 
endchar;

beginchar(c_sha,32uw#+rm#,ht#,0); "U+0D36 malayalam letter sha";
    pickup frame_pen;
    x2=0;
    letter_sha(1,2,3,4,5,6,7,8,9);
    draw z1..z2{down}..z3{right}..z4{up}..z5{right}..z6{down}..z7{left}..
        z8{up}..z9;
    penlabels(1,2,3,4,5,6,7,8,9);
endchar;

beginchar(c_ssa,40uw#+rm#,ht#,0); "U+0D37 malayalam letter ssa";
    pickup frame_pen;
    % bow
    z1=(4uw, 0);
    small_bow(1,2,3,4,5);
    z6=(x5+3uw, 1/40h);
    z7=(40uw, y6);
    end_ssa(7,8,9,10,11,12,13);
    draw_bow(1,2,3,4,5);
    draw z5..z6---z7;
    draw_end_ssa(7,8,9,10,11,12,13)
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13);   
endchar;

beginchar(c_sa,44uw#+rm#,20uh#,0); "U+0D38 malayalam letter sa";
    pickup frame_pen;
    x2=0;
    letter_sa(1,2,3,4,5,6,7,8,9,10);
    draw z1..z2{up}..z3{right}..{down}z4---z5;
    draw z5---z4{up}..z6{right}..z7{down}..z8{right}..z9{up}..z10;
    penlabels(1,2,3,4,5,6,7,8,9,10);
endchar;

beginchar(c_ha,42uw#+rm#,20uh#,0); "U+0D39 malayalam letter ha";
    pickup frame_pen;
	x2=0;
    letter_ha(1,2,3,4,5,6,7,8,9,10,11,12);

    draw_bow(1,2,3,4,5);
    draw z5..z6---z7..z8;
    draw_bow(8,9,10,11,12);
    penlabels(1,2,3,4,5,6,7,8,9,10,11,12);
endchar;
