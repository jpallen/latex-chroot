%-------------------------------------------------------------
% This is ETH_GGU.MF     as of Dec, 1995 by Jochen Metzinger
% originally distributed by Abass B. Alamnehe [February, 1993]
%-------------------------------------------------------------

iff known eth_Gua:
cmchar "The letter Gua";
beginchar(eth_Gua,8u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(1u#,6.5u#);

pickup fine.nib; penpos1(stem,5); penpos2(stem,0); penpos3(stem,0);
pos4(stem,10);
x1r=w-1.5u; x2r=x1r-.5u; x3r=w-.75u; x4l=w-4u;
bot y1=0; y2=.1h; y3=.75h; y4=.95h;
filldraw stroke z1e{left}...{up}z2e---z3e{up}..{left}z4e;
z5=z4l; lft_wing(5,.75u,.025h);
x6=x3; x7=x6+3u; y6=y7=.55h; draw z6--z7; 
x9=x7; y9=y7-.245h; large_ring(9,2);

% BAR
penpos12(stem,0); x12=x4l; y12=y4+0.15h;
penpos12'(stem,0); x12'=x12; y12'+0.015h=y12;
lft_half_ellipse(12,2u); rt_half_ellipse(12',2u);
penlabels(1,2,3,4); endchar;


iff known eth_Gue:
cmchar "The letter Gue";
beginchar(eth_Gue,8u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(1u#,6u#);

pickup fine.nib; penpos1(stem,5); penpos2(stem,0); penpos3(stem,0);
pos4(stem,10);
x1r=w-1.5u; x2r=x1r-.5u; x3r=w-.75u; x4l=w-4u;
bot y1=0; y2=.1h; y3=.75h; y4=.95h;
filldraw stroke z1e{left}...{up}z2e---z3e{up}..{left}z4e;
z5=z4l; lft_wing(5,.75u,.025h);

pos9(vair,90); pos9'(vair,90); penpos10(stem,0); 
penpos10'(stem,0); penpos11(stem,0);
x9=.5[x1r,x4r]+fine; x9'=x9+2.5u; y9=y9'=y1+.6(y4-y1); 
filldraw stroke z9e--z9'e;
x10l=x10'l=x9'r; x11l=x10l+.25u; y10=y9; y10'=y10+.25h; y11=y10+.75(y10'-y10);
filldraw stroke z10e---z11e{up}..{left}z10'e;
x7=x10r; y7=y10; rt_wing(7,0,-.01h);

% BAR
penpos12(stem,0); x12=x4l; y12=y4+0.15h;
penpos12'(stem,0); x12'=x12; y12'+0.015h=y12;
lft_half_ellipse(12,2u); rt_half_ellipse(12',2u);
penlabels(1,2,3,4); endchar;


iff known eth_GuI:
cmchar "The letter GuI";
beginchar(eth_GuI,8u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(1u#,7.5u#);

pickup fine.nib; penpos1(stem,5); penpos2(stem,0); penpos3(stem,0);
pos4(stem,10);
x1r=w-1.5u; x2r=x1r-.5u; x3r=w-.75u; x4l=w-4u;
bot y1=0; y2=.1h; y3=.75h; y4=.95h;
filldraw stroke z1e{left}...{up}z2e---z3e{up}..{left}z4e;
z5=z4l; lft_wing(5,.75u,.025h);

pos9(vair,90); pos9'(vair,90); penpos10(stem,0); 
penpos10'(stem,0); penpos11(stem,0);
x9=.5[x1r,x4r]+fine; x9'=x9+2.5u; y9=y9'=y1+.6(y4-y1); 
filldraw stroke z9e--z9'e;
x10l=x10'l=x9'r; x11l=x10l+.5u; y10=y9-.15h; y10'=y9+.2h; 
y11=y10+.75(y10'-y10);
filldraw stroke z10e---z11e{up}..{left}z10'e;
x7=x10r; y7=y10; rt_wing(7,1.5u,-.01h);

% BAR
penpos12(stem,0); x12=x4l; y12=y4+0.15h;
penpos12'(stem,0); x12'=x12; y12'+0.015h=y12;
lft_half_ellipse(12,2u); rt_half_ellipse(12',2u);
penlabels(1,2,3,4); endchar;


iff known eth_GuA:
cmchar "The letter GuA";
beginchar(eth_GuA,7u#,.5[cap_height#,x_height#],0);
adjust_fit(1.5u#,1.5u#);
if .5w <> good.x .5w :change_width; fi pickup fine.nib;

pickup fine.nib; penpos2(stem,0); penpos3(stem,0); pos4(stem,10);
x2r=w-2u; x3r=w-.75u; x4l=w-4u; y2=.55h; y3=.85h; y4=.95h;
filldraw stroke z2e..z3e{up}..{left}z4e;
z5=z4l; lft_wing(5,u,.02h);
x6=x2; bot y6=y2; lft_wing(6,2u,.055h);

penpos10(stem,0);  penpos11(stem,0);  penpos10'(stem,0);  penpos11'(stem,0);
x10l=x2r; x11'l=x10l-.25u; x11l=x11'l+.5u; x10'l=x10l+u; 
y10=y2; bot y11=00; y10'=.85(y10-y11)+y11;  y11'=.15(y10-y11)+y11;
filldraw stroke z10e{right}..{down}z10'e--z11'e{down}..{right}z11e;

% BAR
penpos12(stem,0); x12=x4l; y12=y4+0.15h;
penpos12'(stem,0); x12'=x12; y12'+0.015h=y12;
lft_half_ellipse(12,2u); rt_half_ellipse(12',2u);
penlabels(1,2,3,4,10,10',11,11'); endchar;


iff known eth_GuE:
cmchar "The letter GuE";
beginchar(eth_GuE,8u#,.5[cap_height#,x_height#],paren_depth#);
adjust_fit(2u#,2u#);

pickup fine.nib; penpos2(1.2stem,5); penpos3(stem,0); pos4(stem,10);
x2r=w-3u; x3r=w-.75u; x4l=w-4u; y2=.15h; y3=.775h; y4=.95h;
filldraw stroke z2e---z3e{up}..{left}z4e;
z5=z4l; lft_wing(5,1.5u,.025h);
x6=x2r-.25u; x7=x2l; y6=y7=y2;  half_ring(6); lft_wing(7,2u,.05h);

% BAR
penpos12(stem,0); x12=x4l; y12=y4+0.15h;
penpos12'(stem,0); x12'=x12; y12'+0.015h=y12;
lft_half_ellipse(12,2u); rt_half_ellipse(12',2u);
penlabels(1,2,3,4); endchar;





