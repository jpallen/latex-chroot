%%%%%%%%%%%%%%%%%%%%%%% nashspec.mf %%%%%%%%%%%%%%%%%%%%%%%%%%%
% NASH Font
% special characters
% 26.02.92
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% (c) Klaus Lagally
%     Institut fuer Informatik
%     Universitaet Stuttgart
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

if unknown nashbase: input nashbase fi;

%%%%%%%%%%%%%% very special characters %%%%%%%%%%%%%%%%%

arabchar(space_spec, f_beg, 5, 8, 6);
endchar;

arabchar(space_spec, f_mid, 0, 8, 6);
endchar;

arabchar(tatwil, f_iso, 5, 5, 0);
draw z.r--z.l;
endchar;

arabchar(tatwil, f_beg, 5, 5, 0);
draw z.r--z.l;
endchar;

arabchar(tatwil, f_mid, 5, 5, 0);
draw z.r--z.l;
endchar;

arabchar(tatwil, f_end, 5, 5, 0);
draw z.r--z.l;
endchar;

arabchar(block, f_iso, 10, 10, 0); % block isoliert
x1 = x2 = x.l + dx; x3 = x4 = x.r - dx;
y1 = y4 = 10dy; y2 = y3 = 0;
fill z1--z2--z3--z4--cycle;
endchar;

arabchar(block, f_beg, 10, 10, 0); % block initial
x1 = x2 = x.l + dx; x3 = x4 = x.m;
%x1 = x2 = x.l + dx; x3 = x4 = x.r - dx;
y1 = y4 = 10dy; y2 = y3 = 0;
fill z1--z2--z3--z4--cycle;
endchar;

arabchar(block, f_mid, 10, 10, 0); % block medial
x1 = x2 = x.l + dx; x3 = x4 = x.r - dx;
y1 = y4 = 5dy; y2 = y3 = 0;
%y1 = y4 = 10dy; y2 = y3 = 0;
fill z1--z2--z3--z4--cycle;
endchar;

arabchar(block, f_end, 10, 10, 0); % block final
x1 = x2 = x.m; x3 = x4 = x.r - dx;
%x1 = x2 = x.l + dx; x3 = x4 = x.r - dx;
y1 = y4 = 10dy; y2 = y3 = 0;
fill z1--z2--z3--z4--cycle;
endchar;

%arabchar(0, 0, 10, 15, 0); % block in position 0
%x1 = x2 = x.l + dx; x3 = x4 = x.r - dx;
%y1 = y4 = 10dy; y2 = y3 = 0;
%fill z1--z2--z3--z4--cycle;
%endchar;

%%%%%%%%%%%%%% special characters %%%%%%%%%%%%%%%%%

arabchar(".", 0, 2, 2, -1.5);
x1 = x.m; y1 = dy;
put_dot(1);
endchar;

arabchar(",", 0, 4, 10, 0);
x1 = x2 = x.m; y1 = 6dy; y2 = dy;
%x1 = x2 = x.m; y1 = 10dy; y2 = 5dy;
draw z1{(-dx,-dy)}..z2{right};
endchar;

arabchar(";", 0, 4, 10, 0);
x1 = x2 = x.m; y1 = 10dy; y2 = 5dy;
draw z1{(-dx,-dy)}..z2{right}; 
one_dot(1);
endchar;

arabchar(":", 0, 4, 10, 0);
x1 = x2 = x.m; y1 = dy; y2 = 6dy;
put_dot(1); put_dot(2);
endchar;

arabchar("!", 0, 4, 15, 0);
x1 = x2 = x3 = x.m; y1 = dy; y2 = 6dy; y3 = 13dy;
put_dot(1); draw z3--z2;
endchar;

arabchar("?", 0, 7, 15, 0);
x1 - 2dx = x3 + 2dx = x2 = x4 = x.m;
y1 = y3 = 10dy; y2 = 13dy; y4 = 4dy;
draw z1..z2{left}..z3..z4{(-4dx,-11dy)};
one_dot(1);
endchar;

arabchar("$", 0, 6, 10, 5);
x1 = x2 = x.m; x3 = x5 = x.m + 2dx; x4 = x6 = x.m - 2dx;
y1 = 8dy; y2 = -6dy; y3 = y4 = 3dy; y5 = y6 = -dy;
draw z1--z2; draw z3..z4..z5..z6;
endchar;

arabchar("*", 0, 8, 10, 0);
x1 = x2 = x.m; x3 = x5 = x.m + 3dx; x4 = x6 = x.m - 3dx;
y1 = 7dy; y3 = y6 = 5dy; y4 = y5 = dy; y2 = -dy;
draw z1--z2; draw z3--z4; draw z5--z6;
endchar;

arabchar("%", 0, 14, 10, 5);
x1 = x5 = x6 = x.m + 4dx;
x2 = x3 = x4 = x.m - 4dx;
y1 = y3 = 8dy; y4 = 3dy; y5 = dy; y2 = y6 = -4dy;
draw z3..z4..cycle; draw z5..z6..cycle;
pickup dia_pen; draw z1--z2; 
endchar;

arabchar("/", 0, 10, 10, 5);
%arabchar("/", 0, 10, 15, 7);
pickup dia_pen;
x1 + 4dx = x2 - 4dx = x.m;
y2 = 9dy; y1 = -6dy;
%y2 = 14dy; y1 = -6dy;
draw z1--z2;
endchar;

arabchar(rquotes, 0, 8, 10, 5); % ``
x1 = x3 = x2 + 2dx = x.m - dx;
x4 = x6 = x5 + 2dx = x.m + 3dx;
y1 = y4 = 5dy; y2 = y5 = dy; y3 = y6 = -3dy;
draw z1..z2..z3; draw z4..z5..z6;
endchar;

arabchar(lquotes, 0, 8, 10, 5); % ''
x1 = x3 = x2 - 2dx = x.m + dx;
x4 = x6 = x5 - 2dx = x.m - 3dx;
y1 = y4 = 5dy; y2 = y5 = dy; y3 = y6 = -3dy;
draw z1..z2..z3; draw z4..z5..z6;
endchar;

arabchar("=", 0, 16, 10, 0);
x5 = x7 = x.m - 7dx; x6 = x8 = x.m + 7dx;
y5 = y6 = 3dy; y7 = y8 = -dy;
draw z5--z6; draw z7--z8;
endchar;

arabchar("+", 0, 10, 10, 0);
x1 = x2 = x3 + 4dx = x4 - 4dx = x.m;
y1 = 5dy; y2 = -3dy; y3 = y4 = dy;
draw z1--z2; draw z3--z4;
endchar;

arabchar("-", 0, 8, 10, 0);
x1 + 3dx = x2 - 3dx = x.m;
y1 = y2 = dy;
%y1 = y2 = 4dy;
draw z1--z2;
endchar;

arabchar("#", 0, 16, 10, 5);
x1 = x4 = x3 - 4dx = x2 + 4dx = x.m;
x5 = x7 = x.m - 7dx; x6 = x8 = x.m + 7dx;
y1 = y3 = 7dy; y2 = y4 = -7dy;
y5 = y6 = 2dy; y7 = y8 = -2dy;
draw z5--z6; draw z7--z8;
pickup dia_pen;
draw z1--z2; draw z3--z4;
endchar;

arabchar("(", 0, 8, 10, 5);
x1 = x2 = x.m + 3dx; x3 = x.m - 3dx;
top y1 = 10dy; bot y2 = -7dy; y3 = .5[y1,y2];
%top y1 = 15dy; bot y2 = -7dy; y3 = 4dy;
draw z1..z3..z2;
endchar;

arabchar(")", 0, 8, 10, 5);
x1 = x2 = x.m - 3dx; x3 = x.m + 3dx;
top y1 = 10dy; bot y2 = -7dy; y3 = .5[y1,y2];
draw z1..z3..z2;
endchar;

arabchar("[", 0, 6, 10, 5);
x1 = x2 = x.m + 2dx; x3 = x4 = x.m - 2dx;
y1 = y3 = 9dy; y2 = y4 = -6dy;
%y1 = y3 = 14dy; y2 = y4 = -6dy;
draw z1--z3--z4--z2;
endchar;

arabchar("]", 0, 6, 10, 5);
x1 = x2 = x.m + 2dx; x3 = x4 = x.m - 2dx;
y1 = y3 = 9dy; y2 = y4 = -6dy;
draw z3--z1--z2--z4;
endchar;

%%%%%%%%%%%%%% EOF %%%%%%%%%%%%%%%%%


