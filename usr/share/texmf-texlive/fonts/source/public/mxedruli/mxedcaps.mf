% Mxedruli CAPITALS `mxedcaps.mf'
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Designed and (c) by Johannes Heinecke                                     %
%                     Humboldt-Universit"at zu Berlin                       %
%                     Philosophische Faskult"at II                          %
%                     Institut f"ur deutsche Sprache und Linguistik         %
%                     Computerlinguistik                                    %
%                     J"agerstr. 10/11                                      %
%                     D - 10099 Berlin                                      %
%                     <heinecke@compling.hu-berlin.de>                      %
% Please send any improvements, corrections, suggestions, enhancements etc  %
% to the Author!                                                            %
%                                                                           %
% Version 2.3 as of 20th May 1996                                           %
%                                                                           %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%% Character Definitions: %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


%% Glottal Stop %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "' - Glottal Stop";
beginchar(oct"021", mxzw#+mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pencc;
   top lft z1 = (mxone, mxheight);
   lft z2 = (mxone, 1.5mxlittle);
   lft z3 = (mxhalf, mxlittle);
   lft z4 = (0, y2);
   z5 = (x4, y1);
   draw z1..{down}z2..{left}z3..{up}z4..z5;
 
   z6 = (x4, .5mxlittle);
   bot z7 = (x3, 0);
   z8 = (x2, y6);
   draw z4--z6{down}..{right}z7..{up}z8;
   penlabels(range 1 thru 8);
endchar;
 
%% qh %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% Contributed by Mark Leisher and adapted by J Heinecke
mxedchar "q - qhar (oct015)";
beginchar(oct"015", mxzw#+mxone#+.5mxhalf#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pencc;
   lft z1 = (0, mxthird);
   bot lft z2 = (mxhalf, 0);
   z4 = (x2, mxlittle);
   lft z3 = (mxone, .67mxlittle);
   z31 = (x3, .33mxlittle);
   lft z5 = (0, .7mxlittle);
   draw z1{down}..{right}z2..{up}z31..{up}z3..{left}z4..z5;
 
   z6 = (x3, 1.5mxlittle);
   z7 = (x4-.25mxthird, 1.75mxlittle);
   z8 =(x6, mxheight);
   draw z4{right}..{up}z6..{dir 90}z7..z8;
 
   penlabels(range 1 thru 8);
endchar;
 


%% long ee %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% Contributed by Mark Leisher and adapted by J Heinecke
mxedchar "ee - (Long e) oct(016)";
beginchar(oct"016", mxzw#+1.5mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   lft top z1 = (0, mxheight);
   lft z2 = (0, mxthird);
   bot z25 = (mxone, 0);
   z3 = (1.5mxone, y2);
   z4 = (1.2mxone, .5mxlittle);
   z5 = (.75mxone, 2/3mxlittle);
   draw z1..z2{down}..{right}z25..z3..z4..z5;
 
   z6 = (x3, 3/4mxlittle);
   z7 = (x4, mxlittle);
   z8 = (x5, 7/6mxlittle);
   draw z4..z6..z7--z8;
 
   z9 = (x6, 17/12mxlittle);
   top z10 = (x5, mxheight);
   draw z7..z9..{up}z10;
 
   penlabels(range 1 thru 10);
endchar;


%% a %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "a - an";
beginchar("a", mxzw#+mxone#, mxheight#, 0);
  if known cmbase: adjust_fit(0,0); fi
  pickup mxed_pencc;
  lft z1 = (0, mxlittle);
  bot lft z2 = (mxhalf, 0);
  lft z3 = (mxone, y1);
  top z4 = (x2, mxheight);
  draw z1{down}..z2{right}..{up}z3..{up}z4;
  penlabels(range 1 thru 4);
endchar;

%% ae %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "e1 - ae (Ossetian), oct013";
beginchar(oct"013", mxzw#+mxone#, mxheight#, 0);
  if known cmbase: adjust_fit(0,0); fi
  pickup mxed_pencc;
  lft z1 = (0, mxlittle);
  bot lft z2 = (mxhalf, 0);
  lft z3 = (mxone, y1);
  top z4 = (.25mxhalf, mxheight); % x4 war x2
  z5 = (x4+mxhalf, y4);
  draw z1{down}..z2{right}..{up}z3..{up}z4..{down}z5;
  penlabels(range 1 thru 5);
endchar;


%% i %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "i - in";
beginchar("i", mxzw#+mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pencc;
   bot lft z1 = (0, 0);
   lft z2 = (0, mxlittle);
   top lft z3 = (mxhalf, mxheight);
   lft z4 = (mxone, y2);
   lft z5 = (mxone, y1);
   draw z1..{up}z2..{right}z3..{down}z4..z5;
   penlabels(range 1 thru 5);
endchar;


%% oo %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "oo - ho, (Old Georgian), oct011";
beginchar(oct"011", mxzw#+mxone#+mxhalf#, mxheight#, 0);
  if known cmbase: adjust_fit(0,0); fi
  pickup mxed_pencc;
  lft z1 = (mxone+mxhalf, .9mxlittle);
  z8 = (x1, 1.1mxlittle);
  lft z2 = (mxhalf, y1);
  z7 = (x2, y8);
  bot z3 = (x2, 0);
  top z6 = (x2, mxheight);
  lft z4 = (mxone, .8mxthird);
  z5 = (x4, mxheight-.8mxthird);
  draw z1--z2{left}..z3..{up}z4--z5{up}..z6..{right}z7--z8;
  penlabels(range 1 thru 8);
endchar;


 
%% o %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "o - on";
beginchar("o", mxzw#+1.6mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pencc;
   bot lft z1 = (0, 0);
   z2 = (x1, 1.5mxlittle);
   top lft z3 = (.8mxhalf, mxheight);
   lft z4 = (.8mxone, mxlittle);
   z41 = (x4, 1.5mxlittle);
   lft z5 = (.8mxone+.8mxhalf, y3);
   lft z6 = (1.6mxone, y2);
   z7 = (x6, y1);
   draw z1..{up}z2..{right}z3..{down}z41--z4;
   draw z4--z41{up}..{right}z5..{down}z6..z7;
   penlabels(range 1 thru 7, 41);
endchar;

%% r %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "r - ran";
beginchar("r", mxzw#+1.6mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pencc;
   bot lft z1 = (0, 0);
   z2 = (x1, .5mxlittle);
   lft z3 = (.8mxhalf, .5mxheight);
   lft z4 = (.8mxone, .5mxlittle);
   lft z5 = (.8mxone+.8mxhalf, y3);
   lft z6 = (1.6mxone, y2);
   z7 = (x6, y1);
   top z8 = (x6, mxheight);
   draw z1..{up}z2..{right}z3..{down}z4;
   draw z4{up}..{right}z5..{down}z6..z7;
   draw z2{up}..z8;
   penlabels(range 1 thru 8);
endchar;
 
 
 
%% b %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "b - ban";
beginchar("b", mxzw#+mxone#, mxheight#, 0);
  if known cmbase: adjust_fit(0,0); fi
  pickup mxed_pencc;
  lft z1 = (0, .5mxlittle);
  bot lft z2 = (mxhalf, 0);
  lft z3 = (mxone, y1);
  z4 = (x2, mxlittle);
  draw z1{down}..{right}z2..{up}z3..{left}z4..cycle;

  z5 = (x4-.7mxhalf, mxheight-.7mxhalf);  
  top z6 = (x4, mxheight);
  draw z5--z6--z4;

  penlabels(range 1 thru 7);
endchar;

 

 
%% c %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "c - can";
beginchar("c", mxzw#+mxone#, mxlittle#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pencc;
   top lft z1 = (0, mxheight);
   lft z4 = (mxone, .4mxheight);
   bot z3 = (.5[x1,x4], 0);
   z2 = (x1, y4);
   z5 = (x4-.5mxhalf, .5mxheight);
   draw z1..{down}z2..{right}z3..{up}z4..tension 2..{left}z5;
 
   z6 = (x4, 1.2mxlittle);
   z7 = (x4, y1);
   draw z5{right}..{up}z6..z7;
 
   penlabels(range 1 thru 7);
endchar;
 


%% .c %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar ".c - .cil (oct000)";
beginchar(oct"000", mxzw#+mxone#+mxhalf#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pencc;
   lft z1 = (0, mxheight-.3mxtop);
   lft z9 = (mxone+mxhalf, y1);
   bot z2 = (.5[x1,x9], 0);
   z3 = (.4[z1,z2]);
   z4 = (.4[z9,z2]);
   z7 = (x2, y1);
   top z6 = (.5[x1, x7], mxheight);
   z8 = (.5[x7, x9], y6);
   draw z1--z2--z4--z3;
   draw z1{up}..{right}z6..{down}z7;
   draw z7{up}..{right}z8..{down}z9;
   penlabels(range 1 thru 9);
endchar;

%% ^c %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "^c - ^cin (+c, oct001)";
beginchar(oct"001", mxzw#+mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pencc;
   bot lft z1 = (0, 0);
   lft z2 = (0, .5mxlittle);
   lft z3 = (mxhalf, mxlittle);
   lft z4 = (mxone, y2);
   bot z5 = (x4, 0);
   draw z1..{up}z2..{right}z3..{down}z4..z5;
 
   lft z6 = (0, mxheight-.05mxtop);
   top lft z9 = (mxhalf, mxheight);
   z7 = (x4-.5mxhalf,mxheight-.25mxtop);
   z10 = (x9, mxheight-.5mxtop);
   lft z8 = (0, mxheight-.45mxtop);
   draw z2--z6;
   draw z6..{right}z9..{down}z7..{left}z10..z8;
 
   penlabels(range 1 thru 8);
endchar;

 
%% ^.c %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "^.c - ^.car (,c .+c oct014)";
beginchar(oct"014", mxzw#+1.2mxone#, mxheight#, 0); % war "y"
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pencc;
   lft z1 = (.5mxhalf, .6mxmiddle);
   lft z2 = (0, .6mxthird);
   bot lft z3 = (mxhalf, 0);
   lft z4 = (mxone, y2);
   lft z5 = (.75mxone, 1.3mxthird);
   top z6 = (x5, mxheight);
   z7 = (x1, .9mxheight);
   draw z1{dir -135}..{down}z2..{right}z3..{up}z4..{up}z5--z6--z7;
 
   z8 = (x5, mxlittle); % z10 and z11 should be 45 degree NE/SE
   z9 = (x1, y8);
   z10 = (x5+.7mxhalf, y8+.7mxhalf);
   z11 = (x5+.7mxhalf, y8-.7mxhalf);
   draw z10--z8..{up}z9..z8--z11;
 
   penlabels(range 1 thru 11);
endchar;
 
%% d %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "d - don";
beginchar("d", mxzw#+1.6mxone#, mxlittle#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pencc;
   lft z1 = (.8mxone, 1.5mxlittle);
   lft z2 = (.8mxone+.8mxhalf, mxlittle);
   lft z3 = (1.6mxone, y1);
   top z4 = (x2, mxheight);
   z5 = (x1, mxmiddle+mxlittle);
   draw z1{down}..{right}z2..{up}z3..{left}z4..{down}z5..cycle;
 
   lft z6 = (.8mxhalf, y4);
   lft z7 = (0, y1);
   z8 = (x7, mxlittle);
   draw z5{up}..{left}z6..{down}z7..z8;
 
   bot z9 = (x3, 0);
   draw z8..{dir -75}z9;
   penlabels(range 1 thru 10);
endchar;
 
%% e %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "e - en";
beginchar("e", mxzw#+mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pencc;
   lft z1 = (0, .4mxlittle);
   bot lft z2 = (mxhalf, 0);
   lft z3 = (mxone, mxlittle);
   z4 = (x3, mxmiddle+mxlittle);
   top z5 = (x2, mxheight);
   lft z6 = (0, y4);
   draw z1{down}..{right}z2..{up}z3--z4..{left}z5..z6;
 
   penlabels(range 1 thru 6);
endchar;
 
%% g %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "g - gan";
beginchar("g", mxzw#+mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pencc;
   bot lft z2 = (mxhalf, 0);
   z4 = (x2, mxlittle);
   lft z1 = (0, .5[y2,y4]); % y was -.5mxlow
   lft z3 = (mxone, y1);
   draw z1{down}..{right}z2..{up}z3..{left}z4..cycle;
 
   z5 = (x3-.3mxhalf, 1.5mxlittle);
   top z6 = (x4, mxheight);
   lft z7 = (.3mxhalf, y5);
   draw z4{right}..{up}z5..{left}z6..{down}z7;
   penlabels(range 1 thru 7);
endchar;
 
 
 
%% .g %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar ".g - .gan (oct002)";
beginchar(oct"002", mxzw#+1.6mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pencc;
   lft z1 = (0, mxlittle);
   lft z2 = (0, 1.5mxlittle);
   top lft z3 = (.8mxhalf, mxheight);
   lft z4 = (.8mxone, mxmiddle+mxlittle);
   lft z5 = (.8mxone+.8mxhalf, y3);
   lft z6 = (1.6mxone, y2);
   z7 = (x6, y1);
   bot z8 = (x6, 0);
   draw z1..{up}z2..{right}z3..{down}z4;
   draw z4{up}..{right}z5..{down}z6..z7;
   draw z1..{dir -75}z8;
   penlabels(range 1 thru 8);
endchar;

 
%% h %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "h - ha";
beginchar("h", mxzw#+mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pencc;
   lft z1 = (0, mxthird);
   bot lft z2 = (mxhalf, 0);
   lft z3 = (mxone, .5mxlittle);
   lft z4 = (.75mxone, 5/6mxlittle);
   lft z5 = (.75mxhalf, y4);
   draw z1{dir -80}..{right}z2..{up}z3..{left}z4--z5;
 
   z6 = (x3, mxlittle);
   z7 = (x4, 7/6mxlittle);
   z8 = (x5, y7);
   draw z4..{up}z6..{left}z7--z8;
 
   z9 = (x6, 8/6mxlittle);
   top lft z10= (0, mxheight);
   draw z7..{up}z9..{dir 110}z10;
 
   penlabels(range 1 thru 10);
endchar;
 
 
%% ^dz %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "^dz/^j - ^jan (oct010)";
beginchar(oct"010", mxzw#+2mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pencc;
   lft z1 = (0, mxthird);
   lft z3 = (1.5mxhalf, y1);
   z2 = (.5[x1,x3], 0);
   lft z4 = (1.2mxhalf, .5mxlittle);
   top lft z5 = (2mxone, mxheight);
   draw z1{down}..{right}z2..{up}z3..z4;
   draw z4--z5;
 
   lft z6 = (.3mxhalf, 11/12mxheight);
   top lft z7 = (mxhalf, mxheight);
   lft z8 = (1.75mxone, .5mxlittle);
   z9 = (x5, y8+1/12mxheight);
   draw z6{up}..{right}z7..{right}z8..z9;
 
   penlabels(range 1 thru 9);
endchar;
 
 
%% dz %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "dz (j) - dzil";
beginchar("j", mxzw#+mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pencc;
   lft z1 = (0, .5mxlittle);
   bot lft z2 = (mxhalf, 0);
   lft z3 = (mxone, y1);
   z4 = (x2, mxlittle);
   draw z1{down}..{right}z2..{up}z3..{left}z4..cycle;
 
   top z5 = (x3, mxheight);
   draw z3--z5;
   penlabels(range 1 thru 5);
endchar;
 
 
%% k %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "k - kan";
beginchar("k", mxzw#+mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pencc;
   lft z1 = (0, .8mxlittle);
   lft z2 = (mxhalf, mxlittle);
   lft z3 = (mxone, .8mxlittle);
   z4 = (x3, .2mxlittle);
   bot z5 = (x2, 0);
   z6 = (x1, y4);
   top z7 = (x3, mxheight);
   z8 = z7 - (mxhalf, mxthird);
   draw z1..z2..{down}z3--z4{down}..{left}z5..{up}z6;
   draw z3--z7--z8;
   penlabels(range 1 thru 8);
endchar;
 
 

mxedchar ".k - .kan (oct003)";
beginchar(oct"003", mxzw#+mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pencc;
   lft z1 = (0, mxthird);
   bot lft z2 = (mxhalf, 0);
   z4 = (x2, mxlittle);
   lft z3 = (mxone, .67mxlittle);
   z31 = (x3, .33mxlittle);
   lft z5 = (0, .7mxlittle);
   draw z1{down}..{right}z2..{up}z31..{up}z3..{left}z4..z5;
 
   z6 = (x3, 1.33mxlittle);
   top z7 = (x6, mxheight);
   draw z4{right}..{up}z6..z7;
 
   penlabels(range 1 thru 7, 31);
endchar;
 
 
 
%% l %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "l - las";
beginchar("l", mxzw#+2.4mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pencc;
   lft z1 = (0, mxlittle);
   lft z2 = (0, 1.5mxlittle);
   top lft z3 = (.8mxhalf, mxheight);
   lft z4 = (.8mxone, 1.5mxlittle);
   lft z9 = (.8mxone+.8mxhalf, y3);
   lft z10= (1.6mxone, y4);
   lft z5 = (1.6mxone+.8mxhalf, y3);
   lft z6 = (2.4mxone, y2);
   lft z7 = (2.4mxone, y1);
   z8 = (x6, 0);
   draw z1..{up}z2..{right}z3..{down}z4;
   draw z4{up}..{right}z9..{down}z10;
   draw z10{up}..{right}z5..{down}z6..z7;
   draw z1..{dir -50}z8;
   penlabels(range 1 thru 10);
endchar;
 
 
%% m %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "m - man";
beginchar("m", mxzw#+mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pencc;
   lft z1 = (0, .5mxlittle);
   bot lft z2 = (mxhalf, 0);
   lft z3 = (mxone, y1);
   z4 = (x2, mxlittle);
   draw z1{down}..{right}z2..{up}z3..{left}z4..cycle;
 
   z5 = (x3, mxheight-.5mxtop);
   z6 = (x4, mxheight);
   lft z7 = (0, y5);
   draw z3..z5{up}..{left}z6..{down}z7;
   penlabels(range 1 thru 7);
endchar;
 
 
%% n %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "n - nar";
beginchar("n", mxzw#+mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pencc;
   lft z1 = (0, .5mxlittle);
   bot lft z2 = (mxhalf, 0);
   lft z3 = (mxone, y1);
   z4 = (x2, mxlittle);
   draw z1{down}..{right}z2..{up}z3..{left}z4..cycle;
 
   lft z5 = (0, mxheight-.25mxtop);
   top lft z6 = (.5mxhalf, mxheight);
   z7 = (x3, y6+.2mxtop);
   draw z1..z5{up}..{right}z6..{dir 75}z7;
   penlabels(range 1 thru 7);
endchar;
 
 
 
%% p %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "p - par";
beginchar("p", mxzw#+1.6mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pencc;
   lft z1 = (0, 1.5mxlittle);
   lft z2 = (.8mxhalf, mxlittle);
   lft z3 = (.8mxone, y1);
   z4 = (x3, mxlittle+mxmiddle);
   top z5 = (x2, mxheight);
   draw z1{down}..{right}z2..{up}z3..z4{up}..{left}z5..cycle;
 
   lft z6 = (.8mxhalf+.8mxone, y5);
   lft z7 = (1.6mxone, y1);
   bot z8 = (x6, 0);
   z9 = (x7, .5mxlittle);
   lft z11= (.6mxone, y9);
   bot z10= (.5[x11,x9], 0);
%   draw z4{up}..{right}z6..{down}z7..{left}z8;
%   draw z8{right}..{down}z9..{left}z10..{dir 70}z11;
   draw z4{up}..{right}z6..{down}z7..{down}z9..
      {left}z8..{left}z10..{up}z11;
   penlabels(range 1 thru 11);
endchar;
 
%% .p %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar ".p - .par (oct004)";
beginchar(oct"004", mxzw#+mxone#, mxthird#+mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pencc;
   lft z1 = (0, mxthird);
   bot lft z2 = (mxhalf, 0);
   lft z3 = (mxone, .5mxlittle);
   z4 = (x2, mxlittle);
   z5 = (mxhalf, y4);
   draw z1{dir -80}..{right}z2..{up}z3..{left}z4--z5;
 
   z6 = (x3, mxlittle+.4mxtop);
   z7 = (x1, .2mxtop+mxheight);
   draw z4..{up}z6..{dir 110}z7;
 
   penlabels(range 1 thru 7);
endchar;
 
 
%% q %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "q - qar";
beginchar("q", mxzw#+mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pencc;
   top lft z1 = (0, mxheight);
   lft z2 = (0, 1.5mxlittle);
   lft z3 = (mxhalf, mxlittle);
   lft z4 = (mxone, y2);
   z5 = (x4, y1);
   draw z1..{down}z2..{right}z3..{up}z4..z5;
 
   z6 = (x4, .5mxlittle);
   bot z7 = (x3, 0);
   z8 = (x2, y6);
   draw z4--z6{down}..{left}z7..{up}z8;
   penlabels(range 1 thru 8);
endchar;
 
 
%% s %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "s - san";
beginchar("s", mxzw#+mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pencc;
   top lft z5 = (0, mxheight);
   lft z1 = (0, .5mxlittle);
   bot lft z2 = (mxhalf, 0);
   lft z3 = (mxone, y1);
   lft z4 = (1/3mxone, mxlittle);
   draw z5--z1{down}..{right}z2..{up}z3..{up}z4;
 
   penlabels(range 1 thru 5);
endchar;
 
 
 
%% ^s %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "^s - ^san (+s, oct005)";
beginchar(oct"005", mxzw#+mxhalf#+mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pencc;
   lft z1 = (mxhalf, .5mxlittle);
   bot lft z2 = (mxhalf+mxhalf, 0);
   lft z3 = (mxone+mxhalf, y1);
   z4 = (x2, mxlittle);
   draw z1{down}..{right}z2..{up}z3..{left}z4..cycle;
 
   z5 = (x3, mxheight-.5mxtop);
   top z6 = (x4, mxheight);
   lft z7 = (mxhalf, y5);
   draw z3..z5{up}..{left}z6..{down}z7;
 
   lft z8 = (0, y6);
   draw z7{up}..{left}z8;
   penlabels(range 1 thru 8);
endchar;
 
 
%% t %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "t - tan";
beginchar("t", mxzw#+1.6mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pencc;
   lft z1 = (0, mxlittle);
   bot lft z2 = (.8mxhalf, 0);
   lft z3 = (.8mxone, y1);
   z4 = (x3, mxlittle+mxmiddle);
   top z5 = (x2, mxheight);
   draw z1{down}..tension 1.3..{right}z2..tension 1.3..{up}z3..z4{up}..{left}z5..tension 1.3..cycle;
 
   lft z6 = (.8mxhalf+.8mxone, y5);
   lft z7 = (1.6mxone, y1);
   bot z8 = (x7, 0);
   draw z4{up}..{right}z6..tension 1.3..{down}z7..z8;
   penlabels(range 1 thru 8);
endchar;
 
 
%% .t %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar ".t - tar (oct006)";
beginchar(oct"006", mxzw#+mxone#+mxhalf#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pencc;
   lft z1 = (0, .8mxlittle);
   bot lft z2 = (.5mxhalf+.5mxone, 0);
   lft z3 = (mxone+mxhalf, y1);
   z4 = (x2, .8mxheight);
   draw z1{down}..{right}z2..{up}z3..{left}z4..cycle;
 
   bot z5 = (x4, y4-.2mxheight);
   top z6 = (x4, y4+.2mxheight);
   draw z4{left}..{right}z5..cycle;
   draw z4{left}..{right}z6..cycle;
   penlabels(range 1 thru 6);
endchar;
 
 
 
%% u %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "u - un";
beginchar("u", mxzw#+2.2mxone#, mxlittle#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pencc;
   top lft z1 = (0, mxheight);
   lft z5 = (mxone, y1);
   z2 = (.5[x1,x5], 1.75mxlittle);
   lft z6 = (1.4mxone, 1.5mxlittle);
   z7 = (x6, mxlittle);
   draw z1..z2..{right}z5..{down}z6--z7;
 
   lft z8 = (1.4mxone+.8mxhalf, y5);
   lft z9 = (2.2mxone, y6);
   z10= (x9, .5mxlittle);
   bot z11= (x6, 0);
   z12= (x2, y10);
   draw z6{dir 75}..{right}z8..{down}z9--z10...{left}z11..{up}z12;
   penlabels(range 1 thru 12);
endchar;
 
 
%% v %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "v - vin";
beginchar("v", mxzw#+mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pencc;
   lft z1 = (0, mxlittle+mxmiddle);
   top lft z2 = (mxhalf, mxheight);
   lft z3 = (mxone, .5[y4,y2]);
   z4 = (x2, mxlittle);
   draw z1{up}..{right}z2..{down}z3..{left}z4; % ..z5;
 
   z6 = (x3, .5[y4,y7]);
   bot z7 = (x2, 0);
   lft z8 = (0, y6);
   draw z4{right}..{down}z6..{left}z7..{up}z8;
 
   penlabels(range 1 thru 8);
endchar;
 
 
%% x %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "x - xan";
beginchar("x", mxzw#+mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pencc;
   lft z1 = (0, .5mxlittle);
   bot lft z2 = (mxhalf, 0);
   lft z3 = (mxone, y1);
   z4 = (x2, 5/6mxlittle);
   z6 = (x1, y4);
   draw z1{down}..{right}z2..{up}z3..{left}z4..z6;
 
   top lft z5 = (0, mxheight);
   draw z1--z5;
 
   z7 = z6 +(1mxu, 1mxu);
   draw z6--z7;
   penlabels(range 1 thru 7);
endchar;
 
 
%% z %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "z - zen";
beginchar("z", mxzw#+2mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pencc;
   lft z1 = (.75mxone, .5mxlittle);
   bot lft z2 = (.75mxone+mxhalf, 0);
   lft z3 = (.75mxone+mxone, y1);
   z4 = (x2, 5/6mxlittle);
   z6 = (x1, y4);
   draw z1{down}..{right}z2..{up}z3..{left}z4..z6;
 
   lft z5 = (.75mxone, mxheight-.5mxtop);
   top z11= (x5, mxheight);
   lft z8 = (mxhalf, y11);
   lft z9 = (0, y5);
   z10 =(.5[x9,x5], mxheight-mxtop);
%   draw z1--z11..{left}z8..{down}z9..{right}z10..{up}z5;
   draw z1--z5{up}..{left}z8..{down}z9..{right}z10..{up}z5;

   z7 = z6 + (1mxu, 1mxu);
   draw z6--z7;
   penlabels(range 1 thru 11);
endchar;
 

 
%% ^z %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "^z - ^zan (+z, oct007)";
beginchar(oct"007", mxzw#+mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pencc;
   lft z1 = (0, 1.5mxlittle);
   lft z2 = (.5mxhalf, mxlittle);
   lft z3 = (mxhalf, y1);
   top lft z4 = (mxone, mxheight);
   draw z1{down}..{right}z2..{up}z3..{up}z4;
 
   z5 = (x4, .5mxlittle);
   bot z6 = (x3, 0);
   lft z7 = (0, y5);
   draw z4--z5{down}..{left}z6..{up}z7;
   penlabels(range 1 thru 7);
endchar;


%% f %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "f, (Ossetian)";
beginchar("f", mxzw#+mxone#+mxhalf#, mxheight#, 0);
  if known cmbase: adjust_fit(0,0); fi
  pickup mxed_pencc;
  lft z1 = (0, mxlittle);
  lft z2 = (1.5mxone, y1);
  draw z1..z2..cycle;

  lft z3 = (0, .8mxheight);
  top lft z31 = (1/3mxone, mxheight);
  lft z4 = (1.5mxhalf, y1);
  bot lft z51 = (7/6mxone, 0);
  lft z5 = (1.5mxone, .2mxheight);
  draw z3{up}..{right}z31..{down}z4..{right}z51..{up}z5;

  penlabels(range 1 thru 5, 31, 51);
endchar;

%% i1 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "i1 - short i, (Old Georgian), oct012";
beginchar(oct"012", mxzw#+mxone#, mxheight#, 0);
  if known cmbase: adjust_fit(0,0); fi
  pickup mxed_pencc;
  bot lft z1 = (0, 0);
  lft z2 = (mxhalf, .5mxthird);
  top z4 = (x2, mxheight);
  lft z3 = (mxone, .5[y2,y4]);
  lft z5 = (0, y3);
  z6 = (x3, y1);
  draw z2..z3..z4..z5..cycle;
  draw z1..z2..z6;
  penlabels(range 1 thru 6);
endchar;

%% w %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "w - wi, (Old Georgian)";
beginchar("w", mxzw#+mxone#, mxheight#, 0);
  if known cmbase: adjust_fit(0,0); fi
  pickup mxed_pencc;
  top lft z1 = (.5mxhalf, mxheight);
  lft z2 = (mxone, y1);
  z3 = (x1, mxlittle);
  z4 = (x2, .5mxlittle);
  bot lft z5 = (mxhalf, 0);
  lft z6 = (0, mxthird);
  draw z1--z2--z3{dir 30}..{down}z4..{left}z5..z6;
  penlabels(range 1 thru 6);
endchar;

%% Schwa %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "y - Schwa, (Ossetian)";
beginchar("y", mxzw#+mxone#, mxheight#, 0);
  if known cmbase: adjust_fit(0,0); fi
  pickup mxed_pencc;
  lft z1 = (0, 1.5mxlittle);
  lft z2 = (mxone, y1);
  top z12 = (.5[x1,x2], mxheight);
  z3 = (x1, .5mxlittle);
  z4 = (x2, y3);
  bot z34 = (x12, 0);
  draw z1{up}..{right}z12..{down}z2..{down}z3..{right}z34..{up}z4;
  penlabels(range 1 thru 4, 12, 34);
endchar;


endinput;
