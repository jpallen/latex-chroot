% Mxedruli FONT `mxedfont.mf'
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Designed and (c) by Johannes Heinecke                                     %
%                     Humboldt-Universit"at zu Berlin                       %
%                     Philosophische Faskult"at II                          %
%                     Institut f"ur deutsche Sprache und Linguistik         %
%                     Computerlinguistik                                    %
%                     J"agerstr. 10/11                                      %
%                     D - 10099 Berlin                                      %
%                     <heinecke@compling.hu-berlin.de>                      %
% Please send any improvements, corrections, suggestions, enhancements etc  %
% to the Author!                                                            %
%                                                                           %
% Version 2.3 as of 20th May 1996                                           %
%                                                                           %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%% Character Definitions: %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%% Glottal Stop %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "' - Glottal Stop";
beginchar(oct"021", mxzw#+mxone#, mxlittle#, mxlow#);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z1 = (mxone-mxfeet, mxlittle);
   z2 = (mxone, .5mxlittle);
   z3 = (mxhalf, 0);
   z4 = (0, y2);
   z5 = (mxfeet, y1);
   draw z1..{down}z2..{left}z3..{up}z4..z5;
 
   z6 = (x4, -.5mxlow);
   z7 = (x3, -mxlow);
   z8 = (x2, y6);
   draw z4--z6..{right}z7..{up}z8;
   penlabels(range 1 thru 8);
endchar;


%% qh %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% Contributed by Mark Leisher
mxedchar "q - qhar (oct015)";
beginchar(oct"015", mxzw#+mxone#+.5mxhalf#, mxlittle#, mxlow#);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z1 = (0, -.5mxlow);
   z2 = (mxhalf, -mxlow);
   z3 = (mxone, -.25mxlow);
   z4 = (x2, 1/3mxlittle);
   z5 = (0, 1/6mxlittle);
   draw z1{down}..{right}z2..{up}z3..{left}z4..z5;
 
   z6 = (x3, 1.75mxthird);
   z7 = (x4-.25mxthird, mxlittle);
   z8 =(x6, y7+.5mxthird);
   draw z4{right}..{up}z6..{dir 90}z7..z8;
 
   penlabels(range 1 thru 8);
endchar;
 

%% long ee %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% Contributed by Mark Leisher
mxedchar "ee - (Long e) oct(016)";
beginchar(oct"016", mxzw#+2mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z0 = (0, .5[mxlittle,2/3mxlittle]);
   z1 = (.5mxthird, mxheight-.5mxthird);
   z2 = (0, mxlittle);
   z3 = (.7w, .1mxlittle);
   z4 = (.6w, 2/6mxlittle);
   z5 = (.4w, 3/6mxlittle);
   draw z0{right}..{left}z1..z2{down}..{up}z3..z4--z5;
 
   z6 = (x3+.5mxthird, .6mxlittle);
   z7 = (x4+.5mxthird, 5/6mxlittle);
   z8 = (x5+.5mxthird, mxlittle);
   draw z4..{up}z6..z7--z8;
 
   z9 = (x6+.5mxthird, mxlittle);
   z10= (.5w, mxheight);
   draw z7..{up}z9..{dir 110}z10;
 
   penlabels(range 0 thru 10);
endchar;


%% ae %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "e1 - ae (Ossetian), oct013";
beginchar(oct"013", mxzw#+mxone#, 4mxthird#, 0);
  if known cmbase: adjust_fit(0,0); fi
  pickup mxed_pen;
  z1 = (0, mxthird);
  z2 = (mxhalf, 0);
  z3 = (mxone, y1);
  z4 = (.2mxhalf, mxlittle); % x4 war x2
  z5 = (x4+mxhalf, y4);
  draw z1{down}..z2{right}..{up}z3..{up}z4..{down}z5;
  penlabels(range 1 thru 5);
endchar;



%% a %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "a - an";
beginchar("a", mxzw#+mxone#, mxlittle#, 0);
  if known cmbase: adjust_fit(0,0); fi
  pickup mxed_pen;
  z1 = (0, mxthird);
  z2 = (mxhalf, 0);
  z3 = (mxone, y1);
  z4 = (x2, mxlittle);
  draw z1{down}..z2{right}..{up}z3..{up}z4;
  penlabels(range 1 thru 4);
endchar;
 
 
%% b %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% New Design
mxedchar "b - ban";
beginchar("b", mxzw#+mxone#, mxheight#, 0);
  if known cmbase: adjust_fit(0,0); fi
  pickup mxed_pen;
  z1 = (0, .5mxlittle);
  z2 = (mxhalf, 0);
  z3 = (mxone, y1);
  z4 = (x2, mxlittle);
  draw z1{down}..{right}z2..{up}z3..{left}z4..cycle;
 
  
  top z5 = (x1, mxheight); 
  z6 = (x5, mxheight);
  z7 = (x4, y6-.3mxtop);
  draw z5--z6--z7--z4;

  penlabels(range 1 thru 7);
endchar;

 
%% b1 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "b - ban (Variation, b1)";
beginchar(oct"020", mxzw#+mxone#, mxheight#, 0);
  if known cmbase: adjust_fit(0,0); fi
  pickup mxed_pen;
  z1 = (0, .5mxlittle);
  z2 = (mxhalf, 0);
  z3 = (mxone, y1);
  z4 = (x2, mxlittle);
  draw z1{down}..{right}z2..{up}z3..{left}z4..cycle;
 
% Correction by JDL (8/5/94)
%  z5 = (x3, mxheight);
%  z6 = (0, y5);
%  z7 = (0, y5-.3mxtop);
%  draw z4{up}..{left}z5;
%  draw z5--z6--z7;
  z5 = (0, 0.5[y6,y7]);
  z6 = (0, mxheight);
  z7 = (0, y6-.3mxtop);
  draw z4{up}..{left}z5;	
  draw z6--z7;
% end correction
  penlabels(range 1 thru 7);
endchar;


 
%% c %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "c - can";
beginchar("c", mxzw#+mxone#+.5mxhalf#, mxlittle#, mxlow#);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z1 = (.5mxhalf, mxlittle);
   z2 = (0, .5mxthird);
   z3 = (mxhalf, -mxlow);
   z4 = (mxone+.5mxhalf, -.4mxlow);
   z5 = (x4-mxhalf, y2);
   draw z1..{down}z2..{right}z3..{up}z4..{left}z5;
 
   z6 = (x4, 1.2mxthird);
   z7 = (x5, y1);
   draw z5{right}..{up}z6..{dir 135}z7;
 
   penlabels(range 1 thru 7);
endchar;
 
 
%% %% .c %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% mxedchar ".c - .cil (oct000)";
%% beginchar(oct"000", mxzw#+mxone#+mxhalf#, mxheight#, mxlow#);
%%    if known cmbase: adjust_fit(0,0); fi
%%    pickup mxed_pen;
%%    z1 = (0, mxheight-.5mxtop);
%%    z2 = (.5mxone+.5mxhalf, -mxlow);
%%    z3 = (.4[z1,z2]);
%%    z4 = (mxone+mxhalf, y3);
%%    x5 = .5[x3,x4];
%%    y5 = y4+mxthird;
%%    z6 = (.5[x1, x7], mxheight);
%%    z7 = (x2, y1);
%%    z8 = (.5[x7, x9], y6);
%%    z9 = (x4, y1);
%%    draw z1--z2--z4..{left}z5..{(z2-z1)}z3;
%%    draw z1..{right}z6..{down}z7;
%%    draw z7..{right}z8..{down}z9;
%%    penlabels(range 1 thru 9);
%% endchar;
 

%% .c %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar ".c - .cil (oct000)";
beginchar(oct"000", mxzw#+mxone#+mxhalf#, mxheight#, mxlow#);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z1 = (0, mxheight-.5mxtop);
   z9 = (mxone+mxhalf, y1);
   z2 = (.5[x1,x9], -mxlow);
   z3 = (.4[z1,z2]);
   z4 = (.4[z9,z2]);
   z7 = (x2, y1);
   z6 = (.5[x1, x7], mxheight);
   z8 = (.5[x7, x9], y6);
   draw z1--z2--z4--z3;
   draw z1{up}..{right}z6..{down}z7;
   draw z7{up}..{right}z8..{down}z9;
   penlabels(range 1 thru 9);
endchar;

%% ^c %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "^c - ^cin (+c, oct001)";
beginchar(oct"001", mxzw#+mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z1 = (0, 0);
   z2 = (0, .5mxlittle);
   z3 = (mxhalf, mxlittle);
   z4 = (mxone, y2);
   z5 = (mxone-mxfeet, 0);
   draw z1..{up}z2..{right}z3..{down}z4..z5;
 
   z6 = (0, mxheight-.05mxtop);
   z9 = (mxhalf, mxheight);
   z7 = (x4-.5mxhalf,mxheight-.25mxtop);
   z10 = (x9, mxheight-.5mxtop);
   z8 = (0, mxheight-.45mxtop);
   draw z2--z6;
   draw z6..{right}z9..{down}z7..{left}z10..z8;
 
   penlabels(range 1 thru 8);
endchar;
 
 
%% ^.c %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "^.c - ^.car (,c .+c oct014)";
beginchar(oct"014", mxzw#+1.2mxone#, mxheight#, mxlow#); % war "y"
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z1 = (.5mxhalf, 0);
   z2 = (0, -.5mxlow);
   z3 = (mxhalf, -mxlow);
   z4 = (mxone, y2);
   z5 = (.75mxone, mxthird);
   z6 = (x5, mxheight);
   z7 = (x1, mxheight-.5mxtop);
   draw z1{dir -135}..{down}z2..{right}z3..{up}z4..{up}z5--z6--z7;
 
   z8 = (x5, .5mxlittle); % z10 and z11 should be 45 degree NE/SE
   z9 = (x1, y8);
   z10 = (x5+.7mxhalf, y8+.7mxhalf);
   z11 = (x5+.7mxhalf, y8-.7mxhalf);
   draw z10--z8..{up}z9..z8--z11;
 
   penlabels(range 1 thru 11);
endchar;
 
%% d %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "d - don";
beginchar("d", mxzw#+2mxone#, mxlittle#, mxlow#);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z1 = (mxone, .5mxlittle);
   z2 = (mxone+mxhalf, 0);
   z3 = (mxone+mxone, y1);
   z4 = (x2, mxlittle);
   z5 = (x1, mxmiddle);
   draw z1{down}..{right}z2..{up}z3..{left}z4..{down}z5..cycle;
 
   z6 = (mxhalf, y4);
   z7 = (0, y1);
   z8 = (mxfeet, 0);
   draw z5{up}..{left}z6..{down}z7..z8;
 
   z9 = (x3, -mxlow);
   draw z8..{dir -50}z9;
   penlabels(range 1 thru 10);
endchar;
 
%% e %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "e - en";
beginchar("e", mxzw#+mxone#, mxlittle#, mxlow#);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z1 = (0, -.5mxlow);
   z2 = (mxhalf, -mxlow);
   z3 = (mxone, 0);
   z4 = (x3, mxmiddle);
   z5 = (x2, mxlittle);
   z6 = (0, y4);
   draw z1{down}..{right}z2..{up}z3--z4..{left}z5..z6;
 
   penlabels(range 1 thru 6);
endchar;
 
%% g %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "g - gan";
beginchar("g", mxzw#+mxone#, mxlittle#, mxlow#);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z2 = (mxhalf, -mxlow);
   z4 = (x2, mxthird);
   z1 = (0, .5[y2,y4]); % y was -.5mxlow
   z3 = (mxone, y1);
   draw z1{down}..{right}z2..{up}z3..{left}z4..cycle;
 
   z5 = (x3-.3mxhalf, mxmiddle);
   z6 = (x4, mxlittle);
   z7 = (.3mxhalf, y5);
   draw z4{right}..{up}z5..{left}z6..{down}z7;
   penlabels(range 1 thru 7);
endchar;
 
 
 
%% .g %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar ".g - .gan (oct002)";
beginchar(oct"002", mxzw#+2mxone#, mxlittle#, mxlow#);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z1 = (mxfeet, 0);
   z2 = (0, .5mxlittle);
   z3 = (mxhalf, mxlittle);
   z4 = (mxone, mxmiddle);
   z5 = (mxone+mxhalf, y3);
   z6 = (2mxone, y2);
   z7 = (2mxone-mxfeet, 0);
   z8 = (x6, -mxlow);
   draw z1..{up}z2..{right}z3..{down}z4;
   draw z4{up}..{right}z5..{down}z6..z7;
   draw z1..{dir -50}z8;
   penlabels(range 1 thru 8);
endchar;
 
 
%% h %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "h - ha";
beginchar("h", mxzw#+mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z1 = (0, mxthird);
   z2 = (mxhalf, 0);
   z3 = (mxone, .5mxlittle);
   z4 = (.75mxone, 5/6mxlittle);
   z5 = (.75mxhalf, y4);
   draw z1{dir -80}..{right}z2..{up}z3..{left}z4--z5;
 
   z6 = (x3, mxlittle);
   z7 = (x4, 7/6mxlittle);
   z8 = (x5, y7);
   draw z4..{up}z6..{left}z7--z8;
 
   z9 = (x6, 8/6mxlittle);
   z10= (0, mxheight);
   draw z7..{up}z9..{dir 110}z10;
 
   penlabels(range 1 thru 10);
endchar;
 
%% i %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "i - in";
beginchar("i", mxzw#+mxone#, mxlittle#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z1 = (mxfeet, 0);
   z2 = (0, .5mxlittle);
   z3 = (mxhalf, mxlittle);
   z4 = (mxone, y2);
   z5 = (mxone-mxfeet, 0);
   draw z1..{up}z2..{right}z3..{down}z4..z5;
   penlabels(range 1 thru 5);
endchar;
 

%% i1 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "i1 - short i, (Old Georgian), oct012";
beginchar(oct"012", mxzw#+mxone#+mxhalf#, mxlittle#, 0);
  if known cmbase: adjust_fit(0,0); fi
  pickup mxed_pen;
  z1 = (0, 0);
  z2 = (1.5mxhalf, .15mxthird); % y2 war .5mxthird
  z4 = (x2, mxlittle);
  z3 = (mxone+.5mxhalf, .5[y2,y4]);
  z5 = (.5mxhalf, y3);
  z6 = (mxone+mxhalf, y1);
  draw z1..z2..z3..z4..z5..z2..z6;
  penlabels(range 1 thru 6);
endchar;


%% %% i1 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% mxedchar "i1 - short i, (Old Georgian), oct012";
%% beginchar(oct"012", mxzw#+mxone#, mxlittle#, 0);
%%   if known cmbase: adjust_fit(0,0); fi
%%   pickup mxed_pen;
%%   z1 = (0, 0);
%%   z2 = (mxhalf, .15mxthird); % y2 war .5mxthird
%%   z4 = (x2, mxlittle);
%%   z3 = (mxone, .5[y2,y4]);
%%   z5 = (0, y3);
%%   z6 = (x3, y1);
%%   draw z1..z2..z3..z4..z5..z2..z6;
%%   penlabels(range 1 thru 6);
%% endchar;

 
%% ^dz %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "^dz/^j - ^jan (oct010)";
beginchar(oct"010", mxzw#+1.75mxone#, mxlittle#, mxlow#);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z1 = (0, -.5mxlow);
   z3 = (1.5mxhalf, y1);
   z2 = (.5[x1,x3], -mxlow);
   z4 = (1.2mxhalf, 0);
   z5 = (1.75mxone, mxlittle);
   draw z1{down}..{right}z2..{up}z3..z4;
   draw z4--z5;
 
   z6 = (.3mxhalf, 5/6mxlittle);
   z7 = (mxhalf, mxlittle);
   z8 = (1.5mxone, 0);
   z9 = (x5, .5mxthird);
   draw z6{up}..{right}z7..{right}z8..{dir 45}z9;
 
   penlabels(range 1 thru 9);
endchar;
 
 
%% dz %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "dz (j) - dzil";
beginchar("j", mxzw#+mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z1 = (0, .5mxlittle);
   z2 = (mxhalf, 0);
   z3 = (mxone, y1);
   z4 = (x2, mxlittle);
   draw z1{down}..{right}z2..{up}z3..{left}z4..cycle;
 
   z5 = (x3, mxheight);
   draw z3--z5;
   penlabels(range 1 thru 5);
endchar;
 
 
%% k %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "k - kan";
beginchar("k", mxzw#+mxone#, mxheight#, mxlow#);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z1 = (0, mxthird);
   z2 = (mxhalf, 2mxthird);
   z3 = (mxone, 1.5mxthird);
   z4 = (x3, -.5mxlow);
   z5 = (x2, -mxlow);
   z6 = (x1, y4);
   z7 = (x3, mxheight);
   z8 = z7 - (mxhalf, mxthird);
   draw z1..z2..{down}z3--z4{down}..{left}z5..{up}z6;
   draw z3--z7--z8;
   penlabels(range 1 thru 8);
endchar;
 
 
%% .k %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% mxedchar ".k - .kan (oct003)";
%% beginchar(oct"003", mxzw#+mxone#, mxlittle#, 1.5mxlow#);
%%    if known cmbase: adjust_fit(0,0); fi
%%    pickup mxed_pen;
%%    z1 = (0, -mxlow);
%% %   z1 = (0, -.66mxlow);
%%    z2 = (mxhalf, -1.5mxlow);
%% %   z2 = (mxhalf, -mxlow);
%%    z3 = (mxone, -.75mxlow);
%% %   z3 = (mxone, -.5mxlow);
%%    z4 = (x2, 0);
%%    z5 = (0, -1/6mxlittle);
%%    draw z1{down}..{right}z2..{up}z3..{left}z4..z5;
%%  
%%    z6 = (x3, .75mxthird);
%%    z7 = (x6, mxlittle);
%%    draw z4{right}..{up}z6--z7;
%%  
%%    penlabels(range 1 thru 7);
%% endchar;

mxedchar ".k - .kan (oct003)";
beginchar(oct"003", mxzw#+mxone#, mxheight#, 1mxlow#);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z1 = (0, -.5mxlow);
   z2 = (mxhalf, -mxlow);
   z3 = (mxone, -.25mxlow);
   z4 = (x2, 1/3mxlittle);
   z5 = (0, 1/6mxlittle);
   draw z1{down}..{right}z2..{up}z3..{left}z4..z5;
 
   z6 = (x3, 1.75mxthird);
%   z7 = (x6, 4/3mxlittle);
   z7 = (x4, 4/3mxlittle);
%   draw z4{right}..{up}z6--z7;
   draw z4{right}..{up}z6..z7;
 
   penlabels(range 1 thru 7);
endchar;
 
 
 
%% l %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "l - las";
beginchar("l", mxzw#+2.4mxone#, mxlittle#, mxlow#);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z1 = (mxfeet, 0);
   z2 = (0, .5mxlittle);
   z3 = (.8mxhalf, mxlittle);
   z4 = (.8mxone, mxmiddle);
   z9 = (.8mxone+.8mxhalf, y3);
   z10= (1.6mxone, y4);
   z5 = (1.6mxone+.8mxhalf, y3);
   z6 = (2.4mxone, y2);
   z7 = (2.4mxone-mxfeet, 0);
   z8 = (x6, -mxlow);
   draw z1..{up}z2..{right}z3..{down}z4;
   draw z4{up}..{right}z9..{down}z10;
   draw z10{up}..{right}z5..{down}z6..z7;
   draw z1..{dir -50}z8;
   penlabels(range 1 thru 10);
endchar;
 
 
%% m %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "m - man";
beginchar("m", mxzw#+mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z1 = (0, .5mxlittle);
   z2 = (mxhalf, 0);
   z3 = (mxone, y1);
   z4 = (x2, mxlittle);
   draw z1{down}..{right}z2..{up}z3..{left}z4..cycle;
 
   z5 = (x3, mxheight-.5mxtop);
   z6 = (x4, mxheight);
   z7 = (0, y5);
   draw z3..z5{up}..{left}z6..{down}z7;
   penlabels(range 1 thru 7);
endchar;
 
 
%% n %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "n - nar";
beginchar("n", mxzw#+mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z1 = (0, .5mxlittle);
   z2 = (mxhalf, 0);
   z3 = (mxone, y1);
   z4 = (x2, mxlittle);
   draw z1{down}..{right}z2..{up}z3..{left}z4..cycle;
 
   z5 = (0, mxheight-.25mxtop);
   z6 = (.5mxhalf, mxheight);
   z7 = (x3, y6+.2mxtop);
   draw z1..z5{up}..{right}z6..{dir 75}z7;
   penlabels(range 1 thru 7);
endchar;
 
 
%% o %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "o - on";
beginchar("o", mxzw#+2mxone#, mxlittle#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z1 = (mxfeet, 0);
   z2 = (0, .5mxlittle);
   z3 = (mxhalf, mxlittle);
   z4 = (mxone, mxmiddle);
   z5 = (mxone+mxhalf, y3);
   z6 = (2mxone, y2);
   z7 = (2mxone-mxfeet, 0);
   draw z1..{up}z2..{right}z3..{down}z4;
   draw z4{up}..{right}z5..{down}z6..z7;
   penlabels(range 1 thru 7);
endchar;
 
%% p %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "p - par";
beginchar("p", mxzw#+2mxone#, mxlittle#, mxlow#);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z1 = (0, .5mxlittle);
   z2 = (mxhalf, 0);
   z3 = (mxone, y1);
   z4 = (x3, mxmiddle);
   z5 = (x2, mxlittle);
   draw z1{down}..{right}z2..{up}z3..z4{up}..{left}z5..cycle;
 
   z6 = (mxhalf+mxone, y5);
   z7 = (2mxone, y1);
   z8 = (x6, 0);
   z9 = (x7, -.5mxlow);
   z11= (.75mxone, y9);
   z10= (.5[x11,x9], -mxlow);
   draw z4{up}..{right}z6..{down}z7..{left}z8;
   draw z8{right}..{down}z9..{left}z10..{dir 70}z11;
   penlabels(range 1 thru 11);
endchar;
 
%% .p %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar ".p - .par (oct004)";
beginchar(oct"004", mxzw#+mxone#, mxthird#+mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z1 = (0, mxthird);
   z2 = (mxhalf, 0);
   z3 = (mxone, .5mxlittle);
   z4 = (x2, mxlittle);
   z5 = (x1, y4);
   draw z1{dir -80}..{right}z2..{up}z3..{left}z4--z5;
 
   z6 = (x3, mxlittle+.4mxtop);
   z7 = (x1, mxthird+mxheight);
   draw z4..{up}z6..{dir 110}z7;
 
   penlabels(range 1 thru 7);
endchar;
 
 
%% q %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar ".q - .qar";
beginchar("q", mxzw#+mxone#, mxlittle#, mxlow#);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z1 = (mxfeet, mxlittle);
   z2 = (0, .5mxlittle);
   z3 = (mxhalf, 0);
   z4 = (mxone, y2);
   z5 = (x4 - mxfeet, y1);
   draw z1..{down}z2..{right}z3..{up}z4..z5;
 
   z6 = (x4, -.5mxlow);
   z7 = (x3, -mxlow);
   z8 = (x2, y6);
   draw z4--z6..{left}z7..{up}z8;
   penlabels(range 1 thru 8);
endchar;
 
%% r %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "r - ran";
beginchar("r", mxzw#+2mxone#, mxthird#+mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z1 = (mxfeet, 0);
   z2 = (0, .5mxlittle);
   z3 = (mxhalf, mxlittle);
   z4 = (mxone, mxmiddle);
   z5 = (mxone+mxhalf, y3);
   z6 = (2mxone, y2);
   z7 = (2mxone-mxfeet, 0);
   z8 = (x6, mxheight+mxthird);
   draw z1..{up}z2..{right}z3..{down}z4;
   draw z4{up}..{right}z5..{down}z6..z7;
   draw z2{up}..{dir 70}z8;
   penlabels(range 1 thru 8);
endchar;
 
 
%% s %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "s - san";
beginchar("s", mxzw#+mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z5 = (0, mxheight);
   z1 = (0, .5mxlittle);
   z2 = (mxhalf, 0);
   z3 = (mxone, y1);
   z4 = (1/3mxone, mxlittle);
   draw z5--z1{down}..{right}z2..{up}z3..{up}z4;
 
   penlabels(range 1 thru 5);
endchar;
 
 
 
%% ^s %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "^s - ^san (+s, oct005)";
beginchar(oct"005", mxzw#+mxhalf#+mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z1 = (mxhalf, .5mxlittle);
   z2 = (mxhalf+mxhalf, 0);
   z3 = (mxone+mxhalf, y1);
   z4 = (x2, mxlittle);
   draw z1{down}..{right}z2..{up}z3..{left}z4..cycle;
 
   z5 = (x3, mxheight-.5mxtop);
   z6 = (x4, mxheight);
   z7 = (mxhalf, y5);
   draw z3..z5{up}..{left}z6..{down}z7;
 
   z8 = (0, y6);
   draw z7{up}..{left}z8;
   penlabels(range 1 thru 8);
endchar;
 
 
%% t %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "t - tan";
beginchar("t", mxzw#+2mxone#, mxlittle#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z1 = (0, .5mxlittle);
   z2 = (mxhalf, 0);
   z3 = (mxone, y1);
   z4 = (x3, mxmiddle);
   z5 = (x2, mxlittle);
   draw z1{down}..{right}z2..{up}z3..z4{up}..{left}z5..cycle;
 
   z6 = (mxhalf+mxone, y5);
   z7 = (2mxone, y1);
   z8 = (x7-.mxfeet, 0);
   draw z4{up}..{right}z6..{down}z7..z8;
   penlabels(range 1 thru 8);
endchar;
 
 
%% .t %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar ".t - tar (oct006)";
beginchar(oct"006", mxzw#+mxone#+mxhalf#, mxheight#, mxlow#);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z1 = (0, .3mxlittle);
   z2 = (.5mxhalf+.5mxone, -mxlow);
   z3 = (mxone+mxhalf, y1);
   z4 = (x2, mxlittle);
   draw z1{down}..{right}z2..{up}z3..{left}z4..cycle;
 
   z5 = (x4, y4-1.2mxthird);
   z6 = (x4, y4+1.2mxthird);
   draw z4{left}..{right}z5..cycle;
   draw z4{left}..{right}z6..cycle;
   penlabels(range 1 thru 6);
endchar;
 
 
 
%% u %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "u - un";
beginchar("u", mxzw#+2.1mxone#, mxlittle#, mxlow#);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z1 = (0, mxlittle);
   z5 = (.9mxone, y1);
   z2 = (.5[x1,x5], 2.4mxthird);
   z6 = (1.3mxone, 2mxthird);
   z7 = (x6, .5mxlittle);
   draw z1..z2..{right}z5..{down}z6--z7;
 
   z8 = (1.3mxone+.8mxhalf, y5);
   z9 = (2.1mxone, y6);
   z10= (x9, 0);
   z11= (x6, -mxlow);
   z12= (x2, -.5mxlow);
   draw z6{dir 75}..{right}z8..{down}z9--z10..{left}z11..{up}z12;
   penlabels(range 1 thru 12);
endchar;
 
 
%% v %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "v - vin";
beginchar("v", mxzw#+mxone#, mxlittle#, mxlow#);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z1 = (0, mxmiddle);
   z2 = (mxhalf, mxlittle);
%   z3 = (mxone, .5mxlittle);
   z3 = (mxone, .5[y4,y2]);
% Esthetic correction JDL (8/5/94)
%  z4 = (x2, 0);
   z4 = (x2, .375y1);
% end of esthetic correction
%   z5 = (0, 1/6mxlittle);
   draw z1{up}..{right}z2..{down}z3..{left}z4; % ..z5;
 
%   z6 = (x3, -.5mxlow);
   z6 = (x3, .5[y4,y7]);
% Correction by JDL (8/5/94)
%   z7 = (x2, -1.5mxlow);
%   z8 = (0, -mxlow);
   z7 = (x2, -mxlow);
   z8 = (0, y6);
   draw z4{right}..{down}z6..{left}z7..{up}z8;
 
   penlabels(range 1 thru 7);
endchar;
 
 
%% x %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "x - xan";
beginchar("x", mxzw#+mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z1 = (0, .5mxlittle);
   z2 = (mxhalf, 0);
   z3 = (mxone, y1);
   z4 = (x2, 5/6mxlittle);
   z6 = (x1, y4);
   draw z1{down}..{right}z2..{up}z3..{left}z4..z6;
 
   z5 = (0, mxheight);
   draw z1--z5;
 
   z7 = z6 +(1mxu, 1mxu);
   draw z6--z7;
   penlabels(range 1 thru 7);
endchar;
 
 
%% z %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "z - zen";
beginchar("z", mxzw#+2mxone#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z1 = (.75mxone, .5mxlittle);
   z2 = (.75mxone+mxhalf, 0);
   z3 = (.75mxone+mxone, y1);
   z4 = (x2, 5/6mxlittle);
   z6 = (x1, y4);
   draw z1{down}..{right}z2..{up}z3..{left}z4..z6;
 
   z5 = (.75mxone, mxheight-.5mxtop);
   z11= (x5, mxheight);
   z8 = (mxhalf, y11);
   z9 = (0, y5);
   z10 =(.5[x9,x5], mxheight-mxtop);
%   draw z1--z11..{left}z8..{down}z9..{right}z10..{up}z5;
   draw z1--z5{up}..{left}z8..{down}z9..{right}z10..{up}z5;
   z7 = z6 + (1mxu, 1mxu);
   draw z6--z7;
   penlabels(range 1 thru 10);
endchar;
 
%% %% z %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% mxedchar "z - zen";
%% beginchar("z", mxzw#+2mxone#, mxheight#, 0); 
%%    if known cmbase: adjust_fit(0,0); fi
%%    pickup mxed_pen;
%%    z1 = (mxone, .5mxlittle);
%%    z2 = (mxone+mxhalf, 0);
%%    z3 = (mxone+mxone, y1);
%%    z4 = (x2, 5/6mxlittle);
%%    z6 = (x1, y4);
%%    draw z1{down}..{right}z2..{up}z3..{left}z4..z6;
%%  
%%    z5 = (mxone, mxheight-.5mxtop);
%%    z11= (x5, mxheight);
%%    z8 = (mxhalf, y11);
%%    z9 = (0, y5);
%%    z10 =(x8, mxheight-mxtop);
%%    draw z1--z11..{left}z8..{down}z9..{right}z10..{up}z5;
%%  
%%    z7 = z6 + (1.8mxu, 1.8mxu);
%%    draw z6--z7;
%%    penlabels(range 1 thru 10);
%% endchar;
 
 
%% ^z %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "^z - ^zan (+z, oct007)";
beginchar(oct"007", mxzw#+mxone#, mxlittle#, mxlow#);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_pen;
   z1 = (0, mxthird);
   z2 = (.5mxhalf, 0);
   z3 = (mxhalf, y1);
   z4 = (mxone, mxlittle);
   draw z1{down}..{right}z2..{up}z3..{up}z4;
 
   z5 = (x4, -.5mxlow);
   z6 = (x3, -mxlow);
   z7 = (0, y5);
   draw z4--z5..{left}z6..{up}z7;
   penlabels(range 1 thru 7);
endchar;


%% f %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "f, (Ossetian)";
beginchar("f", mxzw#+mxone#+mxhalf#, mxheight#, mxlow#);
  if known cmbase: adjust_fit(0,0); fi
  pickup mxed_pen;
  z1 = (0, .5mxlittle);
  z2 = (1.5mxone, y1);
  draw z1..z2..cycle;

  z3 = (0, 4mxthird);
  z31 = (1/3mxone, 5mxthird);
  z4 = (1.5mxhalf, y1);
  z51 = (7/6mxone, -mxlow);
  z5 = (1.5mxone, -.5mxlow);
%  draw z3{dir 45}..z4..{dir 45}z5;
  draw z3{up}..{right}z31..{down}z4..{right}z51..{up}z5;

  penlabels(range 1 thru 5, 31, 51);
endchar;


%% oo %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "oo - ho, (Old Georgian), oct011";
beginchar(oct"011", mxzw#+mxone#+mxhalf#, mxheight#, 0);
  if known cmbase: adjust_fit(0,0); fi
  pickup mxed_pen;
  z1 = (mxone+mxhalf, 2/3mxlittle);
  z8 = (x1, mxlittle);
  z2 = (mxhalf, y1);
  z7 = (x2, y8);
  z3 = (x2, 0);
  z6 = (x2, mxheight);
  z4 = (mxone, .7mxthird);
  z5 = (x4, mxheight-.7mxthird);
  draw z1--z2{left}..z3..{up}z4--z5{up}..z6..{right}z7--z8;
  penlabels(range 1 thru 8);
endchar;


%% w %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "w - wi, (Old Georgian)";
beginchar("w", mxzw#+mxone#, mxheight#, mxlow#);
  if known cmbase: adjust_fit(0,0); fi
  pickup mxed_pen;
  z1 = (.5mxhalf, 2/3mxlittle);
  z2 = (mxone, y1);
  z3 = (x1, .4mxthird);
  z4 = (x2, -.4mxlow);
  z5 = (mxhalf, -mxlow);
  z6 = (0, -.75mxlow);
  draw z1--z2--z3{dir 30}..{down}z4..{left}z5..z6;
  penlabels(range 1 thru 6);
endchar;


%% Schwa %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "y - Schwa, (Ossetian)";
beginchar("y", mxzw#+mxone#, mxlittle#, mxlow#);
  if known cmbase: adjust_fit(0,0); fi
  pickup mxed_pen;
  z1 = (0, 2mxthird);
  z2 = (mxone, y1);
  z3 = (0, -.5mxlow);
  z4 = (x2, y3);
  draw z1{up}..{down}z2..{down}z3..{up}z4;
  penlabels(range 1 thru 4);
endchar;

endinput;
