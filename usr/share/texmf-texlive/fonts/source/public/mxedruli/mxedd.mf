% Mxedruli digits `mxedd.mf'
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Designed and (c) by Johannes Heinecke                                     %
%                     Humboldt-Universit"at zu Berlin                       %
%                     Philosophische Faskult"at II                          %
%                     Institut f"ur deutsche Sprache und Linguistik         %
%                     Computerlinguistik                                    %
%                     J"agerstr. 10/11                                      %
%                     D - 10099 Berlin                                      %
%                     <heinecke@compling.hu-berlin.de>                      %
% Please send any improvements, corrections, suggestions, enhancements etc  %
% to the Author!                                                            %
%                                                                           %
% Version 2.0 as of 16th June 1995                                          %
%                                                                           %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%% Character Definitions: %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


%% 1 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "1";
beginchar("1", mxhalf#+mxzw#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_digitpen;
   top z1 = (mxhalf, mxheight);
   bot z2 = (x1, 0);
   z3 = (0, mxheight-.5mxtop);
   draw z3--z1--z2;
   penlabels(1, 2, 3);
endchar;

%% 2 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "2";
beginchar("2", mxone#+mxzw#, mxheight#, 0);
   if known cmbase: adjust_fit(0,0); fi
   pickup mxed_digitpen;
   z1 = (0, mxheight-.5mxtop);
   top z2 = (mxhalf, mxheight);
   z3 = (mxone, y1);
   bot z4 = (x1, 0);
   z5 = (x3, y4);
%   draw z1{up}..{right}z2..{down}z3..tension 2..z4--z5;
   draw z1{up}..{right}z2..z3..{z4-z3}z4--z5;
   penlabels(1, 2, 3, 4, 5);
endchar;

%% 3 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "3";
beginchar("3", mxone#+mxzw#, mxheight#, 0);
   pickup mxed_digitpen;
   top z1 = (0, mxheight);
   z2 = (mxone, y1);
   z3 = (mxhalf, mxlittle);
   z4 = (x2, .5mxlittle);
   bot z5 = (x3, 0);
   z6 = (x1, .5mxlittle);
   draw z1--z2--z3;
   draw z3{right}..{down}z4..{left}z5..{up}z6;
   penlabels(1, 2, 3, 4, 5, 6);
endchar;

%% 4 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "4";
beginchar("4", mxone#+mxzw#, mxheight#, 0);
   pickup mxed_digitpen;
   top z1 = (mxhalf, mxheight);
   z2 = (0, .6mxlittle);
   z3 = (mxone, y2);
   draw z1--z2--z3;
   bot z4 = (x1, 0);
   z5 = (x4, 1.25mxlittle);
   draw z4--z5;
   penlabels(1, 2, 3, 4, 5);
endchar;

%% 5 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "5";
beginchar("5", mxone#+mxzw#, mxheight#, 0);
   pickup mxed_digitpen;
   top z1 = (mxone, mxheight);
   z2 = (0, y1);
   z3 = (x2, mxlittle);
   z4 = (mxhalf, y3);
   z5 = (x1, .5mxlittle);
   bot z6 = (x4, 0);
   z7 = (x2, .3mxlittle);
   draw z1--z2--z3--z4{right}..z5..{left}z6..z7;
   penlabels(1, 2, 3, 4, 5, 6, 7);
endchar;

%% 6 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "6";
beginchar("6", mxone#+mxzw#, mxheight#, 0);
   pickup mxed_digitpen;
   bot z1 = (mxhalf, 0);
   z2 = (x1, mxlittle);
   z3 = (0, .5mxlittle);
   z5 = (mxone, y3);
   draw z1{left}..{up}z3..{right}z2..{down}z5..cycle;
   top z4 = (1.5mxhalf, mxheight);
%   draw z3{up}..{dir 60}z4;
   draw z3{up}..z4;
   penlabels(1, 2, 3, 4, 5);
endchar;

%% 7 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "7";
beginchar("7", mxone#+mxzw#, mxheight#, 0);
   pickup mxed_digitpen;
   top z1 = (0, mxheight);
   z2 = (mxone, y1);
   bot z3 = (x1, 0);
   draw z1--z2--z3;
   penlabels(1, 2, 3);
endchar;

%% 8 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "8";
beginchar("8", mxone#+mxzw#, mxheight#, 0);
   pickup mxed_digitpen;
   bot z1 = (mxhalf, 0);
   z2 = (x1, mxlittle);
   z6 = (0, .5mxlittle);
   z7 = (mxone, y6);
   draw z1{left}..{up}z6..{right}z2..{down}z7..cycle;
   top z3 = (x1, mxheight);
   z4 = (.2mxhalf, .5[y2, y3]);
   z5 = (1.8mxhalf, y4);
   draw z3{left}..{down}z4..{right}z2..{up}z5..cycle;
   penlabels(1, 2, 3, 4, 5, 6, 7);
endchar;

%% 9 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "9";
beginchar("9", mxone#+mxzw#, mxheight#, 0);
   pickup mxed_digitpen;
   top z1 = (mxhalf, mxheight);
   z2 = (x1, mxlittle);
   z3 = (mxone, mxheight-.5mxtop);
   z4 = (0, y3);

   draw z1{left}..{down}z4..{right}z2..{up}z3..cycle;
   bot z5 = (.5mxhalf, 0);
%   draw z3{down}..{dir -120}z5;
   draw z3{down}..z5;
   penlabels(1, 2, 3, 4);
endchar;

%% 0 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
mxedchar "0";
beginchar("0", mxone#+mxzw#, mxheight#, 0);
   pickup mxed_digitpen;
   bot z1 = (mxhalf, 0);
   z2 = (mxone, mxlittle);
   top z3 = (x1, mxheight);
   z4 = (0, y2);
   draw z1{right}..{up}z2..{left}z3..{down}z4..cycle;
   penlabels(1, 2, 3, 4);
endchar;


endinput;



