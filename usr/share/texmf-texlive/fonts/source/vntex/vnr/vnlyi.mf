% Copyright 2003-2005 Han The Thanh <hanthethanh@gmx.net>.
% This file is part of vntex.  License: LPPL, version 1.3 or newer,
% according to http://www.latex-project.org/lppl.txt

% Computer Modern Roman Italic lower case "y" precomposed glyphs:
% Inclusion:
%               y'      y`      y?      y~      y.

begin_letter(y);
    set_letter_dimens(y)(
        8.5u#, x_height#, desc_depth#,
        x_height#*slant + .5stem# - u#,
        0, 0
    );
    pickup fine.nib;
    interim superness := more_super;
    x0 = 0;
    x2 = x3 + .25u;
    pos3(stem, -180);
    lft x3r = hround(2.5u - .5stem);
    hook_in(0, 1, 2)(skewed);  % opening hook
    pos2'(stem, -180);
    z2' = z2;
    pos4(vair, -90);
    pos5(hair, 0);
    x4 = .5[x3, x5];
    pos6(stem, 0);
    rt x6r = hround(w - 1.5u + .5stem);
    pos7(stem, 0);
    pos8(vair, -90);
    y3 = .7[y4, y5];
    bot y4r=-oo;
    y5 = .57h;
    y6 + .5stem = h;
    y7 = 0;
    bot y8r=-d - oo;
    x5 = x6 = x7;
    x8 = .5w;
    pos9(hair, -180);
    pos10(flare, -180);
    y9=-.5d;
    lft x9r = hround(2.75u - .5flare);
    bulb(8, 9, 10);  % bulb
    filldraw stroke z2'e{ - u, -x_height}
    ... super_arc.e(3, 4) ... {up}z5e; % left stem and arc
    filldraw circ_stroke z6e --- z7e ... {left}z8e;  % right stem and link
    math_fit( - 2/3x_height#*slant + .5hair# + .5u#, ic# - 1/3x_height#*slant);
    penlabels(0, 1, 2, 3, 4, 5, 6, 7, 8, 9, 10);
    set_letter_join(y, x4, .5w#);
    set_letter_join(y.dot_, x5, .5w#);
    vn_dot_shift_y(y) := vn_depth(y) + vn_height(dot_);
    vn_depth(y) := vn_dot_shift_y(y);
end_letter;

input vnycomp;

endinput;
