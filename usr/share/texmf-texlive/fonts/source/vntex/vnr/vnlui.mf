% Copyright 2003-2005 Han The Thanh <hanthethanh@gmx.net>.
% This file is part of vntex.  License: LPPL, version 1.3 or newer,
% according to http://www.latex-project.org/lppl.txt

% Computer Modern Roman Italic lower case "u" precomposed glyphs:
% Inclusion:
%               u'      u`      u?      u~      u.
%       u+      u+'     u+`     u+?     u+~     u+.

begin_letter(u);
    set_letter_dimens(u)(
        9.5u#, x_height#, 0,
        1/3x_height#*slant + .5hair# + .5u#,
        0, 0
    );
    pickup fine.nib;
    interim superness := more_super;
    x0 = 0;
    x2 = x3 + .25u;
    pos3(stem, -180);
    lft x3r = hround(2.5u - .5stem);
    hook_in(0, 1, 2)(skewed);  % opening hook
    pos2'(stem, -180);
    z2' = z2;
    pos4(vair, -90);
    pos5(hair, 0);
    x4 = .5[x3, x5];
    pos6(stem, 0);
    rt x6r = hround(w - 2.5u + .5stem);
    x5 = x6 = x7;
    x9 = w;
    hook_out(7, 8, 9);  % closing hook
    y3 = .7[y4, y5];
    bot y4r=-oo;
    y5 = .57h;
    y6 + .5stem = h;
    filldraw stroke z2'e{ - u, -x_height}
    ... super_arc.e(3, 4) ... {up}z5e; % left stem and arc
    filldraw circ_stroke z6e -- z7e;  % right stem
    math_fit( - 2/3x_height#*slant + .5hair# + .5u#, ic#);
    penlabels(0, 1, 2, 3, 4, 5, 6, 7, 8, 9, 10);
    set_letter_join(u, x4, .5w#);
    set_letter_join(u.horn_, x4, .5w#);
    set_letter_join(u.dot_, x4 + .5u, .5w# + .5u#);
    set_horn_join(u, z5 -- z6, z5 -- z6);
end_letter;

input vnucomp;

endinput;
