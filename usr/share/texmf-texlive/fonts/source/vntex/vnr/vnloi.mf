% Copyright 2003-2005 Han The Thanh <hanthethanh@gmx.net>.
% This file is part of vntex.  License: LPPL, version 1.3 or newer,
% according to http://www.latex-project.org/lppl.txt

% Computer Modern Roman Italic lower case "o" precomposed glyphs:
% Inclusion:
%               o'      o`      o?      o~      o.
%       o+      o+'     o+`     o+?     o+~     o+.

begin_letter(o);
    set_letter_dimens(o)(
        9u#, x_height#, 0,
        .7x_height#*slant + .5curve# - u# if math_fitting: -.5u# fi,
        0, 0
    );
    pickup fine.nib;
    pos1(vair, 90);
    pos2(curve, 180);
    pos3(vair, 270);
    pos4(curve, 360);
    x1 = x3 = .5w;
    lft x2r = hround(1.5u - .5curve);
    x4 = w-x2;
    top y1r = h+oo;
    y2 = y4 = .5[y1, y3];
    bot y3r = -oo;
    filldraw stroke pulled_arc.e(1, 2) & pulled_arc.e(2, 3)
    & pulled_arc.e(3, 4) & pulled_arc.e(4, 1); % bowl
    math_fit( - .3x_height#*slant + .5curve# - u#, ic#);
    penlabels(1, 2, 3, 4);
    set_letter_join(o, x1, .5w#);
    set_letter_join(o.horn_, x1, .5w#);
    set_horn_join(o, pulled_arc.l(4, 1), pulled_arc.r(4, 1));
end_letter;

input vnocomp;

endinput;
