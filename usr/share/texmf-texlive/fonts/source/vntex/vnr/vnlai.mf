% Copyright 2003-2005 Han The Thanh <hanthethanh@gmx.net>.
% This file is part of vntex.  License: LPPL, version 1.3 or newer,
% according to http://www.latex-project.org/lppl.txt

% Computer Modern Roman Italic lower case "a" precomposed glyphs:
% Inclusion:
%               a'      a`      a?      a~      a.
%       a(      a('     a(`     a(?     a(~     a(.
%       a^      a^'     a^`     a^?     a^~     a^.

begin_letter(a);
    set_letter_dimens(a)(
        9u#, x_height#, 0,
        1/3x_height#*slant + .5hair# + .5u#,
        0, 0
    );
    pickup fine.nib;
    pos0(hair, 0);
    pos1(vair, 90);
    pos2(curve, 180);
    pos3(vair, 270);
    pos4(stem, 0);
    x4 = x0;
    y4 + .5stem = vround .98h;
    x1 = x3 = .5[x0, x2];
    lft x2r = hround(1.5u - .5curve);
    rt x4r = hround(w - 2.5u + .5stem);
    top y1r = h+oo;
    bot y3r=-oo;
    y0 = y2 = .5[y1, y3];
    filldraw stroke super_arc.e(0, 1) & pulled_arc.e(1, 2)
    & pulled_arc.e(2, 3) & super_arc.e(3, 0);  % bowl
    x5 = x4;
    x7 = w;
    hook_out(5, 6, 7);  % closing hook
    filldraw circ_stroke z4e -- z5e;  % stem
    math_fit( - .3x_height#*slant + .5curve# - u#, ic#);
    penlabels(0, 1, 2, 3, 4, 5, 6, 7);
    set_letter_join(a, x1 + .25u, 4.5u#);
    set_letter_join(a.dot_, x1 + .5u, .5w# + .5u#);
end_letter;

input vnacomp;

endinput;
