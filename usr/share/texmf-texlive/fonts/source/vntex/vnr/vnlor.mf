% Copyright 2003-2005 Han The Thanh <hanthethanh@gmx.net>.
% This file is part of vntex.  License: LPPL, version 1.3 or newer,
% according to http://www.latex-project.org/lppl.txt

% Computer Modern Roman lower case "o" precomposed glyphs:
% Inclusion:
%               o'      o`      o?      o~      o.
%       o^      o^'     o^`     o^?     o^~     o^.
%       o+      o+'     o+`     o+?     o+~     o+.

begin_letter(o);
    set_letter_dimens(o)(
        9u#, x_height#, 0,
        .7x_height#*slant,
        if monospace: .5u#, .5u# else: 0, 0 fi
    );
    penpos1(vair, 90);
    penpos3(vair', -90);
    penpos2(curve, 180);
    penpos4(curve, 0);
    x2r = hround max(.5u, 1.25u - .5curve);
    x4r = w-x2r;
    x1 = x3 = .5w;
    y1r = h + vround 1.5oo;
    y3r = -oo;
    y2 = y4 = .5h - vair_corr;
    y2l := y4l := .52h;
    penstroke pulled_arc.e(1, 2) & pulled_arc.e(2, 3)
    & pulled_arc.e(3, 4) & pulled_arc.e(4, 1) & cycle;  % bowl
    penlabels(1, 2, 3, 4);
    set_letter_join(o, x1, .5w#);
    set_letter_join(o.horn_, x1 + .1dot_size, .5w# + .1dot_size#);
    set_horn_join(o, pulled_arc.l(4, 1), pulled_arc.r(4, 1));
end_letter;

input vnocomp;

endinput;
