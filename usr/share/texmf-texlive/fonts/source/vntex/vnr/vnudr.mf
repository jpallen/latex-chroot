% Copyright 2003-2005 Han The Thanh <hanthethanh@gmx.net>.
% This file is part of vntex.  License: LPPL, version 1.3 or newer,
% according to http://www.latex-project.org/lppl.txt

% Computer Modern Roman "D-bar" precomposed glyph:
% Inclusion:
%               DD

numeric bar_thickness#;
bar_thickness# := vair#;
define_whole_pixels(bar_thickness);

vnchar casename "d with bar";
beginchar(vn_code(d.bar_), 13.5u#, cap_height#, 0);
    italcorr .7cap_height# * slant - .5u#;
    adjust_fit(cap_serif_fit#, 0);
    pickup tiny.nib;
    pos1(cap_stem', 0);
    pos2(cap_stem', 0);
    lft x1l = lft x2l = hround max(2u, 3u - .5cap_stem');
    top y1 = h;
    bot y2 = 0;
    filldraw stroke z1e -- z2e;                               % stem
    penpos3(cap_band, 90);
    penpos4(cap_band, 90);
    penpos5(cap_curve - stem_corr, 0);
    penpos6(cap_band, -90);
    penpos7(cap_band, -90);
    z3r = top z1;
    y4 = y3;
    y5 = .51[y4, y6];
    y6 = y7;
    z7r = bot z2;
    x4 = x6 = .5w + .25u;
    x5r = hround(w - u);
    x4l := x6l := x4 - .25cap_curve;
    fill stroke z3e .. pulled_arc.e(4, 5) & pulled_arc.e(5, 6) .. z7e;% lobe
    if serifs:
        nodish_serif(1, 2 ,a ,1/3, cap_jut, b, 1/3, .5cap_jut); % upper serif
        nodish_serif(2, 1, c, 1/3, cap_jut, d, 1/3, .5cap_jut); % lower serif
    fi
    pos8(bar_thickness, 90);
    pos9(bar_thickness, 90);
    top y8r = top y9r = vround(y5 + .8bar_thickness) + eps;
    if serifs:
        lft x8 = hround(tiny.lft x2l - max(cap_jut, cap_hair)) - eps;
        rt x9 = hround(tiny.rt x2r + max(cap_jut, cap_hair)) + eps;
    else:
        lft x8 = hround(tiny.lft x2l - .8vair) - eps;
        rt x9 = hround(tiny.rt x2r + .8vair) + eps;
    fi
    filldraw stroke z8e -- z9e;                               % bar
    penlabels(1, 2, 3, 4, 5, 6, 7, 8, 9);
endchar;

endinput;

% eof
