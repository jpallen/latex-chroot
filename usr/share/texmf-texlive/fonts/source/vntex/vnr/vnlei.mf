% Copyright 2003-2005 Han The Thanh <hanthethanh@gmx.net>.
% This file is part of vntex.  License: LPPL, version 1.3 or newer,
% according to http://www.latex-project.org/lppl.txt

% Computer Modern Roman Italic lower case "e" precomposed glyphs:
% Inclusion:
%               e'      e`      e?      e~      e.
%       e^      e^'     e^`     e^?     e^~     e^.

begin_letter(e);
    set_letter_dimens(e)(
        8u#, x_height#, 0,
        max(1/3x_height#*slant, x_height#*slant + .5(.2[hair#, stem#]) - u#),
        0, 0
    );
    pickup fine.nib;
    numeric heavy_hair;
    heavy_hair = hround .2[hair, stem];
    pos0(vair, -90);
    pos1(heavy_hair, 0);
    pos2(vair, 90);
    pos3(curve, 180);
    pos4(vair, 270);
    pos5(hair, 320);
    x0 = rt x3l;
    rt x1r = hround(w - 1.5u + .5heavy_hair);
    x2 = x4 = .5(w + u);
    lft x3r = hround(1.5u - .5curve);
    x5r = good.x(w - eps);
    x6 = x5;
    y0 = y3 = y6 = bar_height;
    y1 = .5[y0, y2];
    top y2r = h+oo;
    bot y4r = -oo;
    top y5l = vround .5bar_height;
    path p;
    p = z4{right} .. z5 .. z6;
    filldraw stroke z0e{right} ... z1e{up} ... pulled_arc.e(2, 3)
    & pulled_arc.e(3, 4) ... {direction 1 of p}z5e;  % arc
    math_fit( - .3x_height#*slant + .5curve# - u#, ic#);
    penlabels(0, 1, 2, 3, 4, 5, 6);
    set_letter_join(e, x2, .5w#);
%     set_letter_join(e.dot_, .5w + .5u, .5w# + .5u#);
end_letter;

input vnecomp;

endinput;
